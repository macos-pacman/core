# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-http-date
pkgver=6.05
pkgrel=6
pkgdesc="Date conversion routines"
arch=('any')
url="https://search.cpan.org/dist/HTTP-Date"
license=('PerlArtistic' 'GPL')
depends=('perl-timedate')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/HTTP-Date-$pkgver.tar.gz)
sha256sums=('365d6294dfbd37ebc51def8b65b81eb79b3934ecbc95a2ec2d4d827efe6a922b')

build() {
  cd HTTP-Date-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Date-$pkgver
  make test
}

package() {
  cd HTTP-Date-$pkgver
  make DESTDIR="$pkgdir" install
}

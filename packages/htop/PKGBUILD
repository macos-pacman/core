# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
_tag='4dbd0b5c87f4225495d6e0591a1d7227f120ead1' # git rev-parse ${pkgver}
pkgver=3.2.2
pkgrel=1
pkgdesc='Interactive process viewer'
arch=('x86_64' 'arm64')
url='https://htop.dev/'
license=('GPL')
depends=('ncurses' 'libncursesw.dylib')
makedepends=('git' 'autoconf')
options=('!emptydirs')
validpgpkeys=('F7ABE8761E6FE68638E6283AFE0842EE36DD8C0C'  # Nathan Scott <nathans@debian.org>
              '0D316B6ABE022C7798D0324BF1D35CB9E8E12EAD') # Benny Baumann <BenBE@geshi.org>
source=("git+https://github.com/htop-dev/htop.git#tag=${_tag}?signed")
sha256sums=('SKIP')

pacman_root=$(pacman-root-dir)


prepare() {
  cd "${pkgname}"

  autoreconf -fi
}


build() {
  cd "${pkgname}"

  ./configure \
      --prefix=${pacman_root}/usr \
      --sysconfdir=${pacman_root}/etc \
      --enable-unicode

  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install
}

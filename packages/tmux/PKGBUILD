# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>

pkgname=tmux
pkgver=3.3_a
pkgrel=7
pkgdesc='Terminal multiplexer'
url='https://github.com/tmux/tmux/wiki'
arch=('x86_64' 'arm64')
license=('BSD')
depends=('ncurses' 'libevent' 'libutf8proc')
source=("https://github.com/tmux/tmux/releases/download/${pkgver/_/}/tmux-${pkgver/_/}.tar.gz"
        '0001-CVE-2022-47016.patch'
        '0002-ncurses.patch')
sha256sums=('e4fd347843bd0772c4f48d6dde625b0b109b7a380ff15db21e97c11a4dcdf93f'
            '58fb690833e086b71b52555aa8eced695ace0a0bc51b8bccfda441e35a47ca93'
            '70e8cc2a95dd4a19ff54bcf0abaeff6464ebecbaace5ed0aec06ca93a84d415a')

prepare() {
	cd "$pkgname-${pkgver/_/}"

	patch -Np1 -i ../0001-CVE-2022-47016.patch
	patch -Np1 -i ../0002-ncurses.patch

	autoreconf -fi
}

build() {
	cd "$pkgname-${pkgver/_/}"

	./configure \
		--prefix=${pacman_root}/usr \
		--enable-utf8proc
	make
}

package() {
	cd "$pkgname-${pkgver/_/}"

	make install DESTDIR="$pkgdir"
	install -D -m0644 COPYING "$pkgdir/${pacman_root}/usr/share/licenses/tmux/LICENSE"
}

# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgbase=check
pkgname=(check)
pkgver=0.15.2
pkgrel=2
pkgdesc="A unit testing framework for C"
url="https://libcheck.github.io/check/"
arch=('x86_64' 'arm64')
license=(LGPL)
makedepends=(git awk cmake)
_commit=11970a7e112dfe243a2e68773f014687df2900e8  # tags/0.15.2
source=("git+https://github.com/libcheck/check#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd check
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd check
  autoreconf -fvi
}

build() {
  CFLAGS+=" -Wno-format-extra-args"

  cmake -Hcheck -Bcmake \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCHECK_ENABLE_TIMEOUT_TESTS=OFF \
    -DAWK_GSUB_DBL_BSLASH='\\\\'
  cmake --build cmake

  cd check
  ./configure --prefix=${pacman_root}/usr --disable-timeout-tests
  make all
}

check() {
  CTEST_OUTPUT_ON_FAILURE=1 cmake --build cmake --target test

  cd check
  make check
}

package_check() {
  depends=(awk)
  optdepends=('check-docs: Documentation')
  provides=(libcheck.dylib)

  DESTDIR="$pkgdir" cmake --build cmake --target install

  DESTDIR="$srcdir/tmp" make -C check install
  mv tmp/${pacman_root}/usr/share/{aclocal,info} "$pkgdir/${pacman_root}/usr/share"
}

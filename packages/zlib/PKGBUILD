# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.13
pkgrel=2
arch=('x86_64' 'arm64')
license=('custom')
url="https://www.zlib.net/"
depends=('glibc')
options=(
  'staticlibs'  # needed by binutils testsuite
)
source=("https://zlib.net/zlib-${pkgver}.tar.gz"{,.asc})
sha256sums=('b3a24de97a8fdbc835b9833169501030b8977031bcb54b3b3ac13740f846ab30'
            'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

pacman_root=$(pacman-root-dir)

prepare() {
	cd "${srcdir}/zlib-$pkgver"
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd "${srcdir}/zlib-$pkgver"

	CC=clang ./configure --prefix=${pacman_root}/usr
	make
}

check() {
	cd "${srcdir}/zlib-$pkgver"
	make test
}

package() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd "${srcdir}/zlib-$pkgver"
	make install DESTDIR="${pkgdir}"
	install -D -m644 LICENSE "${pkgdir}/${pacman_root}/usr/share/licenses/zlib/LICENSE"
}

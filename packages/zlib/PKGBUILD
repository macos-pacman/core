# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=(zlib minizip)
epoch=1
pkgver=1.3
pkgrel=1
arch=('x86_64' 'arm64')
license=('custom')
url="https://www.zlib.net/"
depends=('glibc')
options=(
  'staticlibs'  # needed by binutils testsuite
  'debug'
)
source=(https://github.com/madler/zlib/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('3868ac4da5842dd36c9dad794930675b9082ce15cbd099ddb79c0f6bd20a24aa8f33a123f378f26fe0ae02d91f31f2994dccaac565cedeaffed7b315e6ded2a2'
            'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
  cd $pkgbase-$pkgver/contrib/minizip
  cp Makefile Makefile.orig
  cp ../README.contrib readme.txt
  autoreconf -fiv
}

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=${pacman_root}/usr
  make

  cd contrib/minizip
  ./configure --prefix=${pacman_root}/usr --enable-static=no
  make
}

check() {
  make test -C $pkgbase-$pkgver

  make -f Makefile.orig test -C $pkgbase-$pkgver/contrib/minizip
}

package_zlib() {
  provides=(libz.dylib)
  pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

  make install DESTDIR="${pkgdir}" -C $pkgbase-$pkgver
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "${pkgdir}/${pacman_root}/usr/share/licenses/$pkgname/"
}

package_minizip() {
  pkgdesc='Mini zip and unzip based on zlib'
  depends=('zlib')

  make install DESTDIR="${pkgdir}" -C $pkgbase-$pkgver/contrib/$pkgname
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "${pkgdir}/${pacman_root}/usr/share/licenses/$pkgname/"

  # https://github.com/madler/zlib/pull/229
  rm "${pkgdir}/${pacman_root}/usr/include/minizip/crypt.h"
}

# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Daniel Kozak <kozzi11@gmail.com>

pkgname=(gcc gcc-runtime gcc-fortran gcc-objc lto-dump)
pkgver=14.2.0
_majorver=${pkgver%%.*}
_commit=04696df09633baf97cdbbdd6e9929b9d472161d3
pkgrel=1
pkgrel+=".1"
pkgdesc='The GNU Compiler Collection'
arch=('x86_64' 'arm64')
license=(GPL-3.0-with-GCC-exception GFDL-1.3-or-later)
url='https://gcc.gnu.org'
makedepends=(
  doxygen
  git
  libisl
  libmpc
  gmp
  mpfr
  python
  zstd
  'gnu-make>=4'
)
checkdepends=(
  dejagnu
  expect
  python-pytest
  tcl
)
options=(!emptydirs !lto)
_chost=${CHOST/arm64/aarch64}
_libdir=usr/lib/gcc/$_chost/${pkgver%%+*}

# sourceware is SOOO SLOWWWW
source=(git+https://github.com/gcc-mirror/gcc.git#commit=${_commit}
       "https://raw.githubusercontent.com/Homebrew/formula-patches/master/gcc/gcc-14.2.0-r2.diff"
       "sanitisers-undefined-dynamic-lookup.patch"
       "fix-libcxx-compile.patch::https://gcc.gnu.org/bugzilla/attachment.cgi?id=56010"
       'fix-libsanitizer-compile.patch')
validpgpkeys=(F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
              86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
              13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
              D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62) # Jakub Jelinek <jakub@redhat.com>
sha256sums=('SKIP'
            '6c0a4708f35ccf2275e6401197a491e3ad77f9f0f9ef5761860768fa6da14d3d'
            '8ebf1788bd0d0cbcecdd13d2c88e1cecf523fb5bb4d4c1663f146e97e4e02b00'
            '1ae00f85418c3b95bb957040b678d981ff2572075c17a617e32c8d8277a4f7aa'
            '34304ef7d8af4721f9e8eca494fd35c0f9b97a18c02f276850b3539c7787afc5')

prepare() {
  [[ ! -d gcc ]] && ln -s gcc-${pkgver/+/-} gcc
  cd gcc

  # Do not run fixincludes
  # sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # yes, we are patching a patch
  # cp ../gcc-13.1.0.diff ./gcc-13.1.0.patch
  # patch -Np1 -i ../sanitisers-undefined-dynamic-lookup.patch

  patch -Np1 -i ../gcc-14.2.0-r2.diff
  # patch -Np1 -i ../fix-libcxx-compile.patch
  patch -Np1 -i ../fix-libsanitizer-compile.patch

  # download dependencies
  # contrib/download_prerequisites

  mkdir -p "$srcdir/gcc-build"
}

build() {
  local _confflags=(
      --prefix=${pacman_root}/usr
      --libdir=${pacman_root}/usr/lib
      --libexecdir=${pacman_root}/usr/lib
      --mandir=${pacman_root}/usr/share/man
      --infodir=${pacman_root}/usr/share/info
      --with-sysroot=$(xcrun --show-sdk-path)
      # --with-build-config=bootstrap-lto
      --with-mpc=${pacman_root}/usr
      --with-gmp=${pacman_root}/usr
      --with-mpfr=${pacman_root}/usr
      --with-system-zlib
      --disable-libsanitizer  # disable sanitisers, they broken
      --enable-checking=release
      --disable-darwin-at-rpath
      --disable-multilib
      # --enable-lto
      --enable-plugin
      --disable-libstdcxx-pch
      --disable-werror
      --disable-nls
  )

  cd gcc-build

  # Credits @allanmcrae
  # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
  # TODO: properly deal with the build issues resulting from this
  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  CFLAGS=${CFLAGS/-march=native/}
  CXXFLAGS=${CXXFLAGS/-march=native/}

  CXXFLAGS="${CXXFLAGS} -Wno-mismatched-tags -Wno-unknown-warning-option"

  "$srcdir/gcc/configure" \
    --enable-languages=c,c++,fortran,lto,objc,obj-c++ \
    --enable-bootstrap \
    "${_confflags[@]:?_confflags unset}"

  find ../gcc/libsanitizer -name "Makefile.in" | xargs sed -i            \
    -e 's/@link_libasan@/@link_libasan@ -Wl,-undefined,dynamic_lookup/g' \
    -e 's/@link_liblsan@/@link_liblsan@ -Wl,-undefined,dynamic_lookup/g' \
    -e 's/@link_libtsan@/@link_libtsan@ -Wl,-undefined,dynamic_lookup/g' \
    -e 's/@link_libubsan@/@link_libubsan@ -Wl,-undefined,dynamic_lookup/g'

  # see https://bugs.archlinux.org/task/71777 for rationale re *FLAGS handling
  # note: -Wl,-undefined,dynamic_lookup is needed for sanitizers to build properly (apparently)
  gmake STAGE1_CFLAGS="-O2 -Wno-deprecated-declarations -Wno-unknown-warning-option" \
        STAGE1_CXXFLAGS="-O2 -Wno-deprecated-declarations -Wno-unknown-warning-option -Wno-mismatched-tags" \
          BOOT_CFLAGS="$CFLAGS -Wno-deprecated-declarations" \
          BOOT_CXXFLAGS="$CXXFLAGS -Wno-deprecated-declarations" \
          BOOT_LDFLAGS="$LDFLAGS" \
          LDFLAGS_FOR_TARGET="$LDFLAGS" \
          bootstrap

  # make documentation
  gmake -O -C $_chost/libstdc++-v3/doc doc-man-doxygen
}

check() {
  cd gcc-build

  # disable libphobos test to avoid segfaults
  sed -i '/maybe-check-target-libphobos \\/d' Makefile

  # do not abort on error as some are "expected"
  gmake -k check || true
  "$srcdir/gcc/contrib/test_summary"
}

package_gcc-runtime() {
  pkgdesc='Runtime libraries shipped by GCC'
  options=(!emptydirs !strip)
  provides=(libgfortran.dylib
    # libubsan.dylib
    # libasan.dylib
    # liblsan.dylib
  ) # no go, D, nor tsan

  cd gcc-build
  gmake -C ${_chost}/libgcc DESTDIR="$pkgdir" install-shared
  rm -f "$pkgdir/${pacman_root}/$_libdir/libgcc_eh.a"

  # note: no tsan
  for lib in libatomic \
             libgfortran \
             libgomp \
             libitm \
             libquadmath \
             libstdc++-v3/src; do
    gmake -C ${_chost}/$lib DESTDIR="$pkgdir" install-toolexeclibLTLIBRARIES
  done

  gmake -C ${_chost}/libobjc DESTDIR="$pkgdir" install-libs
  gmake -C ${_chost}/libstdc++-v3/po DESTDIR="$pkgdir" install

  # make -C ${_chost}/libphobos DESTDIR="$pkgdir" install
  # rm -rf "$pkgdir"/${pacman_root}/$_libdir/include/d/
  # rm -f "$pkgdir"/${pacman_root}/usr/lib/libgphobos.spec

  for lib in libgomp \
             libitm \
             libquadmath; do
    gmake -C ${_chost}/$lib DESTDIR="$pkgdir" install-info
  done

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc/COPYING.RUNTIME" \
    "$pkgdir/${pacman_root}/usr/share/licenses/gcc-runtime/RUNTIME.LIBRARY.EXCEPTION"
}

package_gcc() {
  pkgdesc="The GNU Compiler Collection - C and C++ frontends"
  depends=(libmpc zstd libisl.dylib)
  options=(!emptydirs staticlibs)

  cd gcc-build

  gmake -C gcc DESTDIR="$pkgdir" install-driver install-cpp install-gcc-ar \
    c++.install-common install-headers install-plugin install-lto-wrapper

  install -m755 -t "$pkgdir/${pacman_root}/usr/bin/" gcc/gcov{,-tool}
  install -m755 -t "$pkgdir/${pacman_root}/${_libdir}/" gcc/{cc1,cc1plus,collect2,lto1}

  gmake -C ${_chost}/libgcc DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/lib/libgcc_s.*.dylib

  gmake -C ${_chost}/libstdc++-v3/src DESTDIR="$pkgdir" install
  gmake -C ${_chost}/libstdc++-v3/include DESTDIR="$pkgdir" install
  gmake -C ${_chost}/libstdc++-v3/libsupc++ DESTDIR="$pkgdir" install
  gmake -C ${_chost}/libstdc++-v3/python DESTDIR="$pkgdir" install

  gmake DESTDIR="$pkgdir" install-libcc1
  install -d "$pkgdir/${pacman_root}/usr/share/gdb/auto-load/usr/lib"
  mv "$pkgdir"/${pacman_root}/usr/lib/libstdc++.*-gdb.py \
    "$pkgdir/${pacman_root}/usr/share/gdb/auto-load/usr/lib/"

  rm -f "$pkgdir"/${pacman_root}/usr/lib/libstdc++.{a,la,dylib}
  rm -f "$pkgdir"/${pacman_root}/usr/lib/libstdc++.*.{a,la,dylib}

  gmake DESTDIR="$pkgdir" install-fixincludes
  gmake -C gcc DESTDIR="$pkgdir" install-mkheaders

  # gmake -C lto-plugin DESTDIR="$pkgdir" install
  # install -dm755 "$pkgdir"/${pacman_root}/usr/lib/bfd-plugins/
  # ln -s /${pacman_root}/${_libdir}/liblto_plugin.dylib \
  #  "$pkgdir/${pacman_root}/usr/lib/bfd-plugins/"

  gmake -C ${_chost}/libgomp DESTDIR="$pkgdir" install-nodist_{libsubinclude,toolexeclib}HEADERS
  gmake -C ${_chost}/libitm DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  gmake -C ${_chost}/libquadmath DESTDIR="$pkgdir" install-nodist_libsubincludeHEADERS
  # gmake -C ${_chost}/libsanitizer DESTDIR="$pkgdir" install-nodist_{saninclude,toolexeclib}HEADERS
  # gmake -C ${_chost}/libsanitizer/asan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  # gmake -C ${_chost}/libsanitizer/lsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS

  # gcc doesn't support tsan on macos...
  # gmake -C ${_chost}/libsanitizer/tsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS

  gmake -C gcc DESTDIR="$pkgdir" install-man install-info
  rm -f "$pkgdir"/${pacman_root}/usr/share/man/man1/{gfortran,lto-dump}.1
  rm -f "$pkgdir"/${pacman_root}/usr/share/info/{gfortran}.info

  gmake -C libcpp DESTDIR="$pkgdir" install
  gmake -C gcc DESTDIR="$pkgdir" install-po

  # install the libstdc++ man pages
  gmake -C ${_chost}/libstdc++-v3/doc DESTDIR="$pkgdir" doc-install-man

  # byte-compile python libraries
  python -m compileall "$pkgdir/${pacman_root}/usr/share/gcc-${pkgver%%+*}/"
  python -O -m compileall "$pkgdir/${pacman_root}/usr/share/gcc-${pkgver%%+*}/"

  # Install Runtime Library Exception
  install -d "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
  ln -s /${pacman_root}/usr/share/licenses/gcc-runtime/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
}

package_gcc-fortran() {
  pkgdesc='Fortran front-end for GCC'
  depends=("gcc=$pkgver-$pkgrel" libisl.dylib)

  cd gcc-build
  gmake -C ${_chost}/libgfortran DESTDIR="$pkgdir" install-cafexeclibLTLIBRARIES \
    install-{toolexeclibDATA,nodist_fincludeHEADERS,gfor_cHEADERS}
  gmake -C ${_chost}/libgomp DESTDIR="$pkgdir" install-nodist_fincludeHEADERS
  gmake -C gcc DESTDIR="$pkgdir" fortran.install-{common,man,info}
  install -Dm755 gcc/f951 "$pkgdir/${pacman_root}/${_libdir}/f951"

  ln -s gfortran "$pkgdir/${pacman_root}/usr/bin/f95"

  # Install Runtime Library Exception
  install -d "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
  ln -s /${pacman_root}/usr/share/licenses/gcc-runtime/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
}

package_gcc-objc() {
  pkgdesc='Objective-C front-end for GCC'
  depends=("gcc=$pkgver-$pkgrel" libisl.dylib)

  cd gcc-build
  gmake DESTDIR="$pkgdir" -C ${_chost}/libobjc install-headers
  install -dm755 "$pkgdir/${pacman_root}/${_libdir}"
  install -m755 gcc/cc1obj{,plus} "$pkgdir/${pacman_root}/${_libdir}/"

  # Install Runtime Library Exception
  install -d "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
  ln -s /${pacman_root}/usr/share/licenses/gcc-runtime/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
}

package_lto-dump() {
  pkgdesc="Dump link time optimization object files"
  depends=("gcc=$pkgver-$pkgrel" libisl.dylib)

  cd gcc-build
  gmake -C gcc DESTDIR="$pkgdir" lto.install-{common,man,info}

  # Install Runtime Library Exception
  install -d "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
  ln -s /${pacman_root}/usr/share/licenses/gcc-runtime/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"
}


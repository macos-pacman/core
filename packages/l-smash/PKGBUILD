# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=2.14.5
pkgrel=3
pkgdesc='MP4 muxer and other tools'
arch=('x86_64' 'arm64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
provides=('liblsmash.dylib')
source=("l-smash-${pkgver}.tar.gz::https://github.com/l-smash/l-smash/archive/v${pkgver}.tar.gz"
        'fix-linker-flags.patch')
sha256sums=('e6f7c31de684f4b89ee27e5cd6262bf96f2a5b117ba938d2d606cf6220f05935'
            'aeed162e1752b97d9a05f2a6b298f93ca91d108520cc95ea4d9140307a3bf0db')

prepare() {
  cd l-smash-${pkgver}
  patch -Np1 -i ../fix-linker-flags.patch
}

build() {
  cd l-smash-${pkgver}

  ./configure \
    --cc=${CC} \
    --prefix=${pacman_root}/usr \
    --enable-shared \
    --disable-static \
    --extra-cflags="$CFLAGS" \
    --extra-ldflags="$LDFLAGS"
  make
}

package() {
  cd l-smash-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/${pacman_root}/usr/share/licenses/l-smash
  install -m 644 LICENSE "${pkgdir}"/${pacman_root}/usr/share/licenses/l-smash/

  local _f; for _f in ${pkgdir}/${pacman_root}/usr/bin/{boxdumper,remuxer,timelineeditor,muxer} ${pkgdir}/${pacman_root}/usr/lib/liblsmash.dylib; do
    fix_rpath "${_f}"
  done
}

# vim: ts=2 sw=2 et:

# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(gtkmm3 gtkmm3-docs)
pkgver=3.24.7
pkgrel=1
pkgdesc="C++ bindings for GTK+ 3"
url="https://www.gtkmm.org/"
arch=('x86_64' 'arm64')
license=(LGPL)
depends=(gtk3 pangomm atkmm)
makedepends=(git meson mm-common glibmm-docs cairomm-docs pangomm-docs
             atkmm-docs)
checkdepends=()
options=(!emptydirs)
_commit=d8bd947d97684bfd351552b962bdc79d04c0f24d  # tags/3.24.7^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  arch-meson gtkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package_gtkmm3() {
  depends+=(libsigc-2.0.dylib libglibmm-2.4.dylib libcairomm-1.0.dylib libpangomm-1.4.dylib
            libatkmm-1.6.dylib)
  provides=(libgtkmm-3.0.dylib)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/${pacman_root}/usr/share
  mv "$pkgdir"/${pacman_root}/usr/share/{devhelp,doc} docs/${pacman_root}/usr/share/
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir/"
}

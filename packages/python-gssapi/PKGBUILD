# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Samuel Laur√©n <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.8.2
pkgrel=2
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=('x86_64' 'arm64')
license=('ISC')
depends=('python' 'python-decorator' 'krb5')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-parameterized' 'python-k5test')
source=("https://github.com/pythongssapi/python-gssapi/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'disable-macos-framework.patch')
sha512sums=('b3003351aba19553031a1a4343b8c1b93474c9464e166f3a53d1b411b70378363229594f3a1fdee7448aa9981475b654d0996b8b75baa0411dd57fc8740720f4'
            '85f0b4903e6f4415f820c2bc9b31c1b51d77a165810e72ee464f8fab1b84d56b6fabd1be788218d202b5e2d16be21a88059baf5a256674372c59ca279bb08a92')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../disable-macos-framework.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test || :
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.txt -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

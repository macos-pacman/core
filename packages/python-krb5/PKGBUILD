# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=krb5
pkgname=python-${_pkg}
pkgver=0.5.0
pkgrel=2
pkgdesc="krb5 API interface"
arch=('x86_64' 'arm64')
url="https://github.com/jborean93/pykrb5"
license=(MIT)
depends=(python krb5)
makedepends=(cython python-setuptools)
checkdepends=(python-pytest python-k5test)
# No tests in pypi tarball
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        'disable-macos-framework.patch')
sha256sums=('9660773e64a205e59c61272ee7783b58f4617129e23a7c313b4e2210bf98f823'
            '0e86486f95140ef8d08b62b6e531471211c8b747f792924eeabe4d0890a1b48e')

prepare() {
  cd py${_pkg}-$pkgver
  patch -Np1 -i ../disable-macos-framework.patch
}

build() {
  cd py${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd py${_pkg}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="${PWD}"/build/$(_GetPythonPlatformPath) \
    pytest -vv --color=yes -k 'not test_cc_supports_switch'
}

package() {
  cd py${_pkg}-${pkgver}
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}
}

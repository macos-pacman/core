# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=brass
pkgname=ruby-$_gemname
pkgver=1.2.1
pkgrel=1
pkgdesc="Bare-Metal Ruby Assertions System Standard"
arch=(any)
url='https://github.com/rubyworks/'${_gemname}
license=(BSD-2)
depends=(ruby)
checkdepends=(ruby-lemon ruby-rubytest-cli ruby-simplecov)
options=(!emptydirs)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('8f3f97e35014753155ccbc91c64543012628cdfebb0593550e46abb78973ee8c')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' ".gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' ".gemspec"

  rm -fv Gemfile Gemfile.lock
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build .gemspec
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    brass-$pkgver.gem

  GEM_HOME="tmp_install/$_gemdir" rubytest test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --local --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 *.md -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING* -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

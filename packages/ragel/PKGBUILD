# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Will Chappell <mr.chapendi@gmail.com>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=ragel
pkgver=6.10
pkgrel=3
pkgdesc="Compiles finite state machines from regular languages into executable C, C++, Objective-C, or D code."
arch=('x86_64' 'arm64')
url="http://www.complang.org/ragel/"
license=('GPL')
depends=('gcc-libs')
source=(https://www.colm.net/files/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=(C3260F001EE3780F1BC3D4F650FE47277DC196FB)
sha256sums=('5f156edb65d20b856d638dd9ee2dfb43285914d9aa2b6ec779dac0270cd56c3f'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=${pacman_root}/usr CXXFLAGS="$CXXFLAGS -std=gnu++98"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -m0644 -D ragel.vim "$pkgdir"/${pacman_root}/usr/share/vim/vimfiles/syntax/ragel.vim
}

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-macextras
_basever=5.15.9
pkgver=5.15.9
pkgrel=1
_commit=209e3ddcf0a6b48ff47a7dc97f2ea38470c8780d
arch=('x86_64' 'arm64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=('qt5-declarative' 'assimp')
makedepends=('git')
groups=('qt' 'qt5')
_pkgfqn=${pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/${pacman_root}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/${pacman_root}/usr/share/licenses
  ln -s ${pacman_root}/usr/share/licenses/qt5-base "$pkgdir"/${pacman_root}/usr/share/licenses/${pkgname}

  local _f; for _f in $(find ${pkgdir}/${pacman_root}/usr/lib/qt/ -name "*.dylib"); do
    fix_rpath "${_f}"
  done
}

# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db5.3
pkgver=5.3.28
pkgrel=3
pkgdesc="The Berkeley DB embedded database system v5.3"
arch=('x86_64' 'arm64')
url='https://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=(custom:sleepycat)
depends=(gcc-libs sh)
source=(
  https://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz
  db-5.3.21-memp_stat-upstream-fix.patch
  db-5.3.21-mutex_leak.patch
  db-5.3.28-lemon_hash.patch
  db-5.3.28_cve-2019-2708.patch
  db-5.3.28-mmap-high-cpu-usage.patch
  db-5.3.28-atomic_defines.patch
  fix-store-define.patch
  fix-tls-configure.patch
)
b2sums=('5b77a4ec0945fa96ce6c2b452fb4b029295e037fa64d5ff8dcb3b5e01ee3501fd75d9a73b8f5fa54d8907717d93546fbb5348ee70119e877fa2f17d301ecf50d'
        'cfbb212c60c0e75c0d8c1cdbee9bcf0be9aec718ab462f779d5b880a4f976de1423279adde20159eef067be95d0d725eaa0cb950d0a8aaae720de78b94299e72'
        'ba917cf9b69de39337b7141bf145153ae89849921c79b4d2f40734feefe2c0007abcc8c85ed776227875c5102354d8f61069fcee12c7db3b45c4ccabc6993760'
        '682539a382f4423ad4404ad2aa446356d97485a7883a26d31cc2061611b138f20e39d941c575b8997538c4dd609e3e0343505759bba3b89c669ad1830cac1fbf'
        'bc72ba9703cdbabbece3480fd36c22fca26f270cc4b8c66f8f19799b5120010dfdf53772a94374e45d38b25bb8a271d29ea84318aa8bc847a88940d52ee22a8f'
        'bc50b556653a4e33073ed4cbaf716035f0840af2659bb4d6ef900621f92515d9490a698cec70546bd3aededa02b264fcd6e6cba3c4e6412125d2d587094f872d'
        'cd2c6bb39a42b8093022efbdae7d85427f63784e242eac4fbfd2e56264f65c84f3f3a9a75c65b54e516b66c0304bb0318871b1fca40f1a2caf33e08da0968392'
        '75ad485db2cb0a03364ca8efe3d47f7334b0b62564254d2e113c91e4429b6935419b3e29c7e44aa7a52680a998d3576bdd0fd07222c3e307cecf092ba84dd957'
        '36214292f9123cb18e23d64dd4ecd07c5e8ef466f7d66e7801a69e96a4535e9fcf57ceda688b1d005a39b606eae72e94a15427bd9fe971e61268c186a252c0af')

# All Patches retrieved from Fedora
prepare() {
  cd db-${pkgver}
  # memp_stat fix provided by upstream
  patch -Np1 -i ../db-5.3.21-memp_stat-upstream-fix.patch
  # fix for mutexes not being released provided by upstream
  patch -Np1 -i ../db-5.3.21-mutex_leak.patch
  # fix for overflowing hash variable inside bundled lemon
  patch -Np1 -i ../db-5.3.28-lemon_hash.patch
  # cve-2019-2708 fixed by mmuzila
  patch -Np1 -i ../db-5.3.28_cve-2019-2708.patch
  # Prevents high CPU usage
  patch -Np1 -i ../db-5.3.28-mmap-high-cpu-usage.patch
  # gcc fix
  patch -Np1 -i ../db-5.3.28-atomic_defines.patch

  patch -Np1 -i ../fix-store-define.patch
  patch -Np1 -i ../fix-tls-configure.patch
}


build() {
  cd db-${pkgver}/build_unix
  ../dist/configure \
    --prefix=${pacman_root}/usr \
    --bindir=${pacman_root}/usr/bin/db5.3 \
    --includedir=${pacman_root}/usr/include/db5.3 \
    --enable-compat185 \
    --enable-shared \
    --enable-static \
    --enable-cxx \
    --enable-dbm \
    --enable-stl
  make LIBSO_LIBS=-lpthread
}

package() {
  make -C db-${pkgver}/build_unix DESTDIR="${pkgdir}" install
  install -Dm644 db-${pkgver}/LICENSE "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE
  rm "${pkgdir}"/${pacman_root}/usr/lib/libdb.dylib
  rm "${pkgdir}"/${pacman_root}/usr/lib/libdb_cxx.dylib
  rm "${pkgdir}"/${pacman_root}/usr/lib/libdb_stl.dylib
  rm -r "${pkgdir}"/${pacman_root}/usr/docs
}

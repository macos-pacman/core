# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jakub Klinkovsk√Ω <j.l.k@gmx.com>
# Contributor: Matheus de Alcantara <matheus.de.alcantara@gmail.com>

pkgname=mandoc
pkgver=1.14.6
pkgrel=2
pkgrel+=".2"
pkgdesc='A suite of tools compiling mdoc from the OpenBSD project'
arch=('x86_64' 'arm64')
url='https://mdocml.bsd.lv'
license=('ISC')
depends=('zlib')
provides=('man')
conflicts=('man')
source=("${url}/snapshots/${pkgname}-${pkgver}.tar.gz"
        'configure.local')
sha256sums=('8bf0d570f01e70a6e124884088870cbed7537f36328d512909eb10cd53179d9c'
            '3c777cc1b52a4f8a268e6f96f45081404342577b9427473885f8b7965b933ee5')

prepare() {
    cd ${pkgname}-${pkgver}
    sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../configure.local > configure.local
}

build() {
    cd ${pkgname}-${pkgver}

    # apply CFLAGS and LDFLAGS from makepkg.conf
    {
        echo "CFLAGS=${CFLAGS@Q}"
        echo "LDFLAGS=${LDFLAGS@Q}"
    } >> configure.local

    ./configure --prefix=${pacman_root}/usr
    make
}

package() {
    cd ${pkgname}-${pkgver}

    DESTDIR="${pkgdir}" make install
    install -Dm644 LICENSE "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE
}

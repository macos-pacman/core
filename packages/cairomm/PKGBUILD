# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgbase=cairomm
pkgname=(cairomm cairomm-docs)
pkgver=1.14.4
pkgrel=1
pkgdesc="C++ bindings for Cairo"
url="https://www.cairographics.org/cairomm/"
arch=('x86_64' 'arm64')
license=(LGPL MPL)
depends=(cairo libsigc++)
makedepends=(git meson mm-common)
options=(!emptydirs)
_commit=b6b7e0ba65d8362c2f13b17570302015ae7222b4  # tags/1.14.4^0
source=("git+https://gitlab.freedesktop.org/cairo/cairomm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd cairomm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd cairomm
}

build() {
  arch-meson cairomm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_cairomm() {
  depends+=(libsigc-2.0.dylib)
  provides=(libcairomm-1.0.dylib)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/${pacman_root}/usr/share
  mv "$pkgdir"/${pacman_root}/usr/share/{devhelp,doc} docs/${pacman_root}/usr/share/
}

package_cairomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir/"
}

# vim:set sw=2 sts=-1 et:

# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=poppler
pkgname=('poppler' 'poppler-glib')
pkgver=23.05.0
pkgrel=1
arch=('x86_64' 'arm64')
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'pkgconfig' 'lcms2'
             'gobject-introspection' 'icu' 'git' 'nss' 'gtk-doc' 'curl' 'poppler-data'
             'cmake' 'python' 'boost')
options=('!emptydirs')
url="https://poppler.freedesktop.org/"
# https://gitlab.freedesktop.org/poppler/poppler
source=(https://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz{,.sig}
        test::git+https://anongit.freedesktop.org/git/poppler/test/#commit=ae4c5f64986aeaa6701bb65235eccd99243c76bc)
sha256sums=('38294de7149ebe458191a6e6d0e2837da7dba8683900a635252f6d0ee235f990'
            'SKIP'
            'SKIP')
validpgpkeys=('CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7') # "Albert Astals Cid <aacid@kde.org>"

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_INSTALL_LIBDIR=${pacman_root}/usr/lib \
    -DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
    -DWITH_GObjectIntrospection=ON \
    -DENABLE_CMS=lcms2 \
    -DENABLE_GTK_DOC=ON \
    -DENABLE_QT5=OFF \
    -DENABLE_QT6=OFF
  make
}

check() {
  cd build
  LANG=en_US.UTF8 make test
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'lcms2' 'nss' 'curl')
  optdepends=('poppler-data: highly recommended encoding data to display PDF documents with certain encodings and characters')
  provides=('libpoppler.dylib' 'libpoppler-cpp.dylib')

  cd build
  make DESTDIR="${pkgdir}" install

  # cleanup for split build
  rm -vrf "${pkgdir}"/${pacman_root}/usr/include/poppler/{glib,qt5,qt6}
  rm -vf "${pkgdir}"/${pacman_root}/usr/lib/libpoppler-{glib,qt5,qt6}.*
  rm -vf "${pkgdir}"/${pacman_root}/usr/lib/pkgconfig/poppler-{glib,qt5,qt6}.pc
  rm -vrf "${pkgdir}"/${pacman_root}/usr/{lib,share}/gir*
  rm -vrf "${pkgdir}"/${pacman_root}/usr/share/gtk-doc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')
  provides=('libpoppler-glib.dylib')

  cd build

  make -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/${pacman_root}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/${pacman_root}/usr/lib/pkgconfig/"
  rm -vf "${pkgdir}"/${pacman_root}/usr/lib/libpoppler.*
  rm -vf "${pkgdir}/${pacman_root}/usr/bin/poppler-glib-demo"
}


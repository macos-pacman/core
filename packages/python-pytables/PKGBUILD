# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=python-pytables
pkgver=3.7.0
pkgrel=5
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
arch=('x86_64' 'arm64')
url="https://www.pytables.org"
license=('BSD')
depends=(blosc lzo hdf5 python-numexpr)
makedepends=(python-setuptools cython)
source=(https://github.com/PyTables/PyTables/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('41065fc11b958dde09bd5b9c069d88e40ca07ad10687dd597835fcc8199e81ea')

build() {
  cd PyTables-${pkgver}
  python setup.py build --blosc=${pacman_root}/usr --lzo=${pacman_root}/usr --hdf5=${pacman_root}/usr --bzip2=${pacman_root}/usr
}

check() {
  cd PyTables-${pkgver}/build/$(_GetPythonPlatformPath)
  PYTHONPATH="." python -m tables.tests.test_all
}

package() {
  cd PyTables-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/
}

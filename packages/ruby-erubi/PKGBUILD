# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='erubi'
pkgname="ruby-${_gemname}"
pkgver=1.12.0
pkgrel=1
pkgdesc='Small ERB Implementation'
arch=('any')
url="https://github.com/jeremyevans/erubi"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rdoc')
checkdepends=('ruby-minitest' 'ruby-minitest-global_expectations')
options=(!emptydirs)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6243b6e1c4360ceb1eb4bc3ce4492cebcf071e526be43f7393a789e4b0eaf43b6ed770b5baec7cbab4b6a563d4dc821e56a8e4222ad7a13a852d74dcb7c5552a')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/${pacman_root}/usr/bin" "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm -frv \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.bundle" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install -Dm 644 MIT-LICENSE "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 CHANGELOG README.rdoc --target-directory "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
}

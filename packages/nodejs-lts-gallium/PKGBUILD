# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=nodejs-lts-gallium
pkgver=16.20.0
pkgrel=1
pkgdesc="Evented I/O for V8 javascript (LTS release: Gallium)"
arch=('x86_64' 'arm64')
url="https://nodejs.org/"
license=(MIT)
depends=(openssl zlib icu libuv c-ares brotli libnghttp2) # http-parser
makedepends=(python)
optdepends=('npm: nodejs package manager')
options=(!lto)
provides=("nodejs=$pkgver")
conflicts=(nodejs)
source=(${url}/dist/v${pkgver}/node-v${pkgver}.tar.xz
        fix-test-socket-write-after-fin-error.patch
        'skip-ssl-tests.patch')
# https://nodejs.org/download/release/latest-gallium/SHASUMS256.txt.asc
sha256sums=('e0990f992234e40a51fe11f92c3816c93a77e1b081145d3dd762cd1026345349'
            'bb4df6373e4ab93478089268949934efbdd7bb060f35e8d7262af6b2dda4b948'
            '239553deeaf8aa100fd127869b91c50827e211824a967f690450f097192c21a5')
validpgpkeys=(C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8  # Myles Borins <mylesborins@google.com>
              77984A986EBC2AA786BC0F66B01FBB92821C587A  # Gibson Fahnestock <gibfahn@gmail.com>
              B9AE9905FFD7803F25714661B63B535A4C206CA9  # Evan Lucas <evanlucas@me.com>
              DD8F2338BAE7501E3DD5AC78C273792F7D83545D  # Rod Vagg <rod@vagg.org>
              B9E2F5981AA6E0CD28160D9FF13993A75599653C  # Shelley Vohr <shelley.vohr@gmail.com>
              4ED778F539E3634C779C87C6D7062848A1AB005C  # Beth Griggs <Bethany.Griggs@uk.ibm.com>
              C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C  # Richard Lau <rlau@redhat.com>
              74F12602B6F1C4E913FAA37AD3A89613643B6201  # Danielle Adams <adamzdanielle@gmail.com>
              61FC681DFB92A079F1685E77973F295594EC4689) # Juan Jos√© Arboleda <soyjuanarbol@gmail.com>

prepare() {
  cd node-v${pkgver}
  patch -Np1 -i ../fix-test-socket-write-after-fin-error.patch
  patch -Np1 -i ../skip-ssl-tests.patch
}

build() {
  cd node-v${pkgver}

  ./configure \
    --prefix=${pacman_root}/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-cares \
    --shared-brotli \
    --shared-nghttp2
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-v${pkgver}
  make test-only CI_SKIP_TESTS=test-vm-timeout-escape-nexttick,test-tls-no-sslv3,test-tls-session-cache,test-tls-alert,test-tls-streamwrap-buffersize,test-tls-min-max-version,test-tls-getprotocol,test-tls-cli-min-version-1.3,test-tls-cli-min-version-1.2,test-tls-cli-min-version-1.1,test-tls-cli-max-version-1.3,test-tls-cli-max-version-1.2,test-https-agent-session-eviction
}

package() {
  cd node-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/
}

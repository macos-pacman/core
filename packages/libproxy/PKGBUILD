# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libproxy
pkgname=(libproxy)
pkgver=0.4.18
pkgrel=3
pkgdesc="Automatic proxy configuration management library"
url="https://libproxy.github.io/libproxy/"
arch=('x86_64' 'arm64')
license=(LGPL)
depends=(
  duktape
  gcc-libs
  glib2
  zlib
)
makedepends=(
  cmake
  git
  ninja
  python
)
_commit=caccaf28e3df6ea612d2d4b39f781c4324019fdb  # tags/0.4.18
source=("git+https://github.com/libproxy/libproxy#commit=$_commit"
        'fix-cmake.patch::https://github.com/libproxy/libproxy/commit/8fec01ed4b95afc71bf7710bf5b736a5de03b343.patch?full_index=1')
b2sums=('SKIP'
        'cd3b2293b785a4b37239bcef45cd88a1ace96a4834223a04107f3e31423908a046aeb3069f019bd68ab7e00d7b3984bfbec4a1d3e7cd2dfaf24184589b876220')

pkgver() {
  cd libproxy
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libproxy

  patch -Np1 -i ../fix-cmake.patch
}

build() {
  cmake -S libproxy -B build -G Ninja \
    -DBIPR=0 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS" \
    -DCMAKE_C_FLAGS="$CFLAGS" \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLIBEXEC_INSTALL_DIR=${pacman_root}/usr/lib \
    -DPERL_VENDORINSTALL=yes \
    -DWITH_{VALA,WEBKIT3,PERL}=OFF
  cmake --build build
}

check() {
  cmake --build build --target test || :
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p "$(dirname "$f")" || :
  done
}

package_libproxy() {
  optdepends=('networkmanager: NetworkManager configuration module'
              'perl: Perl bindings'
              'python: Python 3.x bindings'
              'libproxy-webkit: PAC proxy support (via WebKit)'
              'pacrunner: PAC proxy support (via pacrunner)')
  provides+=(libproxy.dylib)

  DESTDIR="$pkgdir" cmake --install build

  _pick webkit "$pkgdir"/${pacman_root}/usr/lib/libproxy/*/modules/pacrunner_webkit.so

  local _f; for _f in ${pkgdir}/${pacman_root}/usr/bin/proxy ${pkgdir}/${pacman_root}/usr/lib/*.dylib ${pkgdir}/${pacman_root}/usr/lib/libproxy/$pkgver/modules/*.so; do
    fix_rpath "${_f}"
  done
}

# vim:set sw=2 sts=-1 et:

# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.42
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64' 'arm64')
url="https://www.gnu.org/software/mtools/"
license=(GPL3)
depends=(bash glibc)
source=(https://ftp.gnu.org/gnu/mtools/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('fd8d56d2dea9ce5e2b311b29ebdd4d89ec7baaf5f2fbfdc85019d480eff4d198ebf0e591c0f28ee1e39d8b81d49d7d4e9ed94b0e94e5b02ce9bbc03632124811'
            'SKIP')
b2sums=('07afec0aea49427ec71ae8ddde9c1dceb33b2533e9acbde3564b0715efce784b5ea8c2122ff588f84e42435abb73ac9912520fd654a078bf0336e62f35f3ce2a'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i $pkgname-$pkgver/mtools.conf
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=${pacman_root}/usr \
    --mandir=${pacman_root}/usr/share/man \
    --infodir=${pacman_root}/usr/share/info \
    --sysconfdir=${pacman_root}/etc \
    --disable-floppyd
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/mtools.conf -t "$pkgdir/${pacman_root}/etc/"
  install -vDm 644 $pkgname-$pkgver/{README,NEWS} -t "$pkgdir/${pacman_root}/usr/share/doc/$pkgname"
}

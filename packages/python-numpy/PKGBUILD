# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=python-numpy
pkgver=2.2.3
pkgrel=1
pkgrel+=".2"
pkgdesc="Scientific tools for Python"
arch=('x86_64' 'arm64')
license=('custom')
url="https://www.numpy.org/"
depends=('python')
makedepends=('python-build' 'python-installer' 'meson-python' 'cmake' 'cython' 'pkgconf')
checkdepends=('python-pytest' 'python-hypothesis')
source=("https://github.com/numpy/numpy/releases/download/v$pkgver/numpy-$pkgver.tar.gz")
sha512sums=('78c876d8ecc13aa0f802460f6c1462b53162dfdf85c658e32f1cf9823eeef829b6f1af8285a6c0c75719f5a2e05ff8b987dab88a416df6a8371c16fd032f5e6a')

build() {
  cd numpy-$pkgver

  # numpy specifically wants >= 13.3 to be able to use accelerate.
  export MACOSX_DEPLOYMENT_TARGET="${MACOS_MAJOR}.${MACOS_MINOR}"
  python -m build --wheel --no-isolation \
    -Csetup-args="-Dblas-order=accelerate" \
    -Csetup-args="-Dlapack-order=accelerate" \
    -Csetup-args="-Dallow-noblas=false" \
    -Cbuilddir=build
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd numpy-$pkgver
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  cd "$PWD/tmp_install"
  PATH="$PWD/${pacman_root}/usr/bin:$PATH" PYTHONPATH="$PWD/$site_packages:$PYTHONPATH" python -c 'import numpy; numpy.test()'
}

package() {
  cd numpy-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname
  ln -s "$site_packages"/numpy-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE.txt
}

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

pkgname=python-numpy
pkgver=1.24.3
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64' 'arm64')
license=('custom')
url="https://www.numpy.org/"
depends=('cblas' 'lapack' 'python')
optdepends=('openblas: faster linear algebra')
makedepends=('python-setuptools' 'gcc-fortran' 'cython')
checkdepends=('python-pytest' 'python-hypothesis')
options=('staticlibs')
source=("https://github.com/numpy/numpy/releases/download/v$pkgver/numpy-$pkgver.tar.gz"
        "site.cfg"
        "fix-macos-f2py.patch"
        "fix-macos-threads.patch")
sha512sums=('d4b5841cbe68ee6653f99018e4e0182f2e5519a1cd69c14e0a95aa180f878e6a6786ccf0a7c78bf0d511c53e7535d7144fee428572dec7a4b60b25c8a1c0c9e1'
            'f0f1e45e83bbccc5c62a1f3bccb01212861b0c0cde395e4f1a80db8fcbd97e6415b85cbc555d5d314343b2bb62c49ce57889852f37971f1642157201bfbadd57'
            'f18afbd9a6f3b3d5526efd1d4b7f59931cc93903e2ff48e6d39cf74621323c2bee46d4e1f08a4a925dc4b470218136dbe8ff127b5999a62e21668158a42ea02a'
            '56acd5f052c79baa0f5b30baf309c12ababc680f83f844a201f8de6b99ce36445d077d82af8b3a690ad3c496105a547b0d9f194c451baa3b65a9546aa1cc9d0c')

prepare() {
	cd numpy-$pkgver
	cp ../site.cfg ./

	patch -Np1 -i ../fix-macos-f2py.patch
	patch -Np1 -i ../fix-macos-threads.patch
}

export CC=gcc
export CXX=g++
export FC=gfortran

export BLAS="openblas"
export LAPACK="openblas"
export ATLAS="None"

export NPY_BLAS_LIBS="-lopenblas"
export NPY_CBLAS_LIBS="-lopenblas"
export NPY_LAPACK_LIBS="-lopenblas"

build() {
  cd numpy-$pkgver
  python setup.py build --parallel=4 --fcompiler=gfortran
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd numpy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  cd "$PWD/tmp_install"
  PATH="$PWD/${pacman_root}/usr/bin:$PATH" PYTHONPATH="$PWD/$site_packages:$PYTHONPATH" python -c 'import numpy; numpy.test(verbose=2)'
}

package() {
  cd numpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 LICENSE.txt -t "$pkgdir"/${pacman_root}/usr/share/licenses/python-numpy/
}

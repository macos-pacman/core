pkgname=cabal-bootstrap
pkgver=3.10.1.0
pkgrel=1

# GHC version used to bootstrap cabal via the ./bootstrap.py script.
_bootstrapver=9.6.1
pkgdesc="Cabal version used for bootstrapping"
url="https://haskell.org/cabal"
arch=('x86_64' 'arm64')
license=("BSD-3-Clause")
provides=("cabal" "cabal-install")
depends=("gmp" "zlib")
makedepends=("ghc=$_bootstrapver" "gmp" "libffi" "zlib" "python")
source=("https://github.com/haskell/cabal/archive/refs/tags/cabal-install-v$pkgver.tar.gz"
        "https://github.com/zhiayang/pacman-macos/releases/download/cabal-bootstrap-${_bootstrapver}/bootstrap-sources.tar.gz")
sha512sums=(
'79c839b53d9848a265d063b363542ae96bf1000dab8f00802631eba165fec134e8803299339c5bd14ee7d8e3be10d27e1f406ab8bdad0b4e72c39e6e5a5e925c'
'f15640fac045959ac273e3e6903c659585bf441fc94ab1175c4fad5ca2d712d0964d8504cf77cca2aa6f3edde7a3fdd484855f111cd99607a3c1dcf213e4be1f'
)
# don't extract the bootstrap sources
noextract=(bootstrap-sources.tar.gz)

prepare() {
  cd cabal-cabal-install-v$pkgver

  # bootstrapping with ghc 9.6.1 isn't officially supported (because the haskell people... are weird)
  # we already "hacked" the bootstrap tarball by making it with `--allow-newer=base`, so just hack the
  # actual dependencies a little bit more. what's the harm?
  sed -i.bak -e 's/<4.18/<4.19/g' \
    cabal-install/cabal-install.cabal \
    cabal-testsuite/cabal-testsuite.cabal \
    cabal-install-solver/cabal-install-solver.cabal
}

build() {
  cd cabal-cabal-install-v$pkgver
  ./bootstrap/bootstrap.py \
    -w "$(command -v ghc)" \
    --bootstrap-sources "$srcdir/bootstrap-sources.tar.gz" \
    --no-archive
}

package() {
  cd cabal-cabal-install-v$pkgver

  install -m 755 -D _build/bin/cabal "$pkgdir/${pacman_root}/usr/bin/cabal"
  install -Dm644 LICENSE "$pkgdir/${pacman_root}/usr/share/licenses/cabal/LICENSE"
}


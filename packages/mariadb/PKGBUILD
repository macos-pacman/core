# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=mariadb
pkgname=('mariadb-libs' 'mariadb-clients' 'mariadb' 'mytop')
pkgdesc='Fast SQL database server, derived from MySQL'
pkgver=10.11.3
pkgrel=1
arch=('x86_64' 'arm64')
license=('GPL')
url='https://mariadb.org/'
makedepends=('boost' 'bzip2' 'cmake' 'cracklib' 'curl' 'jemalloc' 'judy' 'krb5'
             'libxcrypt' 'libxml2' 'lz4' 'openssl' 'zlib' 'zstd' 'xz')
validpgpkeys=('177F4010FE56CA3336300305F1656F24C74CD1D8') # MariaDB Signing Key <signing-key@mariadb.org>
# The default links with mirror redirection fail for signatures, specific
# mirrors may be out of date every now and then. Let's use the upstream
# rsync source via https and hope it does not hurt them too much.
# https://mariadb.com/kb/en/library/mirror-sites-for-mariadb/
source=("https://rsync.osuosl.org/pub/mariadb/mariadb-${pkgver}/source/mariadb-${pkgver}.tar.gz"{,.asc})
sha256sums=('b065b0f32a6e9fd479e566fd6671450379886d8dda2d9a7ef930af1e5c26c0c7'
            'SKIP')

prepare() {
  cd $pkgbase-$pkgver/
}

build() {
  local _cmake_options=(
    # build options
    -DCMAKE_BUILD_TYPE=Release
    -Wno-dev

    # file paths
    # /etc
    -DINSTALL_SYSCONFDIR=${pacman_root}/etc
    -DINSTALL_SYSCONF2DIR=${pacman_root}/etc/my.cnf.d
    # /run
    -DINSTALL_UNIX_ADDRDIR=${pacman_root}/run/mysqld/mysqld.sock
    # /usr
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr
    # /usr/bin /usr/include
    -DINSTALL_SCRIPTDIR=bin
    -DINSTALL_INCLUDEDIR=include/mysql
    # /usr/lib
    -DINSTALL_PLUGINDIR=lib/mysql/plugin
    # /usr/share
    -DINSTALL_SHAREDIR=share
    -DINSTALL_SUPPORTFILESDIR=share/mysql
    -DINSTALL_MYSQLSHAREDIR=share/mysql
    -DINSTALL_DOCREADMEDIR=share/doc/mariadb
    -DINSTALL_DOCDIR=share/doc/mariadb
    -DINSTALL_MANDIR=share/man
    # /var
    -DMYSQL_DATADIR=${pacman_root}/var/lib/mysql

    # default settings
    -DDEFAULT_CHARSET=utf8mb4
    -DDEFAULT_COLLATION=utf8mb4_unicode_ci

    # features
    -DENABLED_LOCAL_INFILE=ON
    -DPLUGIN_EXAMPLE=NO
    -DPLUGIN_FEDERATED=NO
    -DPLUGIN_FEEDBACK=NO
    -DWITH_EMBEDDED_SERVER=ON
    -DWITH_EXTRA_CHARSETS=complex
    -DWITH_JEMALLOC=ON
    -DWITH_LIBWRAP=OFF
    -DWITH_PCRE=bundled
    -DWITH_READLINE=ON
    -DWITH_SSL=system
    -DWITH_UNIT_TESTS=OFF
    -DWITH_ZLIB=system
  )

  mkdir build
  cd build

  cmake ../"$pkgbase-$pkgver" "${_cmake_options[@]}"

  make
}

check() {
  cd build/mysql-test

  # Takes *really* long, so disabled by default.
  #./mtr --parallel=5 --mem --force --max-test-fail=0
}

package_mariadb-libs() {
  pkgdesc='MariaDB libraries'
  depends=('libxcrypt' 'libcrypt.dylib' 'openssl' 'zlib' 'zstd')
  optdepends=('krb5: for gssapi authentication')
  conflicts=('libmysqlclient' 'libmariadbclient' 'mariadb-connector-c')
  provides=('libmariadbclient' 'mariadb-connector-c' 'libmariadb.dylib' 'libmariadbd.dylib')
  replaces=('libmariadbclient')

  cd build

  for dir in libmariadb libmysqld libservices include; do
    make -C "$dir" DESTDIR="$pkgdir" install
  done

  # remove static libraries
  rm "${pkgdir}"/${pacman_root}/usr/lib/*.a

  # remove man pages
  rm -r "${pkgdir}"/${pacman_root}/usr/share/man

  ln -s mariadb_config "$pkgdir"/${pacman_root}/usr/bin/mariadb-config
  ln -s mariadb_config "$pkgdir"/${pacman_root}/usr/bin/mysql_config
  install -D -m0644 "$srcdir"/"$pkgbase-$pkgver"/man/mysql_config.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/mysql_config.1
  ln -s mysql_config.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/mariadb_config.1
  ln -s mysql_config.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/mariadb-config.1

  install -D -m0644 support-files/mariadb.pc "$pkgdir"/${pacman_root}/usr/share/pkgconfig/mariadb.pc
  install -D -m0644 "$srcdir"/"$pkgbase-$pkgver"/support-files/mysql.m4 "$pkgdir"/${pacman_root}/usr/share/aclocal/mysql.m4

}

package_mariadb-clients() {
  pkgdesc='MariaDB client tools'
  depends=("mariadb-libs=${pkgver}" 'jemalloc' 'ncurses')
  conflicts=('mysql-clients')
  provides=("mysql-clients=$pkgver")

  make -C build/client DESTDIR="${pkgdir}" install

  # install man pages
  make -C build/man DESTDIR="${srcdir}"/client-man install
  install -d -m0755 "${pkgdir}"/${pacman_root}/usr/share/man/man1/
  for man in $(find "${pkgdir}"/${pacman_root}/usr/bin/ ! -type d); do
    install -D -m0644 -t "${pkgdir}"/${pacman_root}/usr/share/man/man1/ "${srcdir}"/client-man/${pacman_root}/usr/share/man/man1/"$(basename "${man}")".1
  done
}

package_mariadb() {
  pkgdesc='Fast SQL database server, derived from MySQL'
  backup=(${pacman_root#/}/'etc/my.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/client.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/enable_encryption.preset'
          ${pacman_root#/}/'etc/my.cnf.d/mysql-clients.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/provider_bzip2.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/provider_lz4.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/provider_lzma.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/s3.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/server.cnf'
          ${pacman_root#/}/'etc/my.cnf.d/spider.cnf'
          ${pacman_root#/}/'etc/security/user_map.conf')
  install=mariadb.install
  depends=("mariadb-clients=${pkgver}" 'bzip2' 'libxml2' 'lz4' 'libxml2' 'zstd')
  optdepends=('cracklib: for cracklib plugin'
              'curl: for ha_s3 plugin'
              'galera: for MariaDB cluster with Galera WSREP'
              'judy: for Open Query GRAPH (OQGraph) computation engine'
              'perl-dbd-mariadb: for mariadb-hotcopy, mariadb-convert-table-format and mariadb-setpermission'
              'python-mysqlclient: for myrocks_hotbackup'
              'xz: lzma provider')
  conflicts=('mysql')
  provides=("mysql=$pkgver")
  options=('emptydirs')

  cd build

  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  # move to proper licenses directories
  install -d ${pacman_root#/}/usr/share/licenses/mariadb
  mv ${pacman_root#/}/usr/share/doc/mariadb/COPYING* ${pacman_root#/}/usr/share/licenses/mariadb/

  # move it where one might look for it
  mv ${pacman_root#/}/usr/share/{groonga{,-normalizer-mysql},doc/mariadb/}

  # provided by mariadb-libs
  rm ${pacman_root#/}/usr/bin/{mariadb{_,-},mysql_}config
  rm -r ${pacman_root#/}/usr/include/
  rm ${pacman_root#/}/usr/share/man/man1/{mariadb,mysql}_config.1
  rm -r ${pacman_root#/}/usr/share/aclocal
  rm ${pacman_root#/}/usr/lib/lib*
  rm -r ${pacman_root#/}/usr/lib/pkgconfig

  # note: the plugins are always .so
  rm ${pacman_root#/}/usr/lib/mysql/plugin/{auth_gssapi_client,caching_sha2_password,client_ed25519,dialog,mysql_clear_password,sha256_password,zstd}.so

  # provided by mariadb-clients
  for bin in $(find "${pkgdir}"/../mariadb-clients/${pacman_root#/}/usr/bin/ ! -type d); do
    rm "${pkgdir}"/${pacman_root#/}/usr/bin/"$(basename "${bin}")" "${pkgdir}"/${pacman_root#/}/usr/share/man/man1/"$(basename "${bin}")".1
  done

  # provided by mytop
  rm ${pacman_root#/}/usr/bin/mytop

  # not needed
  rm -r ${pacman_root#/}/usr/{mysql-test,sql-bench}
  rm ${pacman_root#/}/usr/share/man/man1/mysql-test-run.pl.1
}

package_mytop() {
  pkgdesc='Top clone for MariaDB'
  depends=('perl' 'perl-dbd-mariadb' 'perl-term-readkey')

  install -D -m0755 build/scripts/mytop "$pkgdir"/${pacman_root#/}/usr/bin/mytop
}

# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgbase=suitesparse
pkgname=(suitesparse suitesparse64)
pkgver=7.0.1
pkgrel=2
pkgdesc='A collection of sparse matrix libraries'
url='http://faculty.cse.tamu.edu/davis/suitesparse.html'
arch=('x86_64' 'arm64')
makedepends=(gcc-fortran cmake lapack lapack64)
license=(GPL)
source=(https://github.com/DrTimothyAldenDavis/SuiteSparse/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dc2f8d5c2657c120b30cce942f634ec08fc3a4b0b10e19d3eef7790b2bec8d1e')

prepare() {
  cp -r SuiteSparse-$pkgver SuiteSparse64
}

build() {
  cd SuiteSparse-$pkgver
  CMAKE_OPTIONS="-DBLA_VENDOR=Generic \
  -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
  -DCMAKE_BUILD_TYPE=Release" \
  make

  cd ../SuiteSparse64
  CMAKE_OPTIONS="-DBLA_VENDOR=Generic \
  -DALLOW_64BIT_BLAS=ON \
  -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
  -DCMAKE_BUILD_TYPE=Release" \
  make
}

package_suitesparse() {
  depends=(lapack mpfr)

  cd SuiteSparse-$pkgver
  DESTDIR="$pkgdir" make install

  local _f
  for _f in "${pkgdir}/${pacman_root}"/usr/bin/* "${pkgdir}/${pacman_root}"/usr/lib/*.dylib; do
    fix_rpath "${pacman_root}" "${pkgdir}" "${_f}"
  done
}

package_suitesparse64() {
  pkgdesc+=' (compiled with BLAS64)'
  depends=(lapack64 mpfr)
  conflicts=(suitesparse)
  provides=(suitesparse)

  cd SuiteSparse64
  DESTDIR="$pkgdir" make install

  local _f
  for _f in "${pkgdir}/${pacman_root}"/usr/bin/* "${pkgdir}/${pacman_root}"/usr/lib/*.dylib; do
    fix_rpath "${pacman_root}" "${pkgdir}" "${_f}"
  done
}

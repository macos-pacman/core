# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libzip
pkgver=1.9.2
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives"
url="https://libzip.org"
license=('BSD')
arch=('x86_64' 'arm64')
depends=('zlib' 'bzip2' 'zstd' 'openssl' 'gnutls')
makedepends=('cmake')
provides=('libzip.dylib')
source=("${url}/download/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c93e9852b7b2dc931197831438fee5295976ee0ba24f8524a8907be5c2ba5937')

build() {
  cd "${pkgname}-${pkgver}"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..
  make
}

package() {
  cd "${pkgname}-${pkgver}"/build

  make DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE"
}

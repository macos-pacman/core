# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-flake8
pkgver=1.1.1
pkgrel=2
pkgdesc='pytest plugin to check FLAKE8 requirements'
arch=('any')
license=('MIT')
url='https://github.com/tholo/pytest-flake8'
depends=('python-pytest' 'flake8')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tholo/pytest-flake8/archive/$pkgver.tar.gz"
        'fix-flake8-6.patch')
sha512sums=('7ad9b6da1fdb9f9ac8720820589d1a213b1c1bda8c2579d988d288031fb717916c81d100488200a4d64b880575041f34387c20685416ed789ced51063e814e13'
            '70d00ebc11868aa68590afb6576419d600dbfdab0af5f5ecba2d0b9aa32985f72117c1b01f8a0f1825b5c0a5e8bc23e2c22ff33c9e0008851698390489336941')

prepare() {
  cd pytest-flake8-$pkgver
  sed -i 's/ignore = E128/ignore = E128 W605/' tox.ini

  patch -Np1 -i ../fix-flake8-6.patch
}

build() {
  cd "$srcdir"/pytest-flake8-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-flake8-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages:$PYTHONPATH" py.test || :
}

package() {
  cd pytest-flake8-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:

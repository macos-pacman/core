# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=arrow
pkgver=12.0.0
pkgrel=1
pkgdesc="Columnar in-memory analytics layer for big data."
arch=('x86_64' 'arm64')
url="https://arrow.apache.org"
license=(Apache)
depends=(apache-orc boost-libs brotli bzip2 double-conversion c-ares gflags
         grpc google-glog jemalloc libutf8proc lz4 openssl protobuf rapidjson re2
         snappy thrift uriparser xsimd zlib zstd)
provides=(parquet-cpp)
conflicts=(parquet-cpp)
makedepends=(boost cmake flatbuffers gmock python-numpy git clang)
source=(https://archive.apache.org/dist/${pkgname}/${pkgname}-${pkgver}/apache-${pkgname}-${pkgver}.tar.gz{,.asc}
        git+https://github.com/apache/parquet-testing.git
        git+https://github.com/apache/arrow-testing.git
        'fix-protobuf.patch::https://patch-diff.githubusercontent.com/raw/apache/arrow/pull/35673.patch')
sha512sums=(f815be4fb20b6001ba5525270765fe239b5468708a7be34b93b60ee0ce63464727d183c9756fbc33bffd199019e1f06a7fddd306ce8388435cea7771070a2ca9
            SKIP
            SKIP
            SKIP
            6258e91e54670c6596a74cbb3bf238c7a1d112c11e762804cfb63953c0d9776fe989623a87bd74f6e2915924e4c3dad81f49aea6d368c9256cc9367210b74f39)
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007) # Kouhei Sutou <kou@cozmixng.org>

prepare() {
  cd apache-arrow-$pkgver
  patch -Np1 -i ../fix-protobuf.patch
}

build(){
  CC=clang \
  CXX=clang++ \
  ARROW_BUILD_TOOLCHAIN=${pacman_root}/usr \
  ORC_HOME=${pacman_root}/usr \
  DOUBLE_CONVERSION_HOME=${pacman_root}/usr \
  cmake \
    -B build -S apache-${pkgname}-${pkgver}/cpp \
    -DCMAKE_INSTALL_PREFIX="${pacman_root}/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DPROTOBUF_USE_DLLS=1" \
    -DARROW_BUILD_STATIC=OFF \
    -DARROW_DEPENDENCY_SOURCE=SYSTEM \
    -DARROW_BUILD_TESTS=ON \
    -DARROW_COMPUTE=ON \
    -DARROW_SUBSTRAIT=ON \
    -DARROW_FLIGHT=ON \
    -DARROW_FLIGHT_SQL=ON \
    -DARROW_GANDIVA=OFF \
    -DARROW_HDFS=ON \
    -DARROW_IPC=ON \
    -DARROW_JEMALLOC=ON \
    -DARROW_ORC=ON \
    -DARROW_PARQUET=ON \
    -DARROW_PLASMA=ON \
    -DARROW_PYTHON=ON \
    -DARROW_TENSORFLOW=ON \
    -DARROW_USE_GLOG=ON \
    -DARROW_WITH_BROTLI=ON \
    -DARROW_WITH_BZ2=ON \
    -DARROW_WITH_LZ4=ON \
    -DARROW_WITH_SNAPPY=ON \
    -DARROW_WITH_ZLIB=ON \
    -DARROW_WITH_ZSTD=ON \
    -DPARQUET_REQUIRE_ENCRYPTION=ON \
    -DCMAKE_CXX_STANDARD=17
  make -C build
}

check(){
  # skip failing test with assertion on float values that are actually equal
  ARGS="-E parquet-internals-test" \
  PARQUET_TEST_DATA="${srcdir}"/parquet-testing/data \
  ARROW_TEST_DATA="${srcdir}"/arrow-testing/data \
  make -C build test
}

package(){
  make -C build DESTDIR="${pkgdir}" install
  find "${pkgdir}"/${pacman_root}/usr/lib/ -name '*testing*' -delete

  local _f; for _f in ${pkgdir}/${pacman_root}/usr/lib/*.dylib; do
    fix_rpath "${_f}"
  done
}

# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgbase=gpgme
pkgname=(gpgme python-gpgme)
pkgver=1.20.0
pkgrel=2
pkgdesc='A C wrapper library for GnuPG'
arch=('x86_64' 'arm64')
url='https://www.gnupg.org/related_software/gpgme/'
license=('LGPL')
makedepends=(
  'libgpg-error' 'gnupg'
  'python'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'swig'
)
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA'  # Werner Koch (dist signing 2020)
              'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD') # Niibe Yutaka (GnuPG Release Key)
source=("https://www.gnupg.org/ftp/gcrypt/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('25a5785a5da356689001440926b94e967d02e13c49eb7743e35ef0cf22e42750'
            'SKIP')

build() {
  # distutils is deprecated but these clowns keep using it
  export PYTHONWARNINGS="ignore::DeprecationWarning"

  cd ${pkgbase}-${pkgver}

  ./configure \
    --prefix=${pacman_root}/usr \
    --disable-fd-passing \
    --disable-static \
    --disable-gpgsm-test
  make

  (
    # use a PEP517 workflow to get a reproducible Python package
    # NOTE: top_builddir is required so that the build takes place against local gpgme, not system gpgme
    cd lang/python/
    top_builddir="$srcdir/$pkgbase-$pkgver" python -m build --wheel --no-isolation
  )
}

check() {
  cd ${pkgbase}-${pkgver}

  # this test fails with gnupg (FS#66572)
  sed -i 's#"t-keylist-secret",##' tests/json/t-json.c

  # python .so links to the wrong thing. use install_name_tool to fix it.
  local _f
  for _f in $(find lang/python -iname "*.so"); do
    install_name_tool -change ${pacman_root}/usr/lib/libgpgme.11.dylib ${srcdir}/${pkgbase}-$pkgver/src/.libs/libgpgme.11.dylib ${_f}
  done

  make check
}

package_gpgme() {
  depends=('libgpg-error' 'gnupg>=2')
  options+=('!emptydirs')
  provides=('libgpgme.dylib'
            'libgpgmepp.dylib')

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # yeet python
  rm -r "${pkgdir}"/${pacman_root}/usr/lib/python*
}

package_python-gpgme() {
  pkgdesc="Python bindings for GPGme"
  depends=('gpgme' 'python')

  # change it back
  local _f
  for _f in $(find lang/python -iname "*.so"); do
    install_name_tool -change ${srcdir}/${pkgbase}-$pkgver/src/.libs/libgpgme.11.dylib ${pacman_root}/usr/lib/libgpgme.11.dylib  ${_f}
  done

  cd ${pkgbase}-${pkgver}/lang/python
  python -m installer --destdir="$pkgdir" dist/*.whl
}

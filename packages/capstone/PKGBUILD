# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: M0Rf30

pkgbase=capstone
pkgname=('capstone' 'python-capstone')
pkgver=4.0.2
pkgrel=6
pkgdesc='Lightweight multi-platform, multi-architecture disassembly framework'
url='https://www.capstone-engine.org/index.html'
arch=('x86_64' 'arm64')
license=('BSD')
makedepends=('glibc' 'python' 'python-setuptools' 'cmake')
options=('staticlibs')
source=(
	https://github.com/capstone-engine/capstone/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
	"fix-apple-check.patch"
)
sha512sums=('7f93534517307b737422a8825b66b2a1f3e1cca2049465d60ab12595940154aaf843ba40ed348fce58de58b990c19a0caef289060eb72898cb008a88c470970e'
'f2b9814d102d9167750f895190b265e114cb33bb27e8711a4aeba3403c961f56df0ad680cd9810f423f13f148c0926fc75acdbef830b6d6ba0d91d6a35054894')
b2sums=('435729a8fef2dce6495635352101b3befe563c8404efdbb0dccabecbe2bded332221665bacdbcd9043dda72b652b6f29c0e1a548cefb8c64d5b6b9dc174ed3d9'
'b445900fefc9e587d2d4525a947201d9186c1a69b029f00e25096654cae1c730ff35ad15d90f12913b32dc8a1eba3b2f04259cbbcf73832831f3dc5d8091ae37')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -Np1 -i ../fix-apple-check.patch
}

build() {
  cd ${pkgbase}-${pkgver}
  CFLAGS+=" ${CPPFLAGS}"
  cmake -B build \
  	-DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
  	-DCMAKE_BUILD_TYPE=Release

  make -C build IS_APPLE=1
}

package_capstone() {
  depends=('glibc')
  provides=('libcapstone.dylib')
  cd ${pkgbase}-${pkgver}

  make -C build install DESTDIR="${pkgdir}"

  install -Dm 644 docs/README -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.TXT -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
}

package_python-capstone() {
  depends=('capstone' 'python')
  cd ${pkgbase}-${pkgver}/bindings/python
  IS_APPLE=1 python setup.py install -O1 --root="${pkgdir}" --prefix=${pacman_root}/usr
  install -Dm 644 ../../LICENSE.TXT -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"

  # fix rpath. it will still error, but that's fine because capstone is a dep
  fix_rpath "${pkgdir}/${pacman_root}/usr/lib/python3.11/site-packages/capstone/lib/libcapstone.dylib"
}

# vim: ts=2 sw=2 et:

# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>

pkgname=libssh2
pkgver=1.10.0
pkgrel=3
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://www.libssh2.org/"
arch=('x86_64' 'arm64')
license=('BSD')
depends=('openssl' 'zlib')
provides=('libssh2.dylib')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)
source=("https://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc}
        "sshd-enable-rsa.patch")
sha256sums=('2d64e90f3ded394b91d3a2e774ca203a4179f69aebee03003e5a6fa621e41d51'
            'SKIP'
            '462e8e3fc1ee6220de8a7e6453b49dd5e1db7dd4d718ca8ff7ec83277cbe1c7b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../sshd-enable-rsa.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=${pacman_root}/usr
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
}

# Maintainer: zhiayang <zhiayang@nowhere>

pkgname=rust-sbnf
pkgver=0.6.4
pkgrel=1
pkgdesc="A BNF-style language for writing sublime-syntax files"
arch=('arm64' 'x86_64')
url="https://github.com/BenjaminSchaaf/sbnf"
license=('MIT')
makedepends=('cargo' 'git')
options=('!lto')
_commit=c99d43b127fb081cdd133c3045a573c07b369c82
source=("$pkgname::https://github.com/BenjaminSchaaf/sbnf/archive/${_commit}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "sbnf-$_commit"
  cargo fetch --locked --target "${CARCH/arm64/aarch64}-apple-darwin"
}

build() {
  cd "sbnf-$_commit"
  cargo build --release --frozen
}

check() {
  cd "sbnf-$_commit"
  cargo test --frozen --all-features
}

package() {
  cd "sbnf-$_commit"
  install -Dm755 "target/release/sbnf" "$pkgdir/${pacman_root}/usr/bin/sbnf"
}

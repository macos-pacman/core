# Maintainer: zhiayang <zhiayang@nowhere>

pkgname=rust-sbnf
pkgver=0.6.0
pkgrel=1
pkgdesc="A BNF-style language for writing sublime-syntax files"
arch=('arm64' 'x86_64')
url="https://github.com/BenjaminSchaaf/sbnf"
license=('MIT')
makedepends=('cargo' 'git')
options=('!lto')
_commit=c1b67aa583b3bd271db167995dd223590643af8a
source=("$pkgname::https://github.com/BenjaminSchaaf/sbnf/archive/${_commit}.tar.gz")
sha256sums=('2eeca6cb0b284799ebda2bc554bbd64743b079b1622e7d3e3911222c523bc3f4')

prepare() {
  cd "sbnf-$_commit"
  cargo fetch --locked --target "${CARCH/arm64/aarch64}-apple-darwin"
}

build() {
  cd "sbnf-$_commit"
  cargo build --release --frozen
}

check() {
  cd "sbnf-$_commit"
  cargo test --frozen --all-features
}

package() {
  cd "sbnf-$_commit"
  install -Dm755 "target/release/sbnf" "$pkgdir/${pacman_root}/usr/bin/sbnf"
}

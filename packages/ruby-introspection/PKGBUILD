# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=introspection
pkgname=ruby-$_gemname
pkgver=0.0.4
pkgrel=1
pkgdesc='Dynamic inspection of the hierarchy of method definitions on a Ruby object'
arch=(any)
url='https://github.com/floehopper/'${_gemname}
license=(BSD-2)
depends=(ruby ruby-metaclass)
makedepends=(ruby-minitest ruby-blankslate ruby-rake)
checkdepends=(ruby-rake)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('44150b51ab79bdcca9371ede9e965d79d05b25592952018badb6d7a3286b6b97')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' "${_gemname}.gemspec"

  rm -fv Gemfile*
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --local --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 *.md -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING.txt -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

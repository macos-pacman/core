# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=glib2
pkgname=(
  glib2
  glib2-docs
)
pkgver=2.78.4
pkgrel=1
pkgdesc="Low level core library"
url="https://gitlab.gnome.org/GNOME/glib"
license=(LGPL-2.1-or-later)
arch=('x86_64' 'arm64')
depends=(
  libffi
  pcre2
  zlib
)
makedepends=(
  gettext
  git
  gtk-doc
  meson
  python
  shared-mime-info
)
checkdepends=(
  glib2
)
options=(
  staticlibs
)
_commit=00edb7f7453a43cff343f9e99d49f2e421e4345c  # tags/2.78.4^0
source=(
  "git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/gvdb.git"
  0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch
  gio-querymodules.hook
  glib-compile-schemas.hook
  fix-macos-warning.patch
  macos-test-ignore.patch
)
b2sums=('SKIP'
        'SKIP'
        '94c73ca7070c239494873dd52d6ee09382bbb5b1201f7afd737cfa140b1a2fb0744b2c2831baf3943d1d072550c35888d21ce6f19f89481ff9d1a60d9a0b30e0'
        '14c9211c0557f6d8d9a914f1b18b7e0e23f79f4abde117cb03ab119b95bf9fa9d7a712aa0a29beb266468aeb352caa3a9e4540503cfc9fe0bbaf764371832a96'
        'd30d349b4cb4407839d9074ce08f5259b8a5f3ca46769aabc621f17d15effdb89c4bf19bd23603f6df3d59f8d1adaded0f4bacd0333afcab782f2d048c882858'
        'dcc4bacf33ea92851fc07016750336f957f2383cd0dfe91bb5f7ebf6f1ff661942616d460345a534632109ab1293c5134c721ba894a3d99e08f1dac8db22dae0'
        '912c8cb70b8058a15070854da8280dae3492eab82b81273c0f3868dec6c1daa59d7568b81203ae0e5ef97d2099468945e44226e22f22b4e2531d97ddcf513303')


pkgver() {
  cd glib
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glib

  patch -Np1 -i ../fix-macos-warning.patch
  patch -Np1 -i ../macos-test-ignore.patch

  # Suppress noise from glib-compile-schemas.hook
  git apply -3 ../0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch

  git submodule init
  git submodule set-url subprojects/gvdb "$srcdir/gvdb"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    --default-library both
    -D glib_debug=disabled
    -D gtk_doc=true
    -D man=true
    -D dtrace=false
    -D selinux=disabled
    -D sysprof=disabled
    -D bsymbolic_functions=false
  )

  # Produce more debug info: GLib has a lot of useful macros
  CFLAGS+=" -g3"
  CXXFLAGS+=" -g3"

  arch-meson glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # 3 tests fail (one is just a shitty doc test, fuck you)
  meson test -C build --no-suite flaky --no-suite slow --print-errorlogs || :
}

package_glib2() {
  depends+=(
    libffi.dylib
  )
  provides+=(libg{lib,io,module,object,thread}-2.0.dylib)
  optdepends=(
    'gvfs: most gio functionality'
    'libelf: gresource inspection tool'
    'python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
  )

  meson install -C build --destdir "$pkgdir"

  install -Dt "$pkgdir/${pacman_root}/usr/share/libalpm/hooks" -m644 *.hook
  touch "$pkgdir/${pacman_root}/usr/lib/gio/modules/.keep"

  python -m compileall -d ${pacman_root}/usr/share/glib-2.0/codegen \
    "$pkgdir/${pacman_root}/usr/share/glib-2.0/codegen"
  python -O -m compileall -d ${pacman_root}/usr/share/glib-2.0/codegen \
    "$pkgdir/${pacman_root}/usr/share/glib-2.0/codegen"

  # Split docs
  mkdir -p docs/${pacman_root}/usr/share
  mv {"$pkgdir",docs}/${pacman_root}/usr/share/gtk-doc
}

package_glib2-docs() {
  pkgdesc+=" - documentation"
  depends=()
  license+=(LicenseRef-Public-Domain)

  mv docs/* "$pkgdir"/
  install -Dt "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname" -m644 glib/docs/reference/COPYING
}

# vim:set sw=2 sts=-1 et:

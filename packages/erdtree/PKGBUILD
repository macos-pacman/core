# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=erdtree
pkgver=3.1.2
pkgrel=1
pkgdesc='A multi-threaded filesystem and disk-usage analysis tool'
arch=('x86_64' 'arm64')
url='https://github.com/solidiquis/erdtree'
license=('MIT')
pkgrel+=".1"
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
_commit='2b1db8bcc843c3fa8b634df86f94716c75ca470a'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "${CARCH/arm64/aarch64}-apple-darwin"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features

  # completions
  cd target/release
  for shell in bash fish zsh; do
    ./erd --completions "$shell" > "$shell-completion"
  done

}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/${pacman_root}/usr/bin" target/release/erd

  # documentation
  install -vDm644 -t "$pkgdir/${pacman_root}/usr/share/doc/$pkgname" README.md
  cp -vr assets "$pkgdir/${pacman_root}/usr/share/doc/$pkgname"

  # completions
  pushd target/release
  install -vDm644 bash-completion "$pkgdir/${pacman_root}/usr/share/bash-completion/completions/erd"
  install -vDm644 fish-completion "$pkgdir/${pacman_root}/usr/share/fish/vendor_completions.d/erd.fish"
  install -vDm644 zsh-completion "$pkgdir/${pacman_root}/usr/share/zsh/site-functions/_erd"
  popd

  # license
  install -vDm644 -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname" LICENSE
}

# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=zeitwerk
pkgname=ruby-$_gemname
pkgver=2.6.11
pkgrel=1
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(BSD-2)
depends=(ruby)
makedepends=(ruby-rake ruby-rdoc)
checkdepends=(ruby-minitest ruby-minitest-focus ruby-minitest-reporters ruby-minitest-proveit ruby-warning)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('88e556d10d18086f56e91eec67fc90e9282ffc8cea64be12999d0e4b517eb0d7')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' "${_gemname}.gemspec"

  rm -fv Gemfile Gemfile.lock
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --local --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 *.md -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 *LICENSE* -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

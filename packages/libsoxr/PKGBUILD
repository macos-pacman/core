# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Anatol Pomozov
# Contributor: JSpaces <jspace@unseen.is>
# Contributor: Triode <triode1@btinternet.com>

pkgname=libsoxr
pkgver=0.1.3
pkgrel=3
pkgdesc='The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio'
arch=('x86_64' 'arm64')
url=https://sourceforge.net/p/soxr/wiki/Home/
license=(GPL)
depends=(gcc-libs)
makedepends=(
  cmake
  ninja
)
source=(https://downloads.sourceforge.net/project/soxr/soxr-$pkgver-Source.tar.xz
        https://raw.githubusercontent.com/Homebrew/formula-patches/76868b36263be42440501d3692fd3a258f507d82/libsoxr/arm64_defines.patch)
b2sums=('5b7078f71404b9ef83917dcabc0c99cf17fd625d3e033d1b934382a7b60e55e359931a6ab53f8adaf955c0d811d6cbf231cd6a226549560b3506a3867e845607'
        '1d3d4cf6d3c38eb30985032cba511b696900e89ad39ac68280808fafa804d96e13db5b47a2000be11e192c2b0957b8da32e0d88b24d2cdfb6f0db63a42b7bae1')

prepare() {
  cd soxr-${pkgver}-Source
  patch -Np1 -i ../arm64_defines.patch
}

build() {
  cmake -S soxr-${pkgver}-Source -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_AVFFT=ON \
    -DWITH_LSR_BINDINGS=ON \
    -DWITH_OPENMP=ON \
    -DWITH_PFFFT=ON
    cmake --build build
}

check() {
  ctest --test-dir build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  rm -rf "${pkgdir}"/${pacman_root}/usr/share/doc
  install -Dm 644 soxr-${pkgver}-Source/LICENCE -t "${pkgdir}"/${pacman_root}/usr/share/licenses/libsoxr/
}

# vim: ts=2 sw=2 et:

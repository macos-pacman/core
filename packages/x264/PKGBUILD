# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264
pkgver=0.164.r3095.baee400
pkgrel=4
epoch=3
pkgdesc='Open Source H264/AVC video encoder'
arch=('x86_64' 'arm64')
url=https://www.videolan.org/developers/x264.html
license=(GPL)
depends=(liblsmash.dylib)
makedepends=(
  git
)
provides=(
  libx264
  libx264.dylib
)
conflicts=(
  libx264
  libx264-10bit
  libx264-all
)
replaces=(
  libx264
  libx264-10bit
  libx264-all
)
_commit=baee400fa9ced6f5481a728138fed6e867b0ff7f  # master
source=(git+https://code.videolan.org/videolan/x264.git#commit=${_commit})
sha256sums=(SKIP)

if [ $CARCH = "x86_64" ]; then
  makedepends+=(nasm)
fi

pkgver() {
  cd x264
  ./version.sh | grep X264_POINTVER | sed -r 's/^#define X264_POINTVER "([0-9]+\.[0-9]+)\.([0-9]+) (.*)"$/\1.r\2.\3/'
}

build() {
  cd x264
  ./configure \
    --prefix=${pacman_root}/usr \
    --enable-shared \
    --enable-static \
    --enable-pic \
    --disable-avs
  make
}

package() {
  make -C x264 DESTDIR="${pkgdir}" BASHCOMPLETIONSDIR="${pacman_root}/usr/share/bash-completion/completions" install-cli install-lib-shared install-bashcompletion
}

# vim: ts=2 sw=2 et:

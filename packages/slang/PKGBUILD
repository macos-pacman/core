# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.3
pkgrel=2
pkgrel+=".1"
pkgdesc="S-Lang is a powerful interpreted language"
arch=('x86_64' 'arm64')
url="https://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=(${pacman_root#/}/'etc/slsh.rc')
options=('!makeflags')
source=(https://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha256sums=('f9145054ae131973c61208ea82486d5dd10e3c5cdad23b7c4a0617743c8f5a18'
            'SKIP')
validpgpkeys=('64083373E9E1DE997EBBE7784B82D0B82930237D')  # John E. Davis

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=${pacman_root}/usr --sysconfdir=${pacman_root}/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}

# Maintainer: zhiayang <zhiayang@nowhere>

_hkgname=servant-server
pkgname=haskell-${_hkgname}
pkgver=0.19.2
pkgrel=1
pkgdesc="A family of combinators for defining webservices APIs and serving them"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64' 'arm64')
depends=('ghc-libs'
  'haskell-bytestring'
  'haskell-constraints'
  'haskell-containers'
  'haskell-mtl'
  'haskell-text'
  'haskell-transformers'
  'haskell-filepath'
  'haskell-servant'
  'haskell-http-api-data'
  'haskell-base-compat'
  'haskell-base64-bytestring'
  'haskell-exceptions'
  'haskell-http-media'
  'haskell-http-types'
  'haskell-network-uri'
  'haskell-monad-control'
  'haskell-network'
  'haskell-sop-core'
  'haskell-string-conversions'
  'haskell-resourcet'
  'haskell-tagged'
  'haskell-transformers-base'
  'haskell-wai'
  'haskell-wai-app-static'
  'haskell-word8'
  'haskell-aeson'
)
checkdepends=(
  'haskell-directory'
  'haskell-hspec'
  'haskell-hspec-wai'
  'haskell-quickcheck'
  'haskell-should-not-typecheck'
  'haskell-temporary'
  'haskell-wai-extra'
)
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('c0814819ab5c1a4b81204d0a7f9d4f0f4f2ab055066f47ef1c4b803baac45b1c4acf99448055de40f0547caa64a79cc93e80d878c1e61ce792019afb8c6d543b')

prepare() {
  cd $_hkgname-$pkgver
  uusi \
    -u 'base' \
    -u 'base-compat' \
    -u 'hspec' \
    -u 'http-api-data' \
    -u 'mtl' \
    -u 'resourcet' \
    -u 'transformers' \
    ${_hkgname}.cabal
}

# template start; name=haskell-default; version=1.0;
# default haskell build steps

build() {
# template start; name=haskell-build; version=1.0;
  cd $_hkgname-$pkgver

  local _opts
  if (( CHECKFUNC )) && [ -z ${_notests+x} ]; then
    _opts=('--enable-tests')
  else
    _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --disable-static --enable-shared --enable-executable-dynamic \
    --prefix=${pacman_root}/usr \
    --docdir=${pacman_root}/usr/share/doc/$pkgname \
    --datasubdir=$pkgname \
    --dynlibdir='$libdir' \
    -ftemplateHaskell --ghc-option=-fllvm --ghc-option=-fhide-source-paths \
    --flags="${_build_flags:-}" \
    "${_opts[@]}"

  runhaskell Setup build $MAKEFLAGS

  if [ -z ${_no_register+x} ]; then
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script

    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
  fi
# template end;
}

check() {
# template start; name=haskell-check; version=1.0;
  cd $_hkgname-$pkgver
  if [ -z ${_notests+x} ]; then
    if [ -z ${_tests_can_fail+x} ]; then
      runhaskell Setup test --show-details=direct
    else
      runhaskell Setup test --show-details=direct || echo "tests failed!"
    fi
  fi
# template end;
}

package() {
# template start; name=haskell-package; version=1.0;
  cd $_hkgname-$pkgver

  if [ -z ${_no_register+x} ]; then
    install -D -m744 register.sh "$pkgdir/${pacman_root}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "$pkgdir/${pacman_root}/usr/share/haskell/unregister/${pkgname}.sh"
  fi

  local _license_name=${license_name-LICENSE}

  runhaskell Setup copy --destdir="$pkgdir"

  if [ -z ${_no_license+x} ]; then
    install -D -m644 "${_license_name}" "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
    rm "$pkgdir"/${pacman_root}/usr/share/doc/$pkgname/${_license_name}
  fi

  local dl; for dl in ${pkgdir}/${pacman_root}/usr/lib/*.dylib ${pkgdir}/${pacman_root}/usr/bin/* ; do
    [ ! -e "${dl}" ] || fix_rpath "${dl}"
  done
# template end;
}
# template end;

# vim: ts=2 sw=2 et:

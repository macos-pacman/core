# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=cargo-c
pkgver=0.9.19
pkgrel=1
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries'
arch=('x86_64' 'arm64')
url='https://github.com/lu-zero/cargo-c/'
license=('MIT')
depends=('curl' 'openssl' 'zlib')
makedepends=('cargo' 'git')
source=("https://github.com/lu-zero/cargo-c/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock"
        'disable-time-machine-fuckery.patch')
sha256sums=('c2633ff22e52da9985394f30c8ef5e9abbac4d14c9b79e3690c8e95cf500ab97'
            '7dc8d68efcda151e0b284654c9ca0a17af4cc8bbf4db88342297b016361ef0d3'
            '8cca3f1fd21e5ef59dc1f557dae4ee1f814ac7e6d2d5bead752c9786e6a89eac')

prepare() {
    cp "../${pkgname}-${pkgver}.Cargo.lock" "${pkgname}-${pkgver}/Cargo.lock"

    # patch and unfuck
    pushd ${srcdir}/${pkgname}-${pkgver}
        # clone cargo
        git clone https://github.com/rust-lang/cargo.git
        pushd cargo
            git checkout 0.70.0
        popd

        patch -Np1 -i ../disable-time-machine-fuckery.patch
    popd

    cargo fetch --locked --target "${CARCH/arm64/aarch64}-apple-darwin" --manifest-path="${pkgname}-${pkgver}/Cargo.toml"
}

build() {
    export RUSTUP_TOOLCHAIN='stable'
    export CARGO_TARGET_DIR="${pkgname}-${pkgver}/target"
    cargo build --release --frozen --manifest-path="${pkgname}-${pkgver}/Cargo.toml"
}

check() {
    export RUSTUP_TOOLCHAIN='stable'
    export CARGO_TARGET_DIR="${pkgname}-${pkgver}/target"
    cargo test --frozen --manifest-path="${pkgname}-${pkgver}/Cargo.toml"
}

package() {

    install -D -m755 -t "${pkgdir}/${pacman_root}/usr/bin/" "${pkgname}-${pkgver}/target/release/cargo-"{capi,cbuild,ctest,cinstall}
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
}

# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase='protobuf'
pkgname=('protobuf' 'python-protobuf')
pkgver=21.12
pkgrel=2
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('x86_64' 'arm64')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=(
  'gcc-libs'
  'glibc'
  'zlib'
)
makedepends=(
  'cmake'
  'gtest'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  python-pytest
)
source=(https://github.com/protocolbuffers/protobuf/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        $pkgbase-21.12-python3.11.patch::https://github.com/protocolbuffers/protobuf/commit/da973aff2adab60a9e516d3202c111dbdde1a50f.patch
        $pkgbase-21.12-pep517.patch  # let's not call setup.py like a script and just build...
        https://github.com/protocolbuffers/protobuf/commit/101b6199.patch)
sha512sums=('2dc8f552388438268d8b9f7a9e84c6abf1736be3d5031438c789c317410c9f4b5cedd25bf7da6d67b3ba32ca890869f9ddaab2284d6ac0e734a5b135ffbb1346'
            '906629e6eb8a128efc3bf5ad039fa61e87d679c3354be066a47a11ea4659ea8ba84d9aa529e73ace4b0964605a9325d40fa0b87450bcd801f7cab6282b2559a3'
            'a297e74ee4f807b3fad7da7d0de6dd9647963521be66cd2a2370343f5bd191cbb38759157ac0cdb161a5893a30a10520a5098e88a292c800859af33db5cf7a41'
            '63a56d6ba2bd452ef5380eca4ec5e294a395a6dae95fbe3ae28ee6bceb601450e234a2052b1df010c9cea008b8d03176ffd08fa62a66bf5e7af64c9829e788c8')

prepare() {
  patch -d $pkgbase-$pkgver -Rp1 < 101b6199.patch # Revert ABI break
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgname-21.12-python3.11.patch
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgname-21.12-pep517.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=${pacman_root}/usr
    -D protobuf_BUILD_SHARED_LIBS=ON
    -D protobuf_USE_EXTERNAL_GTEST=ON
    -S $pkgbase-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose

  ln -sf ${srcdir}/build $pkgbase-$pkgver/src/.libs
  cd $pkgbase-$pkgver/python
  PROTOC="$srcdir"/build/protoc \
  python -m build --wheel --no-isolation
}

check() {
  cmake --build build --target check

  cd $pkgbase-$pkgver/python
  pytest -vv google/$pkgbase/internal/
}

package_protobuf() {
  replaces=('protobuf-cpp')
  provides=('libprotoc.dylib' 'libprotobuf.dylib' 'libprotobuf-lite.dylib')

  DESTDIR="$pkgdir" cmake --install build

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir/${pacman_root}/usr/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}/${pacman_root}/usr/share/vim/vimfiles/syntax"
}

package_python-protobuf() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=(
    "protobuf=$pkgver"
    'python'
  )

  python -m installer --destdir="$pkgdir" $pkgbase-$pkgver/python/dist/*.whl
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"

  # note: we will complain about broken dylibs, but it's ok because the dylib
  # is actually in the `protobuf` package, which we depend on.
  local _site=$(python -c 'import site; print(site.getsitepackages()[0])')
  fix_rpath ${pkgdir}/${_site}/google/protobuf/pyext/_message.cpython-*-darwin.so
}





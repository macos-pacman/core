# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apache
pkgver=2.4.57
pkgrel=2
pkgdesc='A high performance Unix-based HTTP server'
arch=('x86_64' 'arm64')
url='https://www.apache.org/dist/httpd'
license=('APACHE')
backup=(
    ${pacman_root#/}/etc/httpd/conf/httpd.conf
    ${pacman_root#/}/etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
    ${pacman_root#/}/etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
    ${pacman_root#/}/etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
    ${pacman_root#/}/etc/httpd/conf/extra/proxy-html.conf
    ${pacman_root#/}/etc/httpd/conf/{mime.types,magic}
    ${pacman_root#/}/etc/logrotate.d/httpd
)
provides=('mod_proxy_uwsgi')
conflicts=('mod_proxy_uwsgi')
replaces=('mod_proxy_uwsgi')
depends=('zlib' 'apr-util' 'pcre2' 'libnghttp2' 'openssl' 'libxcrypt')
makedepends=('libxml2' 'lua' 'curl' 'brotli' 'jansson' 'db')
optdepends=(
    'lua: for mod_lua module'
    'libxml2: for mod_proxy_html, mod_xml2enc modules'
    'curl: for mod_md module'
    'jansson: for mod_md module'
    'brotli: for mod_brotli module'
    'uwsgi: for mod_proxy_uwsgi module'
    'lynx: apachectl status'
    'perl: for apxs and dbmmanage'
)
source=(
    https://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2{,.asc}
    arch.layout.in
)
sha256sums=('dbccb84aee95e095edfbb81e5eb926ccd24e6ada55dcd83caecb262e5cf94d2a'
            'SKIP'
            '6a07422fe4b4cc2fba4e444442f6cb5065e44b666685ba7ad469dd92ac841638')
# following keys are coming from https://downloads.apache.org/httpd/KEYS
validpgpkeys=('A93D62ECC3C8EA12DB220EC934EA76E6791485A8' # Jim Jagielski
              'B9E8213AEFB861AF35A41F2C995E35221AD84DFF'
              'C55AB7B9139EB2263CD1AABC19B033D1760C227B' # christophe.jaillet@wanadoo.fr
	      '26F51EF9A82F4ACB43F1903ED377C9E7D1944C66' # Stefan Eissing (icing) <stefan@eissing.org>
              'E3480043595621FE56105F112AB12A7ADC55C003' # Daniel Ruggeri
	      '65B2D44FE74BD5E3DE3AC3F082781DE46D5954FA')  # Eric Covener <covener@apache.org>

prepare() {
  cd httpd-${pkgver}

  # set default user
  sed -e 's#User daemon#User _www#' \
      -e 's#Group daemon#Group _www#' \
      -i docs/conf/httpd.conf.in

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" "${srcdir}/arch.layout.in" >> config.layout
}

build() {
  cd httpd-${pkgver}

  ./configure --sbindir=${pacman_root}/usr/bin \
      --enable-layout=Arch \
      --enable-mpms-shared=all \
      --enable-modules=all \
      --enable-mods-shared=all \
      --enable-so \
      --enable-suexec \
      --with-suexec-caller=_wwwp \
      --with-suexec-docroot=${pacman_root}/srv/http \
      --with-suexec-logfile=${pacman_root}/var/log/httpd/suexec.log \
      --with-suexec-bin=${pacman_root}/usr/bin/suexec \
      --enable-ldap --enable-authnz-ldap --enable-authnz-fcgi \
      --enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
      --enable-ssl --with-ssl \
      --enable-deflate --enable-cgi --enable-cgid \
      --enable-proxy --enable-proxy-connect \
      --enable-proxy-http --enable-proxy-ftp \
      --enable-dbd --enable-imagemap --enable-ident --enable-cern-meta \
      --enable-lua --enable-xml2enc --enable-http2 \
      --enable-proxy-http2 --enable-md --enable-brotli \
      --with-apr=${pacman_root}/usr/bin/apr-1-config \
      --with-apr-util=${pacman_root}/usr/bin/apu-1-config \
      --with-z=${pacman_root}/usr/ \
      --with-libxml2=${pacman_root}/usr \
      --with-pcre2

  make
}

package() {
  cd httpd-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -D -m644 LICENSE "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE"

  # symlinks for /etc/httpd
  # do we really need this symlink?
  ln -fs ${pacman_root}/usr/lib/httpd/modules "${pkgdir}/${pacman_root}/etc/httpd/modules"

  # set sane defaults
  sed -e "s#${pacman_root}/usr/lib/httpd/modules/#modules/#" \
      -e 's|#\(LoadModule negotiation_module \)|\1|' \
      -e 's|#\(LoadModule include_module \)|\1|' \
      -e 's|#\(LoadModule userdir_module \)|\1|' \
      -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
      -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
      -i "${pkgdir}/${pacman_root}/etc/httpd/conf/httpd.conf"

  # cleanup
  rm -r "${pkgdir}/${pacman_root}/usr/share/httpd/manual"
  rm -r "${pkgdir}/${pacman_root}/etc/httpd/conf/original"
  rm -r "${pkgdir}/${pacman_root}/srv/"
  rm -r "${pkgdir}/${pacman_root}/run"
}

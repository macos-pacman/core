# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgname=python-pillow
pkgver=10.0.0
pkgrel=2
pkgdesc='Python Imaging Library (PIL) fork'
arch=('x86_64' 'arm64')
url='https://python-pillow.org'
license=('BSD')
depends=(
  'glibc'
  'python'
  'python-packaging'
  'freetype2'
  'lcms2'
  'libraqm'
  'libtiff'
  'openjpeg2'
  'libjpeg-turbo'
  'libimagequant'
  'libxcb'
  'zlib'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'libwebp'
  'tk'
)
checkdepends=(
  'python-pytest'
  'python-pytest-timeout'
)
optdepends=(
  'libwebp: for webp images'
  'tk: for the ImageTK module'
  'python-olefile: OLE2 file support'
  'python-pyqt6: for the ImageQt module'
)
_commit='6e28ed1f36d0eb74053af54e1eddc9c29db698cd'
source=("$pkgname::git+https://github.com/python-pillow/Pillow#commit=$_commit")
b2sums=('SKIP')


pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  export CFLAGS+=" -Wno-undef-prefix "
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  export CFLAGS+=" -Wno-undef-prefix "
  export PYTHONPATH="$PWD/build/$(_GetPythonPlatformPath)"
  python selftest.py
  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  install -vDm644 -t "$pkgdir/${pacman_root}/usr/include/python$python_version" src/libImaging/*.h

  # license
  install -vDm644 -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname" LICENSE
}

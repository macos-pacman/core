# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matt Monaco <cx monaco matt>

pkgname=lmdb
pkgver=0.9.30
_commit=8f44dca45d87bc1f8da65428172668c5e6154179
pkgrel=4
pkgdesc="Symas Lightning Memory-Mapped Database"
arch=('x86_64' 'arm64')
url="https://symas.com/lmdb"
license=('custom:OpenLDAP')
depends=('glibc')
makedepends=('git')
options=('!emptydirs')
source=("git+https://git.openldap.org/openldap/openldap.git#commit=$_commit"
        lmdb.pc.in)
sha512sums=('SKIP'
            '4d9ef95ca075eeed0f8d134710ac8448afcafbd5bbc48af42e2e466635a34e62aaab0c7b7eeaefaa843097e15f6cc3b753b417407d3e3f1cf0f10d78215446f2')

prepare() {
  sed -e "s|@PKGVER@|$pkgver|" -i lmdb.pc
}

build() {
  cd openldap/libraries/liblmdb
  make CFLAGS="$CFLAGS" CPPFLAGS="$CPPFLAGS" LDFLAGS="$LDFLAGS" SOEXT=".dylib" prefix=${pacman_root}/usr

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" ${srcdir}/lmdb.pc.in > ${srcdir}/lmdb.pc
}

check() {
  cd openldap/libraries/liblmdb
  make test
}

package() {
  cd openldap/libraries/liblmdb
  install -dm755 "$pkgdir"/${pacman_root}/usr/{bin,lib,include,man/man1,share}
  make DESTDIR="$pkgdir" SOEXT=".dylib" prefix=${pacman_root}/usr install

  mv "$pkgdir"/${pacman_root}/usr/man "$pkgdir"/${pacman_root}/usr/share/man

  install -Dm644 LICENSE -t "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/
  install -Dm644 "$srcdir"/lmdb.pc -t "$pkgdir"/${pacman_root}/usr/lib/pkgconfig/

  fix_rpath ${pkgdir}/${pacman_root}/usr/lib/liblmdb.dylib
}

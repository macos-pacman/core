# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-base
pkgname=(qt5-base)
_basever=5.15.12
pkgver=5.15.12+kde+r151
pkgrel=1
pkgrel+=".1"
_commit=a43df98d037ad07cf096ef2f775958ceba743613
arch=('x86_64' 'arm64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libjpeg-turbo' 'fontconfig'
         'shared-mime-info' 'sqlite'
         'pcre2' 'zlib' 'python' 'glib2' 'icu' 'freetype2' 'harfbuzz' 'libpng'
         'double-conversion' 'md4c')
makedepends=('mariadb-libs' 'unixodbc' 'postgresql-libs'
             'pkg-config' 'sed'
             'git')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin'
            'perl: for fixqt4headers and syncqt')
conflicts=('qtchooser')
groups=('qt5')
_pkgfqn=${pkgbase/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        work-around-pyside2-brokenness.patch
        qmake-cflags.patch
        qmake-config.patch
        fucking-stop-using-xcode.patch
        fucking-please-stfu-about-sdk.patch
        fucking-fix-mysql-detection.patch
        fucking-fix-macos-compile.patch
        backport-503172.patch)
sha256sums=('SKIP'
            '6a4ec2bfcf4e7cff73346762b252cc28be5ca0ed79fde5e69350efe229b43adc'
            '5411edbe215c24b30448fac69bd0ba7c882f545e8cf05027b2b6e2227abc5e78'
            '4abc22150fa3e06b2fdcec32146abc9be4e316692aa4d5bd5aa53b4b726783fa'
            '51bea035cbfec099924eb1ad8a2faa2dbe16dd859f2394eb9a819391f7b8489d'
            'a6ead4329b1a859f001874bc035fc0dc8e92ebeb5e4a7abbf158e4f3bcc2980c'
            'd992fec118dd843f2e392ef9dd60f5328762608c719f9ea63aa3bbde76efaa7c'
            '5dbb51f3c702d71f4228ae336c9280798e401374140ab20fbe2b5a3d161ad43f'
            '757b83f99c7a28790416cbcfa4e4469b966adf3bfb4c21f678703c1a32acc9df')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  cd ${_pkgfqn}
  patch -p1 < ../work-around-pyside2-brokenness.patch
  patch -p1 < ../qmake-cflags.patch # Use system CFLAGS in qmake
  patch -p1 < ../qmake-config.patch # Don't strip binaries with qmake and use -ltcg, cf. QTBUG-73834
  patch -p1 < ../fucking-please-stfu-about-sdk.patch
  patch -p1 < ../fucking-fix-mysql-detection.patch
  patch -p1 < ../fucking-fix-macos-compile.patch
  patch -p1 < ../fucking-stop-using-xcode.patch
  patch -p1 < ../backport-503172.patch
}

build() {
  cd ${_pkgfqn}

  ./configure -confirm-license -opensource -v \
    -prefix ${pacman_root}/usr \
    -docdir ${pacman_root}/usr/share/doc/qt \
    -headerdir ${pacman_root}/usr/include/qt \
    -archdatadir ${pacman_root}/usr/lib/qt \
    -datadir ${pacman_root}/usr/share/qt \
    -sysconfdir ${pacman_root}/etc/xdg \
    -examplesdir ${pacman_root}/usr/share/doc/qt/examples \
    -plugin-sql-{psql,mysql,sqlite,odbc} \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -no-rpath \
    -system-harfbuzz \
    -no-mimetype-database \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-strip \
    -no-vulkan \
    -openssl \
    -openssl-linked \
    -no-framework \
    -no-securetransport \
    -system-freetype \
    -system-zlib \
    -system-pcre \
    -pkg-config \
    -glib \
    -icu \
    -no-debug-and-release \
    -L "${pacman_root}/usr/lib" \
    -I "${pacman_root}/usr/include" \
    QMAKE_CC="${pacman_root}/usr/bin/clang" \
    QMAKE_CXX="${pacman_root}/usr/bin/clang++ -std=c++11" \
    QMAKE_LINK="${pacman_root}/usr/bin/clang++"

  # note: patch this after we generate the things.
  echo 'CONFIG += c++11' >> config.tests/icu/icu.pro

# No configure flag for fat static libs with lto
  bin/qmake -- -redo

  make VERBOSE=1 AR="llvm-libtool-darwin -static -o"
}

package_qt5-base() {
  pkgdesc='A cross-platform application and UI framework'
  depends+=(qt5-translations)

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/${pacman_root}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/${pacman_root}/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/${pacman_root}/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/${pacman_root}/usr/bin/$(basename $b)-qt5
  done

  # fix rpaths
  local _f; for _f in $(find "${pkgdir}/${pacman_root}/usr/lib/qt/plugins" -iname "*.dylib"); do
    fix_rpath_v2 "${_f}"
  done
}


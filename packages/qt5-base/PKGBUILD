# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-base
pkgname=(qt5-base)
_basever=5.15.9
pkgver=5.15.9+kde+r155
pkgrel=1
_commit=df809fd0b505b61d718fe1dc7fe19a79f9336cd9
arch=('x86_64' 'arm64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libjpeg-turbo' 'fontconfig' 'sqlite' 'libpng' 'pcre2' 'glib2' 'python' 'zlib' 'icu' 'freetype2')
makedepends=('mariadb-libs' 'unixodbc' 'postgresql-libs' 'git')
optdepends=('qt5-svg: to use SVG icon themes'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'perl: for fixqt4headers and syncqt')
conflicts=('qtchooser')
groups=('qt' 'qt5')
_pkgfqn=${pkgbase/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        qmake-cflags.patch
        qmake-config.patch)
sha256sums=('SKIP'
            '5411edbe215c24b30448fac69bd0ba7c882f545e8cf05027b2b6e2227abc5e78'
            '4abc22150fa3e06b2fdcec32146abc9be4e316692aa4d5bd5aa53b4b726783fa')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  cd ${_pkgfqn}
  patch -p1 < ../qmake-cflags.patch # Use system CFLAGS in qmake
  patch -p1 < ../qmake-config.patch # Don't strip binaries with qmake and use -ltcg, cf. QTBUG-73834
}

build() {
  cd ${_pkgfqn}

  ./configure -confirm-license -opensource -v \
    -prefix ${pacman_root}/usr \
    -docdir ${pacman_root}/usr/share/doc/qt \
    -headerdir ${pacman_root}/usr/include/qt \
    -archdatadir ${pacman_root}/usr/lib/qt \
    -datadir ${pacman_root}/usr/share/qt \
    -sysconfdir ${pacman_root}/etc/xdg \
    -examplesdir ${pacman_root}/usr/share/doc/qt/examples \
    -plugin-sql-{psql,mysql,sqlite,odbc} \
    -pkg-config \
    -fontconfig \
    -system-sqlite \
    -system-zlib \
    -system-pcre \
    -qt-harfbuzz \
    -system-freetype \
    -glib \
    -openssl-linked \
    -nomake examples \
    -no-framework \
    -no-securetransport \
    -no-rpath \
    -no-mimetype-database \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-strip \
    -release

# No configure flag for fat static libs with lto
  bin/qmake CONFIG+="sdk_no_version_check" -- -redo
  make
}

package() {
  pkgdesc='A cross-platform application and UI framework'
  optdepends+=(qt5-translations)

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/${pacman_root}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|${pacman_root}/usr|" \
    "${pkgdir}"/${pacman_root}/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/${pacman_root}/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/${pacman_root}/usr/bin/$(basename $b)-qt5
  done

  # fix rpaths
  local _f; for _f in $(find "${pkgdir}/${pacman_root}/usr/lib/qt/plugins" -iname "*.dylib"); do
    fix_rpath "${_f}"
  done
}




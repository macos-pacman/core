# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=subversion
pkgver=1.14.2
pkgrel=7
pkgdesc="A Modern Concurrent Version Control System"
arch=('x86_64' 'arm64')
url="https://subversion.apache.org/"
license=('APACHE')
depends=('apr' 'apr-util' 'bash' 'expat' 'file' 'gcc-libs' 'glibc' 'libsasl' 'libutf8proc' 'lz4' 'serf' 'sqlite' 'zlib' 'db>=6')
makedepends=('apache' 'python' 'python-py3c' 'perl' 'swig' 'ruby')
optdepends=('bash-completion: for svn bash completion'
            'python: for some hook scripts'
            'ruby: for some hook scripts')
checkdepends=('ruby-test-unit')
provides=('svn')
options=('!makeflags' '!emptydirs')
source=(https://www.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        ruby-frozen-nil.patch
        subversion-1.14.1-python-3.11-build.patch
        subversion-1.14.2-swig-py-Fix-conditionals-by-SWIG-version-and-by-Pyth.patch
)
sha512sums=('20ada4688ca07d9fb8da4b7d53b5084568652a3b9418c65e688886bae950a16a3ff37710fcfc9c29ef14a89e75b2ceec4e9cf35d5876a7896ebc2b512cfb9ecc'
            'SKIP'
            'bb772e55acd9601121ad06b254c364e8d8cf772ca59b8df0cf4c5c5ecba110d4108d0363672f121f770550cdd052802474029e57643258f398aacd2b63ccb898'
            'cad1ef6359c4eecce4fc44fb0c8a01a7b9b94cd8b1b3f9bdd9d72b3591f75bada814a9597cea039c6e045f3f07807f17bd720e27cf7d8a92640bb45faaa6a982'
            '9885226c2eea79712f316194fd9710bd940bce517cb2d3f1fcfbcb1edb8f10ae8496e7d03cd377cb991efa7259ff6930c488c603df206611bae4aa30a62cc596')
validpgpkeys=('19BBCAEF7B19B280A0E2175E62D48FAD16A0DE01'
              '8BC4DAE0C5A4D65F404401074F7DBAA99A59B973'
              'BA3C15B1337CF0FB222BD41A1BCA6586A347943F'
              'E7B2A7F4EC28BE9FF8B38BA4B64FFF1209F9FA74'
              '3D1DC66D6D2E0B9039528138C4A6C625CCC8E1DF'
              'A844790FB5743606EE95920776D788E1ED1A599C'
              '7B8CA7F6451AD89C8ADC077B376A3CFD110B1C95'
              '056F8016D9B87B1BDE41746799EC741B57921ACC'
              '3F8E467CB3366E3013E1120D583F00ADF981C39F'
              '91BFBF4D6956BD5DF7B72D23DFE691AE331BA3DB'
              'EC25FCC105618D04ADB43429C4416167349A3BCB')
# Ben Reser / Stefan Sperling / Branko Čibej / Evgeny Kotkov / Bert Huijben / Philip Martin /
# Stefan Fuhrmann / Stefan Hett (CODE SIGNING KEY) /Nathan Hartman (CODE SIGNING KEY) / James McCoy /
# Mark Phippard

prepare() {
   cd ${pkgname}-${pkgver}
   patch -p1 -i ../ruby-frozen-nil.patch
   rm subversion/bindings/swig/proxy/*.swg
   # fix issues with Python 3.11 and swig
   patch -Np1 -i ../subversion-1.14.1-python-3.11-build.patch
   patch -Np1 -i ../subversion-1.14.2-swig-py-Fix-conditionals-by-SWIG-version-and-by-Pyth.patch

   sed -i.bak -e 's/File.exists?/File.exist?/g' subversion/bindings/swig/ruby/test/test_wc.rb

   ./autogen.sh
}

build() {
   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

   export CFLAGS+=" -Wno-error=int-conversion "
   cd ${pkgname}-${pkgver}
   ./configure --prefix=${pacman_root}/usr \
               --with-apxs \
               --with-swig \
               --enable-optimize \
               --enable-bdb6 \
               --with-apache-libexecdir=${pacman_root}/usr/lib/httpd/modules \
               --with-ruby-sitedir=${pacman_root}/usr/lib/ruby/vendor_ruby \
               --disable-plaintext-password-storage \
               --disable-static \
               --disable-werror

   # prevent excessive overlinking due to libtool
   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

   make LT_LDFLAGS="-L$Fdestdir/${pacman_root}/usr/lib"
   make swig_pydir=$site_packages/libsvn \
     swig_pydir_extra=$site_packages/svn swig-py swig-pl swig-rb
}

check() {
   cd ${pkgname}-${pkgver}
   export LANG=C LC_ALL=C
   make check-swig-py check check-swig-pl check-swig-rb CLEANUP=yes
}

package() {
   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

   cd ${pkgname}-${pkgver}

   export DYLD_LIBRARY_PATH="${pkgdir}"/usr/lib:${DYLD_LIBRARY_PATH}
   make DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=$site_packages/libsvn \
     swig_pydir_extra=$site_packages/svn \
     install install-swig-py install-swig-pl install-swig-rb

   install -dm755 "${pkgdir}"/${pacman_root}/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/${pacman_root}/usr/share/subversion/
   rm "${pkgdir}"/${pacman_root}/usr/share/subversion/hook-scripts/*.in

   install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/${pacman_root}/usr/share/bash-completion/completions/subversion

   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/${pacman_root}/usr/share/bash-completion/completions/${i}
   done
}

# vim: ts=2 sw=2 et:

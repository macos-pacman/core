# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgbase=java-common
pkgname=('java-runtime-common' 'java-environment-common')
pkgver=3
pkgrel=5
pkgdesc='Common files for Java Runtime Environments'
url='https://www.archlinux.org/packages/extra/any/java-common/'
arch=('any')
license=('GPL')
makedepends=('bash' 'findutils' 'coreutils')
checkdepends=('wget')
_shunitver=2.1.7
source=(profile_jre.sh
        bin_archlinux-java
        test_archlinux-java
        https://github.com/kward/shunit2/archive/v${_shunitver}/shunit2-${_shunitver}.tar.gz)
sha256sums=('c1a430947e0ab1fa8787fa32a525ad6a8231d3a567ff1063acbdb9e0a3015732'
            '4bb47e2121d8b41608ddc851a4d6f4da8e207f8bb689149bee263b230f9d3582'
            'a0669bfc1f551a76e95a361311aeadb8e9708f39d9c0ef77f6784f49794f7ee6'
            '41a103aa984bfdeea3fec426edb0b3dda19153a5ce448d4b4bbe52a85c4d7fee')

check() {
  ln -sf shunit2-${_shunitver} shunit2
  bash ./test_archlinux-java
}

package_java-runtime-common() {
  pkgdesc='Common files for Java Runtime Environments'
  backup=(${pacman_root#/}/etc/profile.d/jre.sh)
  depends=('bash' 'findutils' 'coreutils')
  install=install_java-runtime-common.sh

  install -d -m 755 "${pkgdir}/${pacman_root}/usr/lib/jvm/"
  ln -s /dev/null "${pkgdir}/${pacman_root}/usr/lib/jvm/default"
  ln -s /dev/null "${pkgdir}/${pacman_root}/usr/lib/jvm/default-runtime"

  install -D -m 644 profile_jre.sh  "${pkgdir}/${pacman_root}/etc/profile.d/jre.sh"
  install -D -m 755 bin_archlinux-java "${pkgdir}/${pacman_root}/usr/bin/archlinux-java"

  install -d -m 755 "${pkgdir}/${pacman_root}/usr/bin/"

  # Official list of binaries that Java headless runtime packages MUST provide
  # This list is based on Arch Linux' default Java implementation: "OpenJDK 7 Icedtea"
  for b in \
    java keytool orbd pack200 rmid rmiregistry servertool tnameserv unpack200;
  do
    ln -s ${pacman_root}/usr/lib/jvm/default-runtime/bin/${b} "${pkgdir}/${pacman_root}/usr/bin/${b}"
  done
}

package_java-environment-common() {
  pkgdesc='Common files for Java Development Kits'

  install -d "${pkgdir}/${pacman_root}/usr/bin"
  # Official list of supplementary binaries that Java environment packages MUST provide
  # This list is based on Arch Linux' default Java implementation: "OpenJDK 7 Icedtea"
  for b in \
    jar jarsigner javac javadoc javah javap jcmd \
    jconsole jdb jinfo jmap jps jstack jstat \
    jstatd rmic schemagen serialver wsgen wsimport xjc;
  do
    ln -s ${pacman_root}/usr/lib/jvm/default/bin/${b} "${pkgdir}/${pacman_root}/usr/bin/${b}"
  done
}

# vim: ts=2 sw=2 et:

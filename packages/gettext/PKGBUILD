# Maintainer:

pkgname=gettext
pkgver=0.21.1
pkgrel=2
pkgdesc="GNU internationalization library"
url="https://www.gnu.org/software/gettext/"
arch=(x86_64 arm64)
license=(GPL)
groups=(base-devel)
depends=(gcc-libs acl sh libunistring libxml2)
makedepends=(git)
optdepends=('git: for autopoint infrastructure updates')
options=(!docs)
source=(https://ftp.gnu.org/pub/gnu/gettext/$pkgname-$pkgver.tar.gz{,.sig}
        gettext-0.21-disable-libtextstyle.patch
        test-execute.patch)
sha256sums=('e8c3650e1d8cee875c4f355642382c1df83058bd5a11ee8555c0cf276d646d45'
            'SKIP'
            '544ce0589e9c70f4f75d77c76fd36f88d009ac9cfecb4812a67f878e38ac6418'
            'c8e306c426f7eae1c93139b483617410a95622a16b6bc9d0edb9dbabfaea87f0')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno
              '68D94D8AAEEAD48AE7DC5B904F494A942E4616C2'
              '9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible (Open Source Development)

pacman_root=$(pacman-root-dir)

prepare() {
  cd $pkgname-$pkgver

  # Do not build libtextstyle, as it depends on libcroco
  # which is now unmaintained and has known security bugs.
  # patch from Fedora
  patch -p1 -i $srcdir/gettext-0.21-disable-libtextstyle.patch

  # fix test-execute.sh on macos
  patch -Np1 -i $srcdir/test-execute.patch
}

build() {
  cd $pkgname-$pkgver

  # note: we use --with-included-gettext here, so that
  # we include `libintl.h`; this is provided by glibc on linux systems,
  # but it needs to come from gettext on macos.
  ./configure \
    --prefix=${pacman_root}/usr \
    --disable-java \
    --disable-csharp \
    --enable-rpath \
    --with-xz \
    --with-included-gettext \
    --with-included-glib \
    --without-libiconv-prefix \

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' \
      -i.bak gettext-{tools,runtime,runtime/libasprintf}/libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=fakeroot
pkgver=1.33
pkgrel=2
pkgrel+=".1"
pkgdesc='Tool for simulating superuser privileges'
arch=('x86_64' 'arm64')
license=('GPL')
url='https://tracker.debian.org/pkg/fakeroot'
depends=('glibc' 'bash' 'sed')
checkdepends=('sharutils')
source=("https://deb.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz"
        'fix-tests.patch'
        'fix-macos.patch'
        'fix-getopt.patch')
sha256sums=('e157d8e5c64d3a755707791e9be93296c6d249d5c4478bf941b675d49c47757d'
            'f8d1c965c38c778cd18ecc655037c18741e5c30497952ab47e1388c51ececd13'
            '3f6c1bba981c4189eec0d845215fb2951dabb183d23cf0d7f289a0c8cc6eb4a3'
            '9c0a6c75c543abf048f1471516e7a99acac5cf0147cbae4c8dc650a8bb0463c4')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-tests.patch
  patch -Np1 -i ../fix-macos.patch
  patch -Np1 -i ../fix-getopt.patch
}

build() {
  cd $pkgname-$pkgver

  CONFIG_SHELL=${SHELL} ./configure --prefix=${pacman_root}/usr \
    --libdir=${pacman_root}/usr/lib/libfakeroot \
    --with-ipc=sysv

  make
}

check() {
  cd $pkgname-$pkgver

  # some tests will just fail and there's no way around it. if the test uses
  # `chown` or `chmod` or `stat` or whatever, these are apple-signed binaries
  # living in /usr/bin or even /usr/sbin. we have no way to inject libraries into
  # them, so fakeroot cannot intercept shit.
  export DYLD_LIBRARY_PATH="${PWD}/.libs/:${DYLD_LIBRARY_PATH}"
  make check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=glu
pkgver=9.0.3
pkgrel=1
pkgdesc="Mesa OpenGL utility library"
arch=('x86_64' 'arm64')
url="https://gitlab.freedesktop.org/mesa/glu"
license=('LGPL')
depends=('mesa' 'gcc-libs' 'glibc')
makedepends=('meson')
source=(https://mesa.freedesktop.org/archive/glu/glu-$pkgver.tar.xz{,.sig}
        'fix-meson-build.patch')
sha512sums=('b2781059c0e176192c3fc0d7244645020937a463311171efddb9f35fb94ee43faabcf627fa7f429d48fceaf6dd9c5adb69c86c7a21ec4ea490f4ab143d52e3ba'
            'SKIP'
            '99dd8ce2333d995fb121954a7e6c04e5a709846a0c9460c8a18db0d2fac5f35cbd741c114e30e9e0520f3e0b59aa39b5d324123c4996cbb7fce3079142ed50a1')
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
validpgpkeys=('57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>

prepare() {
  cd glu-$pkgver
  patch -Np1 -i ../fix-meson-build.patch
}

build() {
  arch-meson glu-$pkgver build \
    -D default_library=shared

  # Print config
  meson configure build | cat

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

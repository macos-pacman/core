# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=(glibmm glibmm-docs)
pkgver=2.66.6
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=('x86_64' 'arm64')
license=(LGPL)
depends=(glib2 libsigc++)
makedepends=(git meson mm-common perl-xml-parser clang)
checkdepends=(glib-networking)
options=(!emptydirs)
_commit=8dd75b3bf85b3ee7500683c9766412ccd1c1bf29  # tags/2.66.6^0
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd glibmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glibmm
}

build() {
  arch-meson glibmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package_glibmm() {
  depends+=(libsigc-2.0.dylib)
  provides=(libglibmm-2.4.dylib)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/${pacman_root}/usr/share
  mv "$pkgdir"/${pacman_root}/usr/share/{devhelp,doc} docs/${pacman_root}/usr/share/
}

package_glibmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir/"
}

# vim:set sw=2 sts=-1 et:

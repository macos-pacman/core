# Maintainer : RÃ©my Oudompheng <remy@archlinux.org>

pkgname=pyalpm
pkgver=0.10.6
pkgrel=5
pkgdesc="Python 3 bindings for libalpm"
arch=('x86_64' 'arm64')
url="https://gitlab.archlinux.org/archlinux/pyalpm"
license=('GPL')
makedepends=('git' 'python-setuptools' 'python-pytest' 'python-pkgconfig' 'python-pytest-pacman')
depends=('python' 'pacman')
source=("git+https://gitlab.archlinux.org/archlinux/pyalpm.git#tag=$pkgver"
        'fix-paths.patch.in'
        'fix-tests.patch')
validpgpkeys=('E499C79F53C96A54E572FEE1C06086337C50773E')
sha512sums=('SKIP'
            '58c8558aa30b6e83eab02647e0402a217b751c0179dd441cd07dcceaaa88aea18021ff0bbffff485666773fad6267ea3339e033d9453f4b0dffc98394152ca11'
            '9e9345e3c2077f277e587dd3a9cb2eca6ea96e5d0489740845d190cb0c30fd42114dfbb7dbbc07ea9999b20dee3d2cc15eb57cea528930e8c7add41850338d01')

prepare() {
  cd "${pkgname}"

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" \
      -e "s|%%PACMAN_ROOT_REL%%|${pacman_root#/}|g" \
      ../fix-paths.patch.in > fix-paths.patch

  patch -Np1 -i fix-paths.patch
  patch -Np1 -i ../fix-tests.patch
}

build() {
  cd "${pkgname}"
  python setup.py build
}

check() {
  cd "${pkgname}"
  PYTHONPATH="$PWD/build/$(_GetPythonPlatformPath)" pytest \
    -k 'not test_db_grpcache_pkg_segfault and not test_db_read_grp'
}

package() {
  cd "${pkgname}"
  python setup.py install --root=${pkgdir}
}

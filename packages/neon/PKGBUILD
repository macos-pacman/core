# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.33.0
pkgrel=1
pkgrel+=".2"
pkgdesc='HTTP/1.1 and WebDAV client library'
arch=('x86_64' 'arm64')
url='https://notroj.github.io/neon/'
license=('LGPL-2.0-or-later')
depends=('ca-certificates' 'expat' 'glibc' 'krb5' 'openssl' 'zlib')
source=("https://notroj.github.io/neon/$pkgname-$pkgver.tar.gz")
sha256sums=('659a5cc9cea05e6e7864094f1e13a77abbbdbab452f04d751a8c16a9447cf4b8')
validpgpkeys=('190555472DCC589BEF01609C608A86DF9833CC49') # Joe Orton

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=${pacman_root}/usr \
    --enable-shared \
    --with-ssl=openssl \
    --with-ca-bundle=${pacman_root}/etc/ssl/certs/ca-certificates.crt
  make
}

check() {
  cd $pkgname-$pkgver
  make check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

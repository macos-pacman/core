# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=(python-gobject python-gobject-docs)
pkgver=3.44.1
pkgrel=4
pkgdesc="Python bindings for GLib/GObject/GIO/GTK"
url="https://wiki.gnome.org/Projects/PyGObject"
arch=('x86_64' 'arm64')
license=(LGPL)
depends=(
  gobject-introspection-runtime
  python
)
makedepends=(
  git
  gobject-introspection
  meson
  python-cairo
  python-sphinx
  python-sphinx_rtd_theme
)
checkdepends=(
  gtk3
  python-pytest
)
optdepends=('cairo: Cairo bindings')
_commit=1a2544c898ca2aafcab0c9e472be22657f310cf3  # tags/3.44.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/pygobject.git#commit=$_commit"
)
b2sums=('SKIP')

pkgver() {
  cd pygobject
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pygobject
  # https://gitlab.gnome.org/GNOME/pygobject/-/merge_requests/244
  git cherry-pick -n ce5b0a622e376d8ac77ba5e8c3c000a2ebbad073
}

build() {
  arch-meson pygobject build
  meson compile -C build
}

check() {
  PYTEST_ADDOPTS="-v -k 'not test_file_chooser_dialog and not test_path_exists_various_types'" PYGI_TEST_VERBOSE=1 DYLD_LIBRARY_PATH="${pacman_root}/usr/lib" \
    meson test -C build --print-errorlogs
}

package_python-gobject() {
  provides=("pygobject-devel=$pkgver")
  conflicts=(pygobject-devel)
  replaces=('pygobject-devel<=3.36.1-1')

  meson install -C build --destdir "$pkgdir"
  python -m compileall -d ${pacman_root}/usr/lib "$pkgdir/${pacman_root}/usr/lib"
  python -O -m compileall -d ${pacman_root}/usr/lib "$pkgdir/${pacman_root}/usr/lib"
}

package_python-gobject-docs() {
  pkgdesc="Developer documentation for PyGObject"
  depends=()
  optdepends=()

  python -m sphinx -b html pygobject/docs "$pkgdir/${pacman_root}/usr/share/doc/pygobject/html"
  rm -r "$pkgdir/${pacman_root}/usr/share/doc/pygobject/html/.doctrees"
}


# vim:set sw=2 sts=-1 et:

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=2.3.0
pkgrel=2
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://github.com/gorakhargosh/watchdog"
license=('Apache')
depends=('python')
optdepends=('python-yaml: for watchmedo')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-timeout')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('733d408e74a81ce834075472d7488e8d767f80ef92a2a13cc986c1f5d879b0353f0cb85c1996168ae6b8b82d135bcb26c6c18b51a01a8d75915bdec94acd607d')

prepare() {
  cd watchdog-$pkgver
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver

  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  # Invokes sudo
  PYTHONPATH="$PWD/build/lib.macosx-${MACOSX_DEPLOYMENT_TARGET}-$(uname -m)-cpython-${python_version}" pytest \
    --deselect 'tests/test_inotify_buffer.py::test_unmount_watched_directory_filesystem' \
    --deselect 'tests/test_0_watchmedo.py::test_auto_restart_subprocess_termination[False]' \
    --deselect 'tests/test_emitter.py' \
    --deselect 'tests/test_fsevents.py::test_converting_cfstring_to_pyunicode' # doesn't work right
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-tools
_basever=5.15.9
pkgver=5.15.9+kde+r1
pkgrel=1
_commit=5649efd376ed7dbb171905e9edebbd547d1f73eb
arch=('x86_64' 'arm64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Development Tools, QtHelp)'
depends=('qt5-base')
makedepends=('qt5-declarative' 'clang' 'git')
optdepends=('clang: for qdoc')
groups=('qt' 'qt5')
conflicts=('qtchooser')
_pkgfqn=${pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="${pkgdir}" install

  # fix rpath
  fix_rpath "${pkgdir}/${pacman_root}/usr/lib/qt/plugins/designer/libqquickwidget.dylib"

  # move the apps out
  mkdir -p "${pkgdir}/Applications/Qt"
  local _app; for _app in Assistant Designer Linguist pixeltool qdbusviewer; do
    mv "${pkgdir}/${pacman_root}/usr/bin/${_app}.app" "${pkgdir}/Applications/Qt/"
  done

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/${pacman_root}/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/${pacman_root}/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/${pacman_root}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/${pacman_root}/usr/share/licenses
  ln -s ${pacman_root}/usr/share/licenses/qt5-base "$pkgdir"/${pacman_root}/usr/share/licenses/${pkgname}
}

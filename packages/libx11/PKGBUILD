# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libx11
pkgver=1.8.4
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64' 'arm64')
url="https://xorg.freedesktop.org/"
# keep xorgproto runtime dependency
# https://lists.archlinux.org/pipermail/arch-dev-public/2019-December/029767.html
depends=('libxcb' 'xorgproto')
makedepends=('xorg-util-macros' 'xtrans')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.xz{,.sig})
sha512sums=('3150a47498b0cb012482ee02efeaae16d9e736288f2b3f917be912e1613d56ad6b4ab180de8820305deb2b95dfd993633f43a65344d75979d6b86bdf110cb63e'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alanc@freedesktop.org>
#validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>
#validpgpkeys=('995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # Adam Jackson <ajax@nwnk.net>
#validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # "Keith Packard <keithp@keithp.com>"

build() {
  cd libX11-${pkgver}
  ./configure --prefix=${pacman_root}/usr \
    --disable-static \
    --disable-xf86bigfont \
    --enable-unix-transport \
    --enable-tcp-transport \
    --enable-ipv6 \
    --enable-local-transport \
    --enable-loadable-i18n \
    --enable-xthreads \
    --enable-specs=no

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libX11-${pkgver}
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/"
}

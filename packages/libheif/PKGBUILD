# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Adam Fontenot <fontenot@ucla.edu>

pkgname=libheif
pkgver=1.16.1
pkgrel=2
pkgdesc='HEIF file format decoder and encoder'
arch=('x86_64' 'arm64')
url='https://github.com/strukturag/libheif'
license=(GPL3)
makedepends=(cmake libjpeg libpng gdk-pixbuf2 svt-av1 rav1e)
depends=(libde265 x265 aom dav1d)
optdepends=('libjpeg: for heif-convert and heif-enc' 'libpng: for heif-convert and heif-enc'
            'svt-av1: svt-av1 encoder' 'rav1e: rav1e encoder')
source=(https://github.com/strukturag/libheif/releases/download/v$pkgver/libheif-$pkgver.tar.gz
        'fix-macos-compile.patch')
sha256sums=('ac15b54b6d7c315710e156d119b8a1bfc89f29621e99222b2750b1f31c9c3558'
            '7df26e5fe1b12bc228cd3a712903fb805fb906651ca402388871c92e3bc60bb6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-macos-compile.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver\
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DAOM_ENCODER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

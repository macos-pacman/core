# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=hoe
pkgname=ruby-${_gemname}
_gitcommit=c191c3fc4c090df5d743ac751e5a8d191c880f2a
pkgver=4.0.4
pkgrel=1
pkgdesc='Hoe is a rake/rubygems helper for project Rakefiles'
url='https://www.zenspider.com/projects/hoe.html'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('git' 'ruby-rdoc' 'ruby-rake')
checkdepends=('ruby-minitest')
options=('!emptydirs')
source=("git+https://github.com/seattlerb/hoe#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${_gemname}
  grep 'VERSION = "' lib/hoe.rb|sed -E 's|.*"(.+)"|\1|'
}

build() {
  cd ${_gemname}
  rake gem
}

check() {
  cd ${_gemname}
  rake test
}

package() {
  cd ${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/${pacman_root}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.bundle} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:

# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg
pkgver=2.4.1
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
arch=('x86_64' 'arm64')
url='https://www.gnupg.org/'
license=(BSD custom custom:CC0 GPL2 GPL3 LGPL3 LGPL2.1 MIT)
depends=(
  'bzip2'
  'glibc'
  'gnutls'
  'libgcrypt'
  'libgpg-error'
  'libksba'
  'libassuan' 'libassuan.dylib'
  'npth' 'libnpth.dylib'
  'pinentry'
  'readline'
  'sqlite'
  'zlib'
)
checkdepends=('openssh')
optdepends=(
  'libldap: gpg2keys_ldap'
  'libusb-compat: scdaemon'
)
source=(
  "https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
  'drop-import-clean.patch'
)
sha256sums=('76b71e5aeb443bfd910ce9cbc8281b617c8341687afb67bae455877972b59de8'
            'SKIP'
            '7549f20c94a8284ab7377bc1518b57801680abb24a4a1a821f3cc62fb2d2f90f')
validpgpkeys=(
  '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28' # Andre Heinecke (Release Signing Key)
  '6DAA6E64A76D2840571B4902528897B826403ADA' # Werner Koch (dist signing 2020)
  'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD' # Niibe Yutaka (GnuPG Release Key)
  '02F38DFF731FF97CB039A1DA549E695E905BA208' # GnuPG.com (Release Signing Key 2021)
)

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../drop-import-clean.patch

  # improve reproducibility
  rm doc/gnupg.info*
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=${pacman_root}/usr \
    --sysconfdir=${pacman_root}/etc \
    --sbindir=${pacman_root}/usr/bin \
    --libexecdir=${pacman_root}/usr/lib/gnupg \
    --enable-maintainer-mode \
    --enable-all-tests \
    --disable-doc \
    --enable-rpath

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ln -s gpg "${pkgdir}/${pacman_root}"/usr/bin/gpg2
  ln -s gpgv "${pkgdir}/${pacman_root}"/usr/bin/gpgv2

  install -Dm 644 COPYING.{CC0,other} -t "${pkgdir}/${pacman_root}/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:

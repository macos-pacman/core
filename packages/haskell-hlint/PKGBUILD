# Maintainer: zhiayang <zhiayang@nowhere>

_hkgname=hlint
pkgname=haskell-${_hkgname}
pkgver=3.5
pkgrel=2
pkgdesc="Source code suggestions"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64' 'arm64')
depends=('ghc-libs'
  'haskell-process'
  'haskell-filepath'
  'haskell-directory'
  'haskell-containers'
  'haskell-unordered-containers'
  'haskell-vector'
  'haskell-text'
  'haskell-bytestring'
  'haskell-transformers'
  'haskell-file-embed'
  'haskell-utf8-string'
  'haskell-data-default'
  'haskell-cpphs'
  'haskell-cmdargs'
  'haskell-uniplate'
  'haskell-ansi-terminal'
  'haskell-extra'
  'haskell-refact'
  'haskell-aeson'
  'haskell-deriving-aeson'
  'haskell-filepattern'
  'haskell-hscolour'
  'haskell-yaml'
  'haskell-ghc-lib-parser'
)
checkdepends=()
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        "ghc9.6-support.patch::https://patch-diff.githubusercontent.com/raw/ndmitchell/hlint/pull/1470.patch")
sha512sums=('a66503787ac78383d6e5566be3f32c19ab51408ebc2bcc0499d101be67fd0699e6ac9c90efaa142ecdb0216bc2a74d2a120e5ba5861c3d22087b0f5931090bd5'
            '086abac13e2f8e37309122b4235ca8dd5d3a42d27a7d665f72412b7176fa80f37cf62697b4117e87a52d62f23aeeee726dd5a1be184e5112b9cdc9001a562a78')

_build_flags="+gpl"

prepare() {
  cd $_hkgname-$pkgver
  patch -fNp1 -i ../ghc9.6-support.patch || :

  # uusi -u 'ghc-lib-parser' -u 'ghc-lib-parser-ex' -u 'hscolour' ${_hkgname}.cabal
}

# template start; name=haskell-default; version=1.0;
# default haskell build steps

build() {
# template start; name=haskell-build; version=1.0;
  cd $_hkgname-$pkgver

  local _opts
  if (( CHECKFUNC )) && [ -z ${_notests+x} ]; then
    _opts=('--enable-tests')
  else
    _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --disable-static --enable-shared --enable-executable-dynamic \
    --prefix=${pacman_root}/usr \
    --docdir=${pacman_root}/usr/share/doc/$pkgname \
    --datasubdir=$pkgname \
    --dynlibdir='$libdir' \
    -ftemplateHaskell --ghc-option=-fllvm --ghc-option=-fhide-source-paths \
    --flags="${_build_flags:-}" \
    "${_opts[@]}"

  runhaskell Setup build $MAKEFLAGS

  if [ -z ${_no_register+x} ]; then
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script

    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
  fi
# template end;
}

check() {
# template start; name=haskell-check; version=1.0;
  cd $_hkgname-$pkgver
  if [ -z ${_notests+x} ]; then
    if [ -z ${_tests_can_fail+x} ]; then
      runhaskell Setup test --show-details=direct
    else
      runhaskell Setup test --show-details=direct || echo "tests failed!"
    fi
  fi
# template end;
}

package() {
# template start; name=haskell-package; version=1.0;
  cd $_hkgname-$pkgver

  if [ -z ${_no_register+x} ]; then
    install -D -m744 register.sh "$pkgdir/${pacman_root}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "$pkgdir/${pacman_root}/usr/share/haskell/unregister/${pkgname}.sh"
  fi

  local _license_name=${license_name-LICENSE}

  runhaskell Setup copy --destdir="$pkgdir"

  if [ -z ${_no_license+x} ]; then
    install -D -m644 "${_license_name}" "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
    rm "$pkgdir"/${pacman_root}/usr/share/doc/$pkgname/${_license_name}
  fi

  local dl; for dl in ${pkgdir}/${pacman_root}/usr/lib/*.dylib ${pkgdir}/${pacman_root}/usr/bin/* ; do
    [ ! -e "${dl}" ] || fix_rpath "${dl}"
  done
# template end;
}
# template end;

# vim: ts=2 sw=2 et:

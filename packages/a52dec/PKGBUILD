# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=a52dec
pkgver=0.8.0
pkgrel=2
pkgrel+=".4"
pkgdesc='Library for decoding ATSC A/52 (AC-3) audio streams'
arch=('x86_64' 'arm64')
url='https://git.adelielinux.org/community/a52dec/'
license=('GPL-2.0-or-later')
depends=('glibc')
source=("https://distfiles.adelielinux.org/source/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('03c181ce9c3fe0d2f5130de18dab9bd8bc63c354071515aa56983c74a9cffcc9')

build() {
  cd $pkgname-$pkgver
  export CFLAGS="${CFLAGS} -std=gnu89"
  ./configure --prefix=${pacman_root}/usr --enable-shared
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m644 liba52/a52_internal.h "$pkgdir"/${pacman_root}/usr/include/a52dec/
}

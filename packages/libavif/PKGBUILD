# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libavif
pkgver=0.11.1
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=('x86_64' 'arm64')
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
depends=(glibc aom dav1d rav1e svt-av1 zlib libpng libjpeg libyuv)
makedepends=(cmake pkgconf gdk-pixbuf2)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0eb49965562a0e5e5de58389650d434cff32af84c34185b6c9b7b2fccae06d4e')

if [ "$CARCH" = "x86_64" ]; then
  makedepends+=(nasm)
fi

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/
}

# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

_pkg=h5py
pkgname=python-${_pkg}
pkgver=3.9.0
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library"
arch=('x86_64' 'arm64')
url="https://www.h5py.org/"
license=(BSD)
depends=(hdf5 liblzf python-numpy)
makedepends=(cython python-pkgconfig python-setuptools)
checkdepends=(python-pytest python-pytest-mpi python-pytables)
conflicts=(hdf5-openmpi)
source=(https://files.pythonhosted.org/packages/source/h/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('e604db6521c1e367c6bd7fad239c847f53cc46646f2d2651372d05ae5e95f817')
validpgpkeys=(AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7  # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>
              96B7334D7610EE3E68AFFE589E027116943D6A8B) # Thomas A Caswell <tcaswell@bnl.gov> (new key)
# See https://github.com/h5py/h5py/issues/1299 about lack of GPG sigs for recent releases

prepare() {
  cd ${_pkg}-${pkgver}
  # Allow our numpy version
  sed -i 's/numpy ==/numpy >=/g' setup.py
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" setup_build.py

  export HDF5_DIR=${pacman_root}/usr
}

build() {
  cd ${_pkg}-${pkgver}
  H5PY_SYSTEM_LZF=1 python setup.py build
}

check() {
  export H5PY_TEST_CHECK_FILTERS=1
  PYTHONPATH="${PWD}/${_pkg}-${pkgver}/build/$(_GetPythonPlatformPath)" \
    python -m pytest --pyargs h5py -rxXs --color=yes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 licenses/license.txt -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}/
}

# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

shopt -s extglob

pkgbase=python
pkgname=(python python-tests)
pkgver=3.11.6
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="The Python programming language"
arch=('x86_64' 'arm64')
license=('custom')
url="https://www.python.org/"
depends=('bzip2' 'expat' 'gdbm' 'libffi' 'libnsl' 'libxcrypt' 'openssl' 'zlib' 'tzdata' 'readline' 'gnureadline') # note: explicitly depend on gnureadline
makedepends=('tk' 'sqlite')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"{,.asc}
		    "fix-pkg-config.patch"
		    "disable-bad-test.patch"
		    "fix-dyld-paths.patch.in")
sha512sums=('94b1038f6f53de0c44f99f72ed0f2e0791fd9d2a325ae00ba145b2b2c332c27b300b3ea3473017518089478f15e01867b1bb203c16610039cce36f8366de341a'
            'SKIP'
            '3dc3f94928cd10ed97759bc3ffb95f50dfb97e8a4df3587e46789e9c551f5232dd5e1bb329f598949e39c5bd5c472f128ca1d6d0db369c1b497c05ea85f7c88e'
            '7ab5f314bdc866854610731c763e53e173a95af9356112ce15ba633c9f810df9720c27a566d37721c65f29ec4614ddf409ee572d5dd584f9599c16b135a9ed5d'
            'cc9f5e0800cb1fdae5f1dab91b9fb04823e093b1b49434d3a0f19770e2ccf46a1736e17d19848c033c671fe071f8478b255839716532fb3550cab7d71dcb7b10')
validpgpkeys=('0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D'  # Ned Deily (Python release signing key) <nad@python.org>
              'E3FF2839C048B25C084DEBE9B26995E310250568'  # Łukasz Langa (GPG langa.pl) <lukasz@langa.pl>
              'A035C8C19219BA821ECEA86B64E628F8D684696D') # Pablo Galindo Salgado <pablogsal@gmail.com>

pacman_root=$(pacman-root-dir)

prepare() {
  cd Python-${pkgver}

  # FS#23997
  sed -i.bak -e "s|^#.* /usr/local/bin/python|#!${pacman_root}/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, libffi)
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/_ctypes/{darwin,libffi}*

  patch -Np1 -i ../fix-pkg-config.patch
  patch -Np1 -i ../disable-bad-test.patch

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../fix-dyld-paths.patch.in | patch -Np1
}

build() {
  cd Python-${pkgver}

  # PGO should be done with -O3
  CFLAGS="${CFLAGS/-O2/-O3}"

  # Disable bundled pip & setuptools
  ./configure --prefix=${pacman_root}/usr \
              --disable-framework \
              --enable-shared \
              --with-computed-gotos \
              --enable-optimizations \
              --with-lto \
              --with-readline \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip \
              --with-tzpath=/usr/share/zoneinfo \
              CC=clang CXX=clang++

  make EXTRA_CFLAGS="$CFLAGS"
}

check() {
  # test_socket: test.test_socket.RDSTest.testPeek hangs https://bugs.python.org/issue35247
  # test_tk: tkinter.test.test_tkinter.test_colorchooser.DefaultRootTest hangs
  # test_pyexpat: our `debug` implementation rewrites source location, which breaks the build-time
  #               only test test.test_pyexpat.HandlerExceptionTest as it cannot find source file in
  #               the to-be-installed debug package
  # test_socket: https://github.com/python/cpython/issues/79428
  # test_unittest: https://github.com/python/cpython/issues/108927

  cd Python-${pkgver}

  # because there is a folder called 'python' here, we have to call the python using python.exe.
  export DYLD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${DYLD_LIBRARY_PATH}
  export DYLD_FALLBACK_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${DYLD_FALLBACK_LIBRARY_PATH}

  # hmm. '-x test_many_opens' doesn't seem to actually disable the test. this crashes us.
  ${srcdir}/Python-${pkgver}/python.exe -m test.regrtest -v -uall -x test_socket -x test_tk -x test_pyexpat -x test_many_opens || :
}

package_python() {
  optdepends=('python-setuptools: for building Python packages using tooling that is usually bundled with Python'
              'python-pip: for installing Python packages using tooling that is usually bundled with Python'
              'python-pipx: for installing Python software not packaged on Arch Linux'
              'sqlite: for a default database integration'
              'mpdecimal: for decimal'
              'xz: for lzma'
              'tk: for tkinter')
  provides=('python3')
  replaces=('python3')

  cd Python-${pkgver}

  # Hack to avoid building again
  sed -i.bak 's/^all:.*$/all: build_all/' Makefile

  # PGO should be done with -O3
  CFLAGS="${CFLAGS/-O2/-O3}"

  make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" install

  # Why are these not done by default...
  ln -s python3               "${pkgdir}/${pacman_root}"/usr/bin/python
  ln -s python3-config        "${pkgdir}/${pacman_root}"/usr/bin/python-config
  ln -s idle3                 "${pkgdir}/${pacman_root}"/usr/bin/idle
  ln -s pydoc3                "${pkgdir}/${pacman_root}"/usr/bin/pydoc
  ln -s python${_pybasever}.1 "${pkgdir}/${pacman_root}"/usr/share/man/man1/python.1

  chmod a+x ${pkgdir}/${pacman_root}/usr/bin/python-config

  # some useful "stuff" FS#46146
  install -dm755 "${pkgdir}/${pacman_root}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}/${pacman_root}"/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}/${pacman_root}"/usr/lib/python${_pybasever}/Tools/scripts/

  # License
  install -Dm644 LICENSE "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE"

  # Split tests
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/
  rm -fr {ctypes/test,distutils/tests,idlelib/idle_test,lib2to3/tests,sqlite3/test,tkinter/test,unittest/test}
  cd test
  # FS#76193
  rm -r !(support)
}

package_python-tests() {
  pkgdesc="Regression tests packages for Python"
  depends=('python')

  cd Python-${pkgver}

  make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" libinstall
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/
  rm -r !(test|ctypes|distutils|idlelib|lib2to3|sqlite3|tkinter|unittest)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/test
  rm -r support
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/ctypes
  rm -r !(test)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/distutils
  rm -r !(tests)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/idlelib
  rm -r !(idle_test)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/lib2to3
  rm -r !(tests)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/sqlite3
  rm -r !(test)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/tkinter
  rm -r !(test)
  cd "$pkgdir/${pacman_root}"/usr/lib/python*/unittest
  rm -r !(test)
}

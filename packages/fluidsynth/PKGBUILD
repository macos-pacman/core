# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.3.2
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64' 'arm64')
url="https://www.fluidsynth.org/"
license=(LGPL2.1)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  glib2
  sdl2
  portaudio
  libsndfile
  readline
)
makedepends=(
  cmake
)
provides=(
  libfluidsynth.dylib
  soundfont-synthesizer
)
backup=(${pacman_root#/}/etc/conf.d/$pkgname)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('21798b5a80a7edd8ef02b983d9b230af45cc66b98b32d593228e12dbec15b12c6cf6c0f3692c12af66de1ba2049fa9d6ad2b68c7d1579347eec14d24870b0025')
b2sums=('2d88e52223ff9c6bc5bcbcd8b15f30abb12632202b9a5c5eecc5eb95620aeaa2d7d9b80b76e6b26eef95ca92521bdc9e6d539f8235d90ca0429b12bb72db1a6f')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=${pacman_root}/usr
    -D FLUID_DAEMON_ENV_FILE=${pacman_root}/etc/conf.d/fluidsynth
    -D LIB_SUFFIX=""
    -D enable-framework=OFF
    -D enable-portaudio=ON
    -D enable-coreaudio=ON
    -D enable-coremidi=ON
    -D enable-threads=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  make check -k -C build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 build/$pkgname.conf "$pkgdir/${pacman_root}/etc/conf.d/$pkgname"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,{CONTRIBUTING,README}.md,THANKS,TODO} -t "$pkgdir/${pacman_root}/usr/share/doc/$pkgname/"
}

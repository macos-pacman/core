# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=simplecov-html
pkgname=ruby-$_gemname
pkgver=0.12.3
pkgrel=1
pkgdesc='Default HTML formatter for SimpleCov code coverage tool for ruby 2.4+'
arch=(any)
url='https://github.com/simplecov-ruby/'${_gemname}
license=(MIT)
depends=(ruby)
checkdepends=('ruby-rake' 'ruby-simplecov')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c39df80c6b83a374804efe76b225832a6acc0bceddd82f93f540f2c0dceb3f4c')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' "${_gemname}.gemspec"

  rm -v Gemfile*
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=glib-networking
pkgver=2.78.0
pkgrel=1
epoch=1
pkgdesc="Network extensions for GLib"
url="https://gitlab.gnome.org/GNOME/glib-networking"
arch=('x86_64' 'arm64')
pkgrel+=".3"
license=(LGPL-2.1-or-later)
depends=(
  glib2
  gnutls
  gsettings-desktop-schemas
  libproxy
)
makedepends=(
  git
  meson
)
checkdepends=(ca-certificates)
_commit=40c448faf7cf32647d457a8bcda171c3d21bef22  # tags/2.78.0^0
source=("git+https://gitlab.gnome.org/GNOME/glib-networking.git#commit=$_commit"
        'fix-tests.patch')
b2sums=('SKIP'
        'c0806946bfb6f1d406a1a110ebe3538ae591159d815fdced77697872676b422294d18bdf3e302ffb579a593d9a95b471c9b8e37aecd4d188d7756a4df09af4c0')

pkgver() {
  cd glib-networking
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glib-networking

  patch -Np1 -i ../fix-tests.patch
}

build() {
  local meson_options=(
    -D openssl=enabled
  )

  export G_DEBUG="fatal-criticals"
  arch-meson glib-networking build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:

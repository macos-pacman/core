# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Daniel Martí <mvdan@mvdan.cc>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Rémy Oudompheng <remy@archlinux.org>
# Contributor: Andres Perera <andres87p gmail>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Contributor: Christian Himpel <chressie@gmail.com>
# Contributor: Mike Rosset <mike.rosset@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw@gmail.com>
# Contributor: John Luebs <jkluebs@gmail.com>

pkgname=go
pkgver=1.21.1
pkgrel=1
pkgdesc='Core compiler tools for the Go programming language'
arch=('arm64' 'x86_64')
url='https://golang.org/'
license=(BSD)
makedepends=(git go)
options=(!strip staticlibs)
source=(https://go.dev/dl/go${pkgver}.src.tar.gz{,.asc})
validpgpkeys=('EB4C1BFD4F042F6DDDCCEC917721F63BD38B4796')
sha256sums=('bfa36bf75e9a1e9cbbdb9abcf9d1707e479bd3a07880a8ae3564caee5711cb99'
            'SKIP')

build() {
  if [ ${CARCH} = "x86_64" ]; then
    export GOARCH=amd64
    export GOAMD64=v3
  else
    export GOARCH=arm64
  fi

  export GOROOT_FINAL=${pacman_root}/usr/lib/go
  export GOROOT_BOOTSTRAP=${pacman_root}/usr/lib/go

  cd "$pkgname/src"
  ./make.bash -v
}

check() {
  export GO_TEST_TIMEOUT_SCALE=3

  cd $pkgname/src
  ./run.bash --no-rebuild -v -v -v -k
}

package() {
  cd go

  install -d "$pkgdir/${pacman_root}/usr/bin" \
    "$pkgdir/${pacman_root}/usr/lib/go"       \
    "$pkgdir/${pacman_root}/usr/share/doc/go"

  cp -a bin pkg src lib misc api "$pkgdir/${pacman_root}/usr/lib/go"
  cp -r doc/* "$pkgdir/${pacman_root}/usr/share/doc/go"

  ln -sf /${pacman_root}/usr/lib/go/bin/go    "$pkgdir/${pacman_root}/usr/bin/go"
  ln -sf /${pacman_root}/usr/lib/go/bin/gofmt "$pkgdir/${pacman_root}/usr/bin/gofmt"
  ln -sf /${pacman_root}/usr/share/doc/go     "$pkgdir/${pacman_root}/usr/lib/go/doc"

  install -Dm644 VERSION "$pkgdir/${pacman_root}/usr/lib/go/VERSION"

  rm -rf "$pkgdir/${pacman_root}/usr/lib/go/pkg/bootstrap"
  rm -rf "$pkgdir/${pacman_root}/usr/lib/go/pkg/tool/*/api"

  # TODO: Figure out if really needed
  rm -rf "$pkgdir"/${pacman_root}/usr/lib/go/pkg/obj/go-build

  # https://github.com/golang/go/issues/57179
  install -Dm644 go.env "$pkgdir/${pacman_root}/usr/lib/go/go.env"
  install -Dm644 LICENSE "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et

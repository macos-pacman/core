# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Daniel Martí <mvdan@mvdan.cc>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Rémy Oudompheng <remy@archlinux.org>
# Contributor: Andres Perera <andres87p gmail>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Contributor: Christian Himpel <chressie@gmail.com>
# Contributor: Mike Rosset <mike.rosset@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw@gmail.com>
# Contributor: John Luebs <jkluebs@gmail.com>

pkgname=go
epoch=2
pkgver=1.24.1
pkgrel=1
pkgrel+=".2"
pkgdesc='Core compiler tools for the Go programming language'
arch=('x86_64' 'arm64')
url='https://golang.org/'
license=(BSD-3-Clause)
makedepends=(git go)
replaces=(go-pie)
provides=(go-pie)
options=(!strip staticlibs)
source=("https://go.dev/dl/go${pkgver}.src.tar.gz"{,.asc})
validpgpkeys=('EB4C1BFD4F042F6DDDCCEC917721F63BD38B4796')
sha256sums=('8244ebf46c65607db10222b5806aeb31c1fcf8979c1b6b12f60c677e9a3c0656'
            'SKIP')

build() {
  if [ ${CARCH} = "x86_64" ]; then
    export GOARCH=amd64
    export GOAMD64=v3
  else
    export GOARCH=arm64
  fi
  export GOROOT_FINAL=${pacman_root}/usr/lib/go
  export GOROOT_BOOTSTRAP=${pacman_root}/usr/lib/go

  cd "$pkgname/src"
  ./make.bash -v
}

check() {
  export GO_TEST_TIMEOUT_SCALE=3

  cd $pkgname/src
  ./run.bash --no-rebuild -v -v -v -k || :
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/${pacman_root}/usr/bin" \
    "$pkgdir/${pacman_root}/usr/lib/go" \
    "$pkgdir/${pacman_root}/usr/share/doc/go"

  cp -a bin pkg src lib misc api test "$pkgdir/${pacman_root}/usr/lib/go"
  # We can't strip all binaries and libraries,
  # as that also strips some testdata directories and breaks the tests.
  # Just strip the packaged binaries as a compromise.
  strip $STRIP_BINARIES "$pkgdir/${pacman_root}/usr/lib/go"{/bin/*,/pkg/tool/*/*}

  cp -r doc/* "$pkgdir/${pacman_root}/usr/share/doc/go"

  ln -sf ${pacman_root}/usr/lib/go/bin/go "$pkgdir/${pacman_root}/usr/bin/go"
  ln -sf ${pacman_root}/usr/lib/go/bin/gofmt "$pkgdir/${pacman_root}/usr/bin/gofmt"
  ln -sf ${pacman_root}/usr/share/doc/go "$pkgdir/${pacman_root}/usr/lib/go/doc"

  install -Dm644 VERSION "$pkgdir/${pacman_root}/usr/lib/go/VERSION"

  rm -rf "$pkgdir/${pacman_root}/usr/lib/go/pkg/bootstrap" "$pkgdir/${pacman_root}/usr/lib/go/pkg/tool/*/api"

  # TODO: Figure out if really needed
  rm -rf "$pkgdir"/${pacman_root}/usr/lib/go/pkg/obj/go-build

  # https://github.com/golang/go/issues/57179
  install -Dm644 go.env "$pkgdir/${pacman_root}/usr/lib/go/go.env"

  install -Dm644 LICENSE "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et

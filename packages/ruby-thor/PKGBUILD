# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=thor
pkgname=ruby-$_gemname
pkgver=1.2.2
pkgrel=2
pkgdesc='Thor is a toolkit for building powerful command-line interfaces'
arch=(any)
url='https://github.com/rails/'${_gemname}
license=(MIT)
depends=(ruby)
checkdepends=(ruby-rake ruby-childlabor ruby-rspec ruby-rspec-mocks ruby-simplecov ruby-webmock ruby-coveralls_reborn)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f08db22fb045efb43658204b78742fddfc942fb4806b5c84d98b1d434a12379c')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rspec spec
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 *.md -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE* -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

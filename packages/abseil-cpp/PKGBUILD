# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

pkgname=abseil-cpp
pkgver=20230802.1
pkgrel=1
pkgdesc="Collection of C++ library code designed to augment the C++ standard library"
arch=('x86_64' 'arm64')
url='https://abseil.io'
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake' 'gtest')
source=(
  https://github.com/abseil/abseil-cpp/archive/$pkgver/$pkgname-$pkgver.tar.gz
  scoped-mock-log.patch
)
sha256sums=(
  '987ce98f02eefbaf930d6e38ab16aa05737234d7afbab2d5c4ea7adbe50c28ed'
  'a6cbc612a2b96fcbd52d081e03e8581107ceb4827edb19d96510a31c568e1396'
)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../scoped-mock-log.patch # Install target needed by protobuf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -Bbuild \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_SHARED_LIBS=ON \
    -DABSL_BUILD_TEST_HELPERS=ON \
    -DABSL_USE_EXTERNAL_GOOGLETEST=ON \
    -DABSL_FIND_GOOGLETEST=ON \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
  cd build
  cmake --build .
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .
}

# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=bzip2
pkgver=1.0.8
pkgrel=5
pkgdesc="A high-quality data compression program"
arch=('x86_64' 'arm64')
license=('BSD')
url="https://sourceware.org/bzip2/"
depends=('glibc' 'sh')
provides=('libbz2.dylib')
source=(https://sourceware.org/pub/bzip2/$pkgname-$pkgver.tar.gz{,.sig}
        bzip2.pc.in)
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            'SKIP'
            '1c9ed8ac055d5bb717dae79df25e012c24855a242a8882e1ade56fb811ad916a')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

prepare() {
  cd $pkgname-$pkgver
  sed -e "s|@VERSION@|$pkgver|" -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../bzip2.pc.in > bzip2.pc

  sed -i.bak -e 's|$(PREFIX)/man|$(PREFIX)/share/man|g' Makefile
}

build() {
  cd $pkgname-$pkgver

  make bzip2 bzip2recover CC="${CC-clang} $CFLAGS $CPPFLAGS $LDFLAGS"
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver

  make install PREFIX="${pkgdir}/${pacman_root}/usr"

#  install -dm755 "$pkgdir"/${pacman_root}/usr/{bin,lib,include,share/man/man1}
#
#  install -m755 bzip2-shared "$pkgdir"/${pacman_root}/usr/bin/bzip2
#  install -m755 bzip2recover bzdiff bzgrep bzmore "$pkgdir"/${pacman_root}/usr/bin
#  ln -sf bzip2 "$pkgdir"/${pacman_root}/usr/bin/bunzip2
#  ln -sf bzip2 "$pkgdir"/${pacman_root}/usr/bin/bzcat
#
#  install -m644 bzlib.h "$pkgdir"/${pacman_root}/usr/include/
#
#  install -m644 bzip2.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/
#  ln -sf bzip2.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/bunzip2.1
#  ln -sf bzip2.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/bzcat.1
#  ln -sf bzip2.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/bzip2recover.1
#
  install -Dm644 bzip2.pc -t "$pkgdir"/${pacman_root}/usr/lib/pkgconfig
  install -Dm644 LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE
}

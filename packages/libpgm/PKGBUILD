# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Vladimir Kirillov <proger@wilab.org.ua>

pkgname=libpgm
pkgver=5.3.128
_pkgver="${pkgver//./-}"
pkgrel=2
pkgdesc="OpenPGM: implementation of the Pragmatic General Multicast (PGM, RFC3208)"
arch=('x86_64' 'arm64')
url='https://www.freshports.org/net/openpgm/'
license=('LGPL2.1')
depends=('glibc')
makedepends=('python')
options=(!strip)
#source=("http://openpgm.googlecode.com/files/$pkgname-$pkgver.tar.gz")
#source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/openpgm/libpgm-$pkgver.tar.gz")
source=("libpgm-$pkgver.tgz::https://github.com/steve-o/openpgm/archive/release-${_pkgver}.tar.gz"
        "fix-arm-build.patch::https://github.com/steve-o/openpgm/commit/8d507fc0af472762f95da44036fb77662ff4cd2a.patch")
sha256sums=('8d707ef8dda45f4a7bc91016d7f2fed6a418637185d76c7ab30b306499c6d393'
            '323fef0a86484ca0dfdf85b0760accf2715fc836ec764e08006b8ca9176867a1')

prepare() {
  cd $srcdir/openpgm-release-$_pkgver
  patch -Np1 -i ../fix-arm-build.patch

  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  2to3 -wn version_generator.py
  # 5.3.128 is a little broken
  cp openpgm-5.2.pc.in openpgm-5.3.pc.in

  # fix the restrict thing
  local _f
  for _f in include/pgm/{time,tsi,skbuff,messages,gsi,error,socket}.h \
            include/impl/{source,slist,socket,string,receiver,queue,rxw,txw,md5,reed_solomon,indextoaddr,packet_parse,sockaddr,list,inet_network}.h \
            include/impl/{inet_lnaof,histogram,getnodeaddr,hashtable,net,getifaddrs,checksum}.h; do
    sed -i.bak -e 's/restrict/__restrict__/g' ${_f}
  done
}

build() {
  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  ./bootstrap.sh
  ./configure --prefix=${pacman_root}/usr
  make
}

package() {
  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  make prefix="$pkgdir/${pacman_root}/usr" install
}

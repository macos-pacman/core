# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=imagemagick
pkgver=7.1.1.8
pkgrel=1
_relname=ImageMagick-${pkgver%%.*}
_tarname=ImageMagick-${pkgver%.*}-${pkgver##*.}
pkgdesc='An image viewing/manipulation program'
url='https://www.imagemagick.org/'
arch=('x86_64' 'arm64')
license=(custom)
depends=(libltdl lcms2 fontconfig libxext liblqr libraqm libpng libxml2)
optdepends=('ghostscript: PS/PDF support'
            'libheif: HEIF support'
            'libjxl: JPEG XL support'
            'libraw: DNG support'
            'librsvg: SVG support'
            'libwebp: WEBP support'
            'libwmf: WMF support'
            'libxml2: Magick Scripting Language'
            'libzip: OpenRaster support'
            'ocl-icd: OpenCL support'
            'openexr: OpenEXR support'
            'openjpeg2: JPEG2000 support'
            'djvulibre: DJVU support'
            'pango: Text rendering')
options+=(!emptydirs libtool)
backup=(${pacman_root#/}/etc/$_relname/{colors,delegates,log,mime,policy,quantization-table,thresholds,type,type-{dejavu,ghostscript}}.xml)
provides=(libmagick)
makedepends=(ghostscript openexr libwmf librsvg openjpeg2 libraw opencl-headers libwebp libzip libjxl
             ocl-icd ghostpcl ghostxps libheif jbigkit djvulibre)
checkdepends=(gsfonts)
replaces=(imagemagick-doc)
source=(https://imagemagick.org/archive/$_tarname.tar.xz{,.asc}
        arch-fonts.diff)
sha256sums=('05a24995cb4ad1c1185255fa0d65c24842a71972336cda20f9fbd330299d160a'
            'SKIP'
            'a85b744c61b1b563743ecb7c7adad999d7ed9a8af816650e3ab9321b2b102e73')
validpgpkeys=(D8272EF51DA223E4D05B466989AB63D48277377A)  # Lexie Parsimoniae

shopt -s extglob

prepare() {
  mkdir -p docpkg/usr/share

  cd $_tarname

  # Fix up typemaps to match our packages, where possible
  patch -p1 -i ../arch-fonts.diff
}

build() {
  cd $_tarname
  ./configure \
    --prefix=${pacman_root}/usr \
    --sysconfdir=${pacman_root}/etc \
    --enable-shared \
    --disable-static \
    --with-gs-font-dir=${pacman_root}/usr/share/fonts/gsfonts \
    --enable-hdri \
    --enable-opencl \
    --without-gslib \
    --with-djvu \
    --with-jxl \
    --with-lqr \
    --with-modules \
    --with-openexr \
    --with-openjp2 \
    --with-perl \
    --with-perl-options=INSTALLDIRS=vendor \
    --with-rsvg \
    --with-heic \
    --with-webp \
    --with-raw \
    --with-wmf \
    --with-xml \
    --without-x \
    --without-autotrace \
    --without-dps \
    --without-fftw \
    --without-fpx \
    --without-gcc-arch \
    --without-gvc \
    --enable-openmp

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() (
  cd $_tarname
  ulimit -n 4096
  make check
)

package() {
  cd $_tarname
  make DESTDIR="$pkgdir" install

  # find "$pkgdir/${pacman_root}/usr/lib/perl5" -name '*.so' -exec chrpath -d {} +
  rm "$pkgdir"/${pacman_root}/etc/$_relname/type-{urw-base35,windows}.xml
  rm "$pkgdir"/${pacman_root}/usr/lib/*.la

  install -Dt "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname" -m644 LICENSE NOTICE
}

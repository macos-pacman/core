# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcb
pkgver=1.15
pkgrel=2
pkgdesc="X11 client-side library"
arch=('x86_64' 'arm64')
url="https://xcb.freedesktop.org/"
depends=('xcb-proto' 'libxdmcp' 'libxau')
makedepends=('libxslt' 'python' 'xorg-util-macros' 'xorgproto')
license=('custom')
source=(https://xorg.freedesktop.org/archive/individual/lib/$pkgname-$pkgver.tar.xz{,.sig}
		"no-pthread-stubs.patch")
sha512sums=('f6c7d766b7cf9cab6abe948ee424cbae6951dbfe2f76fa58a109cdc05b0f0919dcc4e38176a22ff4b95fc1e38596115317e32ba33b24cae0035a4e30dbcbdad9'
            'SKIP'
            '905eb32464aedd9049e405a06490b08c0171d396559417bd393f4c0a562aa49167511c201c83570207f10dbb86a015c457bb4ebff887a50a12d6c4021922d62e')
#validpgpkeys=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

prepare() {
	cd "$pkgname-$pkgver"
	patch -Np1 -i ../no-pthread-stubs.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=${pacman_root}/usr \
    --disable-static \
    --enable-dri3 \
    --enable-ge \
    --enable-xevie \
    --enable-xprint

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
}

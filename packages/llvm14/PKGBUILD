# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname='llvm14'
pkgver=14.0.6
pkgrel=4
pkgrel+=".1"
arch=('x86_64' 'arm64')
url="https://llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
makedepends=('cmake' 'ninja' 'libffi' 'libedit' 'ncurses' 'libxml2'
             'python')
checkdepends=()
options=('staticlibs' '!lto') # Getting thousands of test failures with LTO
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        llvm-coroutines-ubsan.patch)
sha256sums=('050922ecaaca5781fdf6631ea92bc715183f202f9d2f15147226f023414f619a'
            'SKIP'
            'ee9baf6df05474083857044d92f26f59d3ee709cdf82ba3bdb2792e6645f71d9')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd llvm-$pkgver.src
  mkdir build

  # https://github.com/llvm/llvm-project/issues/49689
  patch -Np2 -i ../llvm-coroutines-ubsan.patch
}

build() {
  cd llvm-$pkgver.src/build

  # Build only minimal debug info to reduce size
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr/lib/llvm14
    -DCMAKE_INSTALL_NAME_DIR=${pacman_root}/usr/lib/llvm14
    -DLLVM_TARGETS_TO_BUILD="all"
    -DLLVM_PARALLEL_LINK_JOBS=1
    -DLLVM_BUILD_LLVM_DYLIB=ON
    -DLLVM_BUILD_TESTS=ON
    -DLLVM_ENABLE_BINDINGS=OFF
    -DLLVM_ENABLE_FFI=ON
    -DLLVM_ENABLE_RTTI=ON
    -DLLVM_INCLUDE_BENCHMARKS=OFF
    -DLLVM_INSTALL_UTILS=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
    -DDEFAULT_SYSROOT=$(xcrun --show-sdk-path)
  )

  cmake .. "${cmake_args[@]}"
  ninja
}

check() {
  cd llvm-$pkgver.src/build
  DYLD_FALLBACK_LIBRARY_PATH=$PWD/lib ninja check || :
}

package() {
  cd $srcdir/llvm-$pkgver.src
  DESTDIR="${pkgdir}" ninja -C build install

  # install versioned binary symlinks to /usr/bin/
  install -d "$pkgdir/${pacman_root}/usr/bin"
  local _binary
  for _binary in "$pkgdir"/${pacman_root}/usr/lib/llvm14/bin/*; do
    local _basename=${_binary##*/}
    ln -s ../lib/llvm14/bin/$_basename "$pkgdir/${pacman_root}/usr/bin/${_basename}-14"
  done

  # symlink versioned dylibs
  ln -s ${pacman_root}/usr/lib/llvm14/libLLVM.dylib ${pkgdir}/${pacman_root}/usr/lib/libLLVM-14.dylib
  ln -s ${pacman_root}/usr/lib/llvm14/libLLVM.dylib ${pkgdir}/${pacman_root}/usr/lib/libLLVM-${pkgver}.dylib

  fix_rpath_v2 $(find ${pkgdir}/${pacman_root}/usr/lib/llvm14 -iname '*.dylib')
  fix_rpath_v2 $(find ${pkgdir}/${pacman_root}/usr/lib/llvm14/bin/ -type f)

  install -Dm644 LICENSE.TXT "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
}


# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.49.93
pkgrel=7
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('x86_64' 'arm64')
license=('GPL')
url="https://clisp.sourceforge.io/"
depends=('readline' 'libsigsegv' 'ffcall' 'libxcrypt' 'libunistring')
provides=('common-lisp')
makedepends=('mercurial')
options=('!makeflags' '!emptydirs')
_rev=b55b8196c9f25428304ec3de87383319fd1f2264
source=(hg+http://hg.code.sf.net/p/clisp/clisp#revision=$_rev
        'fix-macos-compile.patch')
sha256sums=('SKIP'
            'b34a0b9fc8748c488dec08443051b63a752090864b458382c431752c84502884')

prepare() {
  cd $pkgname
  patch -Np1 -i ../fix-macos-compile.patch
}

build() {
  cd $pkgname
  ./configure --prefix=${pacman_root}/usr --with-readline --with-ffcall src

  cd src
  ./makemake --prefix=${pacman_root}/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make FALIGNFLAGS="-falign-functions=16"
}

check() {
  cd $pkgname
  make check || :
}

package() {
  cd $pkgname/src
  make DESTDIR="$pkgdir" install
}

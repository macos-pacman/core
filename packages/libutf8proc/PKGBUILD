# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libutf8proc
pkgver=2.8.0
_sover=2.6.0
pkgrel=1
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=('x86_64' 'arm64')
url='https://github.com/JuliaStrings/utf8proc'
license=(custom)
makedepends=(cmake git ninja)
source=("git+$url#commit=1cb28a66ca79a0845e99433fd1056257456cef8b" # tag: v2.8.0
        $pkgname.pc.in)
b2sums=('SKIP'
        'fe2bc4ae1106f7858d80e2e454b490abfa65617ace22f1c6979fbff5bca47b89cb19b39bc7c4f8788d231413c3691b235d88ab1cf9d7952ec9b7ef636f8a02b1')

prepare() {
  sed -e "s#@VERSION@#$pkgver#" -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" libutf8proc.pc.in > libutf8proc.pc
}

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -D BUILD_SHARED_LIBS=ON \
    -G Ninja \
    -S utf8proc
  ninja -C build
}

package() {
  # The install command does not work for libutf8proc
  #DESTDIR="$pkgdir" ninja -C $pkgname-$pkgver/build install

  cd utf8proc
  install -Dm644 utf8proc.h "$pkgdir/${pacman_root}/usr/include/utf8proc.h"
  install -Dm644 LICENSE.md "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE.md"
  install -Dm644 "$srcdir/$pkgname.pc" "$pkgdir/${pacman_root}/usr/lib/pkgconfig/$pkgname.pc"
  install -Dm644 ../build/$pkgname.${_sover}.dylib "$pkgdir/${pacman_root}/usr/lib/$pkgname.${_sover}.dylib"

  fix_rpath "${pacman_root}" "${pkgdir}" "${pkgdir}/${pacman_root}/usr/lib/${pkgname}.${_sover}.dylib"

  ln -s ${pacman_root}/usr/lib/$pkgname.${_sover}.dylib "$pkgdir/${pacman_root}/usr/lib/$pkgname.dylib"
}

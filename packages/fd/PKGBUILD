# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=fd
pkgver=8.7.0
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find'
arch=('x86_64' 'arm64')
url=https://github.com/sharkdp/fd
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("fd-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('911b9b718f584bed7e569e11f098ede5effdb74822debca677b555e02032844ee7dd5644a5d7ad2d138d358025e08c02590a2ae2e99e8bac4ab1c88835e84c50')

build() {
  cd fd-$pkgver
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

check() {
  cd fd-$pkgver
  cargo test --release --locked
}

package() {
  cd fd-$pkgver
  install -Dm755 target/release/fd "$pkgdir"/${pacman_root}/usr/bin/fd

  make completions
  install -Dm644 autocomplete/fd.bash "$pkgdir"/${pacman_root}/usr/share/bash-completion/completions/fd
  install -Dm644 autocomplete/fd.fish "$pkgdir"/${pacman_root}/usr/share/fish/vendor_completions.d/fd.fish
  install -Dm644 autocomplete/_fd "$pkgdir"/${pacman_root}/usr/share/zsh/site-functions/_fd

  install -Dm644 doc/fd.1 "$pkgdir"/${pacman_root}/usr/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"/${pacman_root}/usr/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/${pacman_root}/usr/share/licenses/fd/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:

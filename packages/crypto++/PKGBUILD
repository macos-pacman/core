# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=8.7.0
pkgrel=1
pkgdesc='A free C++ class library of cryptographic schemes'
arch=('x86_64' 'arm64')
url=https://www.cryptopp.com/
license=(custom)
depends=(gcc-libs)
makedepends=(
  git
  unzip
)
_tag=511806c0eba8ba5b5cedd4b4a814e96df92864a6
source=(git+https://github.com/weidai11/cryptopp.git#tag=${_tag})
b2sums=(SKIP)
validpgpkeys=(B8CC19802062211A508B2F5CCE0586AF1F8E37BD) # Jeffrey Walton <noloader@gmail.com>

pkgver() {
  cd cryptopp
  git describe --tags | sed 's/^CRYPTOPP_//; s/_/./g'
}

build() {
  export CXXFLAGS="$CXXFLAGS -DNDEBUG -fPIC"
  make PREFIX=${pacman_root}/usr -C cryptopp dynamic cryptest.exe libcryptopp.pc
}

check() {
  make PREFIX=${pacman_root}/usr -C cryptopp test
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=${pacman_root}/usr -C cryptopp install
  install -Dm 644 cryptopp/License.txt -t "${pkgdir}"/${pacman_root}/usr/share/licenses/crypto++/

  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm -rf "${pkgdir}"/${pacman_root}/usr/{bin,share/cryptopp}

  fix_rpath "${pkgdir}/${pacman_root}/usr/lib/libcryptopp.dylib"
}

# vim: ts=2 sw=2 et:

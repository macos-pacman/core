# Maintainer: zhiayang <zhiayang@nowhere>

_hkgname=aeson
pkgname=haskell-${_hkgname}
pkgver=2.1.2.1
pkgrel=2
pkgdesc="Fast JSON parsing and encoding"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64' 'arm64')
depends=('ghc-libs'
  'haskell-attoparsec'
  'haskell-base-compat-batteries'
  'haskell-bytestring'
  'haskell-containers'
  'haskell-data-fix'
  'haskell-deepseq'
  'haskell-dlist'
  'haskell-exceptions'
  'haskell-generically'
  'haskell-ghc-prim'
  'haskell-hashable'
  'haskell-indexed-traversable'
  'haskell-onetuple'
  'haskell-primitive'
  'haskell-quickcheck'
  'haskell-scientific'
  'haskell-semialign'
  'haskell-strict'
  'haskell-tagged'
  'haskell-template-haskell'
  'haskell-text'
  'haskell-text-short'
  'haskell-th-abstraction'
  'haskell-these'
  'haskell-time'
  'haskell-unordered-containers'
  'haskell-uuid-types'
  'haskell-vector'
  'haskell-witherable'
)
checkdepends=(
  'haskell-base16-bytestring'
  'haskell-generic-deriving'
  'haskell-quickcheck'
  'haskell-quickcheck-instances'
  'haskell-tasty'
  'haskell-tasty-golden'
  'haskell-tasty-hunit'
  'haskell-tasty-quickcheck'
  'haskell-diff'
)
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('c7e3c5d61ac99b7e66a468cdcd0a20c9d0f42142b2398086aa1a3a992c60814c9d7505e4c15120fb00f0015ab1fb5592d6acdb31995e7af956f2867b95f12a40')

prepare() {
  cd $_hkgname-$pkgver

  uusi                          \
    -u 'OneTuple'               \
    -u 'base-compat-batteries'  \
    -u 'base-orphans'           \
    -u 'base16-bytestring'      \
    -u 'generic-deriving'       \
    -u 'ghc-prim'               \
    -u 'semialign'              \
    -u 'strict'                 \
    -u 'template-haskell'       \
    -u 'these'                  \
    ${_hkgname}.cabal
}

# template start; name=haskell-default; version=1.0;
# default haskell build steps

build() {
# template start; name=haskell-build; version=1.0;
  cd $_hkgname-$pkgver

  local _opts
  if (( CHECKFUNC )) && [ -z ${_notests+x} ]; then
    _opts=('--enable-tests')
  else
    _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --disable-static --enable-shared --enable-executable-dynamic \
    --prefix=${pacman_root}/usr \
    --docdir=${pacman_root}/usr/share/doc/$pkgname \
    --datasubdir=$pkgname \
    --dynlibdir='$libdir' \
    -ftemplateHaskell --ghc-option=-fllvm --ghc-option=-fhide-source-paths \
    --flags="${_build_flags:-}" \
    "${_opts[@]}"

  runhaskell Setup build $MAKEFLAGS

  if [ -z ${_no_register+x} ]; then
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script

    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
  fi
# template end;
}

check() {
# template start; name=haskell-check; version=1.0;
  cd $_hkgname-$pkgver
  if [ -z ${_notests+x} ]; then
    if [ -z ${_tests_can_fail+x} ]; then
      runhaskell Setup test --show-details=direct
    else
      runhaskell Setup test --show-details=direct || echo "tests failed!"
    fi
  fi
# template end;
}

package() {
# template start; name=haskell-package; version=1.0;
  cd $_hkgname-$pkgver

  if [ -z ${_no_register+x} ]; then
    install -D -m744 register.sh "$pkgdir/${pacman_root}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "$pkgdir/${pacman_root}/usr/share/haskell/unregister/${pkgname}.sh"
  fi

  local _license_name=${license_name-LICENSE}

  runhaskell Setup copy --destdir="$pkgdir"

  if [ -z ${_no_license+x} ]; then
    install -D -m644 "${_license_name}" "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
    rm "$pkgdir"/${pacman_root}/usr/share/doc/$pkgname/${_license_name}
  fi

  local dl; for dl in ${pkgdir}/${pacman_root}/usr/lib/*.dylib ${pkgdir}/${pacman_root}/usr/bin/* ; do
    [ ! -e "${dl}" ] || fix_rpath "${dl}"
  done
# template end;
}
# template end;

# vim: ts=2 sw=2 et:

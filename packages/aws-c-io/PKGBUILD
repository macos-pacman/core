# Maintainer: Anatol Pomozov

pkgname=aws-c-io
pkgver=0.13.30
pkgrel=1
pkgrel+=".1"
pkgdesc='AWS SDK module to handle all IO and TLS work for application protocols'
arch=('x86_64' 'arm64')
url='https://github.com/awslabs/aws-c-io'
license=(Apache)
depends=(aws-c-common aws-c-cal s2n-tls)
makedepends=(cmake)
source=(aws-c-io-$pkgver.zip::https://github.com/awslabs/aws-c-io/archive/v$pkgver.zip)
sha256sums=('3488f441ff9acce00658a46708103d160415f96bd53cfe5b32e772418fbf6c31')

build() {
  cd aws-c-io-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-io-$pkgver
  cmake --build build --target test --verbose || :
}

package() {
  cd aws-c-io-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"
}

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgname=python-cffi
pkgver=1.15.1
pkgrel=5
pkgdesc="Foreign Function Interface for Python calling C code"
arch=('x86_64' 'arm64')
url="https://cffi.readthedocs.org/"
license=('MIT')
depends=('libffi' 'python-pycparser')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://foss.heptapod.net/pypy/cffi/-/archive/v$pkgver/cffi-v$pkgver.tar.bz2"
        $pkgname-remove-py-usage.patch
        $pkgname-remove-py-test-usage.patch::https://foss.heptapod.net/pypy/cffi/-/merge_requests/115.patch
        $pkgname-python-3.11.patch::https://foss.heptapod.net/pypy/cffi/-/commit/8a3c2c816d789639b49d3ae867213393ed7abdff.patch
        0003-apple-api.patch
        error-fix.patch
        $pkgname-issue-556.patch::https://foss.heptapod.net/pypy/cffi/-/commit/cf818d0f37b79f3569c413c74c3f803af774cde8.patch
        add-dyld-paths.patch.in)
sha512sums=('4505c1e3206b66fbdf06ba39be6daac14a35f7148cef3156bb2015792cb59c004ea13fe7c7514feb35ecc5e7526a88fc0b3036ae84fd146a0a3b55bc676e8e51'
            '3b3c0b34719704cb593919ed50af1eaee29db45b1e3c942c3ca4e0ceebfd263c0bb9ca3703fae19929ed5622ecb78969d89d2d6e32719c5b67f20e7b55548116'
            'bb5707330a30f9f66ab9bf642b8cc74adee61e3b0f4b5dababde8cc1d5637e946e327e9358dbb83ecad49db4a1457ea76d79b6465708464d3e8571993553410d'
            'c4b675c96f931fb9177701830c41da9354f2e2875d31723a759aa35c4e975003ade23ed72dab18065a3437db660863b2ce4f91c15a24e983cd927426a1fc4199'
            'c617a7b87ff3b91dff3424488349c5d12d3c25b870ab675ed3e3ccb6207400f2c013f1dcf986fa5c66915e943e47b3577e08d8dc7764a096e2d89c1cebec5da3'
            '0412350d5ae62345f23b714645bffe923d156af07a5789ac89624ed564932f136a54f3426a5e3c9c28f0ed15dc7718d50acbeec45c5bf01a9b49b22240e0318a'
            '968c443f56ecc7194f0af8f6214f9b3db1d6010d346fc73ddaa4a5991141c6465c72edf2c3ab6838280a216098ce9a2e5111cb0fcde69b27f217955f47ca7dc8'
            '56eb3d8a773d6fe72d931c087ca06c0f5ac60ba0bb617ed0ded33a2ea7b46e33d28ae9d9d2eccbdf6133aab7e1d538db40c3950d726e71fc5da2ed1a7713ac63')

prepare() {
  cd cffi-v$pkgver
  patch -Np1 -i ../$pkgname-remove-py-test-usage.patch
  patch -Np1 -i ../$pkgname-remove-py-usage.patch
  patch -Np1 -i ../$pkgname-python-3.11.patch
  patch -Np1 -i ../0003-apple-api.patch
  patch -Np1 -i ../error-fix.patch
  patch -Np1 -i ../$pkgname-issue-556.patch

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../add-dyld-paths.patch.in | patch -Np1
}

build() {
  cd "$srcdir"/cffi-v$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/cffi-v$pkgver
  python setup.py pytest
}

package() {
  cd cffi-v$pkgver

  # remove files created during check() for reproducible SOURCES.txt
  rm -rf testing/cffi{0,1}/__pycache__/

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE
}

# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: KokaKiwi <kokakiwi+aur at kokakiwi dot net>

pkgname=cargo-nextest
pkgver=0.9.88
pkgrel=1
pkgrel+=".0"
pkgdesc="A next-generation test runner for Rust."
arch=('x86_64' 'arm64')
url="https://github.com/nextest-rs/nextest"
license=('Apache-2.0' 'MIT')
depends=('cargo' 'gcc-libs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/cargo-nextest-$pkgver.tar.gz")
sha256sums=('77a6029a409f8ab37e5c8bfc31fccc13bc5ea8475ff3309947a5811fdc758ef4')
b2sums=('21168cb72a175cd35210520e04507d0616284c20f71dcd81b0f4a0d9347dc7072f1d943ad00a5d1571733a8a526fd9a92cf42b5e8329b4cdaa569fc60a05fdcf')
options=('!lto')

prepare() {
  mv "nextest-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --package "$pkgname" --no-default-features --features default-no-update
}

check() {
  cd "$pkgname-$pkgver"
  cargo run --release --package cargo-nextest -- nextest run -- --skip test_version_info
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/${pacman_root}/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/${pacman_root}/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:

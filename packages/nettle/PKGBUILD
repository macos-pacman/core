# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=nettle
pkgver=3.8.1
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=('x86_64' 'arm64')
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=('gmp')
provides=('libnettle.dylib' 'libhogweed.dylib')
# checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('364f3e2b77cd7dcde83fd7c45219c834e54b0c75e428b6f894a23d12dd41cbfe'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>



_libs=(libhogweed.6.dylib libnettle.8.dylib)
_exes=(tools/sexp-conv tools/pkcs1-conv tools/nettle-pbkdf2 examples/rsa-{sign,encrypt,decrypt,verify,keygen})


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=${pacman_root}/usr \
    --disable-static
  make

  # we need to change the dylib install names to run tests, because they are already
  # installed with their "final" install name.
  for _e in ${_exes[@]}; do
    for _l in ${_libs[@]}; do
      install_name_tool -change ${pacman_root}/usr/lib/${_l} ${srcdir}/$pkgname-$pkgver/.lib/${_l} ${_e}
    done
  done
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver

  # now that we changed it, change it back...
  for _e in ${_exes[@]}; do
    for _l in ${_libs[@]}; do
      install_name_tool -change ${srcdir}/$pkgname-$pkgver/.lib/${_l} ${pacman_root}/usr/lib/${_l} ${_e}
    done
  done

  make DESTDIR="$pkgdir/" install
}

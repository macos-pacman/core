# Maintainer: zhiayang <zhiayang@nowhere>

_gemname=warning
pkgname=ruby-$_gemname
pkgver=1.3.0
pkgrel=1
pkgdesc="Add custom processing for warnings"
arch=(any)
url='https://github.com/jeremyevans/ruby-'${_gemname}
license=(BSD-2)
depends=(ruby)
checkdepends=(ruby-rake)
options=(!emptydirs)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('eae73d9be1af92293536a396d1d07dc6dc0ebcbfce3f1086a3d27e74acb6710a')

prepare() {
  cd "${srcdir}/ruby-${_gemname}-${pkgver}"
  # sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # purge development dependencies
  sed --in-place '/spec.add_development_dependency/d' "${_gemname}.gemspec"

  rm -fv Gemfile*
}

build() {
  cd "${srcdir}/ruby-${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

check() {
  cd "${srcdir}/ruby-${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/ruby-${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --local --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/${pacman_root}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 *.rdoc -t "${pkgdir}/${pacman_root}/usr/share/doc/${pkgname}"
  install -Dm 644 *LICENSE* -t "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

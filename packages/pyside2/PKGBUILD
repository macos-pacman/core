# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyside2
pkgname=(shiboken2 python-shiboken2 pyside2 pyside2-tools)
_qtver=5.15.10
_clangver=16.0.6
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64' 'arm64')
url='https://www.qt.io'
license=(LGPL)
makedepends=(cmake python-setuptools llvm clang=$_clangver
             qt5-multimedia qt5-tools qt5-sensors qt5-charts qt5-webengine qt5-datavis3d
             qt5-websockets qt5-speech qt5-3d qt5-svg qt5-script qt5-scxml
             qt5-quickcontrols2 qt5-serialport qt5-remoteobjects qt5-xmlpatterns)
optdepends=('qt5-svg: QtSvg bindings'
            'qt5-script: QtScript bindings'
            'qt5-speech: QtTextToSpeech bindings'
            'qt5-websockets: QtWebSockets bindings'
            'qt5-webengine: QtWebEngine bindings'
            'qt5-datavis3d: QtDataVisualization bindings'
            'qt5-scxml: QtScxml bindings'
            'qt5-sensors: QtSensors bindings'
            'qt5-3d: Qt3D bindings'
            'qt5-charts: QtCharts bindings'
            'qt5-tools: QtHelp bindings'
            'qt5-remoteobjects: QtRemoteObjects bindings'
            'qt5-serialport: QtSerialPort bindings'
            'qt5-quickcontrols2: QtQuickControls2 bindings')
_pkgfqn=pyside-setup-opensource-src-$_qtver
source=(https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz
        python3.11.patch
        fix-macos-deployment.patch)
sha256sums=('2af691d3613a41f83a60439b46568fc2c696dbfae42f7cd7b07152d115ead33a'
            'e91549f7d3da412e864af26f604d0023b80233a7165f50fa21e53cbe861db886'
            'f314f3614c3976616cfe27ba59491a87111af580ed139b48963605a26842efb5')

prepare() {
  patch -d $_pkgfqn -p1 < python3.11.patch # Fix build with Python 3.11
  sed -e 's|0x030AFFFF|0x030BFFFF|' -i $_pkgfqn/sources/shiboken2/libshiboken/pep384impl.h

  sed -i.bak -e 's/NPY_ARRAY_UPDATEIFCOPY/NPY_ARRAY_WRITEBACKIFCOPY/g' \
    pyside-setup-opensource-src-${_qtver}/sources/shiboken2/libshiboken/sbknumpyarrayconverter.cpp

  patch -Np1 -d $_pkgfqn < ../fix-macos-deployment.patch
}

build() {
  cmake -B build -S $_pkgfqn \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=${pacman_root}/usr/bin/python
  cmake --build build
}

package_shiboken2() {
  pkgdesc='Generates bindings for C++ libraries using CPython source code'
  depends=(clang=$_clangver llvm libxslt qt5-xmlpatterns)

  DESTDIR="$pkgdir" cmake --install build/sources/shiboken2
# Provided in python-shiboken2
  rm -r "$pkgdir"/${pacman_root}/usr/lib/{python*,libshiboken*}
# Conflicts with shiboken6 and doesn't work anyway
  rm "$pkgdir"/${pacman_root}/usr/bin/shiboken_tool.py
}

package_python-shiboken2() {
  pkgdesc='Python bindings for shiboken2'
  depends=(python)

  DESTDIR="$pkgdir" cmake --install build/sources/shiboken2
# Provided in shiboken2
  rm -r "$pkgdir"/${pacman_root}/usr/{bin,include,lib/{cmake,pkgconfig}}

# Install egg-info
  cd $_pkgfqn
  python setup.py egg_info --build-type=shiboken2
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r shiboken2.egg-info "$pkgdir"/$_pythonpath
}

package_pyside2() {
  pkgdesc='Enables the use of Qt5 APIs in Python applications'
  depends=(python-shiboken2 qt5-declarative)
  optdepends=('qt5-svg: QtSvg bindings'
              'qt5-script: QtScript bindings'
              'qt5-speech: QtTextToSpeech bindings'
              'qt5-websockets: QtWebSockets bindings'
              'qt5-webengine: QtWebEngine bindings'
              'qt5-datavis3d: QtDataVisualization bindings'
              'qt5-scxml: QtScxml bindings'
              'qt5-sensors: QtSensors bindings'
              'qt5-3d: Qt3D bindings'
              'qt5-x11extras: QtX11Extras bindings'
              'qt5-charts: QtCharts bindings'
              'qt5-tools: QtHelp bindings'
              'qt5-remoteobjects: QtRemoteObjects bindings'
              'qt5-serialport: QtSerialPort bindings'
              'qt5-quickcontrols2: QtQuickControls2 bindings')
  provides=(qt5-python-bindings)

  DESTDIR="$pkgdir" cmake --install build/sources/pyside2
# Install egg-info
  cd $_pkgfqn
  python setup.py egg_info --build-type=pyside2
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r PySide2.egg-info "$pkgdir"/$_pythonpath
}

package_pyside2-tools() {
  pkgdesc='Tools for PySide2'
  depends=(pyside2)

  DESTDIR="$pkgdir" cmake --install build/sources/pyside2-tools
  rm -f "$pkgdir"/${pacman_root}/usr/bin/{rcc,uic,designer,pyside_tool.py} # provided by qt5-base
}

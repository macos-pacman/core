# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=re2
_re2ver=2023-03-01
pkgrel=2
pkgver=${_re2ver//-}
pkgdesc="Fast, safe, thread-friendly regular expression engine"
arch=('x86_64' 'arm64')
url="https://github.com/google/re2"
license=('BSD')
depends=('gcc-libs')
makedepends=('sed')
provides=('libre2.dylib')
source=(re2-$pkgver.tar.gz::https://github.com/google/re2/archive/$_re2ver.tar.gz)
sha256sums=('7a9a4824958586980926a300b4717202485c4b4115ac031822e29aa4ef207e48')

build() {
  cd $pkgname-$_re2ver
  make
}

check() {
  cd $pkgname-$_re2ver
  make test
}

package() {
  cd $pkgname-$_re2ver
  make prefix=${pacman_root}/usr DESTDIR="$pkgdir" SED_INPLACE="sed -i" install
  install -Dm644 LICENSE "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/LICENSE"

  # totally borked
  fix_rpath "${pkgdir}/${pacman_root}/usr/lib/libre2.10.0.0.dylib"
}

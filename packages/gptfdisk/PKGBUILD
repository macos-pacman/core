# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Hokum <hokum_at_mail_dot_ru>

pkgname=gptfdisk
pkgver=1.0.9
pkgrel=2
pkgdesc="A text-mode partitioning tool that works on GUID Partition Table (GPT) disks"
arch=('x86_64' 'arm64')
url="https://www.rodsbooks.com/gdisk/"
license=('GPL2')
depends=('gcc-libs' 'popt' 'libncursesw.dylib')
provides=("gdisk=$pkgver")
conflicts=('gdisk')
replaces=('gdisk')
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        popt-1.19.patch
        popt-1.19-follow-up.patch
        util-linux-2.38.patch
        mac-ncurses.patch
        no-arch-flags.patch)
sha256sums=('dafead2693faeb8e8b97832b23407f6ed5b3219bc1784f482dd855774e2d50c2'
            'ac8ee9b46a089855535c483a93fd51badecdd9028b32c7cc561c5863f52f79ca'
            '644dc3e2c5812c12bdfd3bb174d220cd3af8903fb1baafe8181bf9dfa303b78c'
            'a79206e1e2c57ba74cdeed0f47eeb79919b99159d95c4b42b54e709a29b1af65'
            '65b465f110a10adb82ccd2bd0f6cfe7e20372ab3f0e3b51387459be3eeee18cb'
            '27a438ba9342d30bc27f217b648409ed0bcc7b9f880382077b960170152ad85d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../popt-1.19.patch
  patch -Np1 -i ../util-linux-2.38.patch

  # https://sourceforge.net/p/gptfdisk/code/merge-requests/28/
  patch -Np1 -i ../popt-1.19-follow-up.patch

  # https://sourceforge.net/p/gptfdisk/discussion/939590/thread/bc29ca06f8/
  sed -i '/^#include /s:ncursesw/::' gptcurses.cc

  # wtf
  patch -Np1 -i ../mac-ncurses.patch

  # don't touch my flags
  patch -Np1 -i ../no-arch-flags.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test || :
}

package () {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/${pacman_root}"/usr/{bin,share/{doc/gdisk,man/man8}}
  install -t "$pkgdir/${pacman_root}/usr/bin" {,c,s}gdisk fixparts
  install -m644 -t "$pkgdir/${pacman_root}/usr/share/man/man8" {{,c,s}gdisk,fixparts}.8
  install -m644 -t "$pkgdir/${pacman_root}/usr/share/doc/gdisk" README NEWS
}

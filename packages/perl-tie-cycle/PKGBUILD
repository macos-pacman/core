# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-tie-cycle
_dist=Tie-Cycle
pkgver=1.227
pkgrel=2
pkgdesc="Cycle through a list of values via a scalar"
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/${_dist}-${pkgver}.tar.gz)
md5sums=('3d5004bd8e6f41345091a697673879b7')
sha256sums=('7838335791e71a3b33b8a19de3052949e1890a4823def6397823c99222fa1dd8')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

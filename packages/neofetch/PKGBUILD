# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Dylan Araps <dylan.araps@gmail.com>

pkgname=neofetch
pkgver=7.1.0
pkgrel=2
pkgdesc="A CLI system information tool written in BASH that supports displaying images."
arch=('any')
url="https://github.com/dylanaraps/neofetch"
license=('MIT')
depends=('bash')
makedepends=('git')
backup=(${pacman_root#/}/'etc/neofetch/config.conf')
optdepends=(
  'catimg: Display Images'
  'chafa: Image to text support'
  'feh: Wallpaper Display'
  'imagemagick: Image cropping / Thumbnail creation / Take a screenshot'
  'jp2a: Display Images'
  'libcaca: Display Images'
  'nitrogen: Wallpaper Display'
  'w3m: Display Images'
)
_commit="60d07dee6b76769d8c487a40639fb7b5a1a7bc85"
source=("git+https://github.com/dylanaraps/neofetch.git#commit=${_commit}?signed"
        'pacman-on-macos.patch')
validpgpkeys=("7C20ADCD35D9797789B6BCC046D62DD9F1DE636E") # Dylan Araps
sha256sums=('SKIP'
            '1c4a77323c9397e75b89c632da02e66c7be9ae05c58e00850a53b54e8c5b3d4d')

prepare() {
  cd "$pkgname"
  patch -Np1 -i ../pacman-on-macos.patch
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" PREFIX=${pacman_root}/usr install
  install -Dm644 LICENSE.md "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE.md"
}

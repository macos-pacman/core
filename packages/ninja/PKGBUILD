# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja
pkgver=1.11.1
pkgrel=3
pkgdesc='Small build system with a focus on speed'
arch=('x86_64' 'arm64')
url='https://ninja-build.org/'
license=(Apache)
depends=(gcc-libs)
makedepends=(python re2c)
source=($pkgname-$pkgver.zip::https://github.com/martine/ninja/archive/v$pkgver.zip)
sha1sums=('fbcb284b7280335835c3f085fb825e6f1514396e')
sha256sums=('03b7ea14bf04b87cbdd64b9a1262577093e177f8e5662515de77f3a796b75951')

build() {
  cd ninja-$pkgver

  python configure.py --bootstrap
}

check() {
  cd ninja-$pkgver

  python ./configure.py
  ./ninja ninja_test
  ./ninja_test
}

package() {
  cd ninja-$pkgver

  install -m755 -D ninja "$pkgdir/${pacman_root}/usr/bin/ninja"
  install -m644 -D doc/manual.asciidoc "$pkgdir/${pacman_root}/usr/share/doc/ninja/manual.asciidoc"
  install -Dm644 COPYING "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/COPYING"

  install -m644 -D misc/ninja.vim "$pkgdir/${pacman_root}/usr/share/vim/vimfiles/syntax/ninja.vim"

  install -m644 -D misc/bash-completion "$pkgdir/${pacman_root}/usr/share/bash-completion/completions/ninja"
  install -m644 -D misc/zsh-completion "$pkgdir/${pacman_root}/usr/share/zsh/site-functions/_ninja"
}

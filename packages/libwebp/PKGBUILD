# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=libwebp
pkgver=1.3.0
pkgrel=2
pkgdesc="WebP library and conversion tools"
url="https://developers.google.com/speed/webp/"
arch=('x86_64' 'arm64')
license=(BSD)
depends=(libpng libjpeg libtiff giflib)
makedepends=(cmake ninja)
provides=(libwebp{,decoder,demux,mux}.dylib libsharpyuv.dylib)
source=(https://storage.googleapis.com/downloads.webmproject.org/releases/webp/libwebp-$pkgver.tar.gz{,.asc})
sha256sums=('64ac4614db292ae8c5aa26de0295bf1623dbb3985054cb656c55e67431def17c'
            'SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D') # WebP release signing key

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr
    -DCMAKE_BUILD_TYPE=Release
    -DBUILD_SHARED_LIBS=ON
    -DWEBP_BUILD_VWEBP=OFF
    -DWEBP_BUILD_EXTRAS=OFF
  )

  # Mimic autotools build without --enable-asserts
  CFLAGS+=" -DNDEBUG"
  CXXFLAGS+=" -DNDEBUG"

  cmake -S libwebp-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  fix_rpath "${pacman_root}" "${pkgdir}" "${pkgdir}/${pacman_root}/usr/bin/gif2webp"
  install -Dt "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname" -m644 \
    libwebp-$pkgver/COPYING
}

# vim:set sw=2 sts=-1 et:

# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=tk
pkgver=8.6.13
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64' 'arm64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('tcl')
options=('staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
sha256sums=('2e65fa069a23365440a3c56c556b8673b5e32a283800d8d9b257e3f584ce0675')

pacman_root=$(pacman-root-dir)

build() {
  cd tk$pkgver/unix
  ./configure \
    --prefix=${pacman_root}/usr \
    --mandir=${pacman_root}/usr/share/man \
    --enable-threads --disable-rpath \
    --enable-aqua \
    --without-x
  make
}

check() {
  cd tk$pkgver/unix
  make test
}

package() {
  cd tk$pkgver/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/${pacman_root}/usr/bin/wish"
  ln -sf libtk${pkgver%.*}.dylib "${pkgdir}/${pacman_root}/usr/lib/libtk.dylib"
  install -Dm644 license.terms "${pkgdir}/${pacman_root}/usr/share/licenses/${pkgname}/LICENSE"

  # install private headers (FS#14388, FS#47616)
  cd ..
  for dir in compat generic generic/ttk unix; do
    install -dm755 "${pkgdir}/${pacman_root}"/usr/include/tk-private/$dir
    install -m644 -t "${pkgdir}/${pacman_root}"/usr/include/tk-private/$dir $dir/*.h
  done

  # remove buildroot traces
  sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i.bak "${pkgdir}/${pacman_root}/usr/lib/tkConfig.sh"
  rm -f ${pkgdir}/${pacman_root}/usr/lib/*.bak
}

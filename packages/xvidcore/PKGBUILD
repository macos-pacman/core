# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=xvidcore
pkgver=1.3.7
pkgrel=2
pkgdesc='XviD is an open source MPEG-4 video codec'
arch=('x86_64' 'arm64')
url=https://www.xvid.com/
license=(GPL)
depends=(glibc)
makedepends=()
provides=(libxvidcore.dylib)
source=(https://downloads.xvid.com/downloads/xvidcore-${pkgver}.tar.gz)
sha256sums=('abbdcbd39555691dd1c9b4d08f0a031376a3b211652c0d8b3b8aa9be1303ce2d')

if [ $CARCH = "x86_64" ]; then
  makedepends+=(nasm)
fi

build() {
  cd xvidcore/build/generic

  ./configure \
    --prefix=${pacman_root}/usr
  make
}

package() {
  cd xvidcore/build/generic

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

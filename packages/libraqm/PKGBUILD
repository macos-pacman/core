# Maintainer:
# Contributor: Daniel Bermond < yahoo-com: danielbermond >

pkgname=libraqm
pkgver=0.10.1
pkgrel=1
pkgdesc='A library that encapsulates the logic for complex text layout'
arch=('x86_64' 'arm64')
url='https://github.com/HOST-Oman/libraqm/'
license=(MIT)
depends=(freetype2 fribidi harfbuzz)
makedepends=(meson gtk-doc)
source=(https://github.com/HOST-Oman/libraqm/releases/download/v$pkgver/raqm-$pkgver.tar.xz)
sha256sums=('4d76a358358d67c5945684f2f10b3b08fb80e924371bf3ebf8b15cd2e321d05d')

build() {
  arch-meson build raqm-$pkgver \
    -D docs=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -D -m644 raqm-$pkgver/COPYING "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE
}

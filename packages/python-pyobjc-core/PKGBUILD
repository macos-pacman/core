pkgname=python-pyobjc-core
pkgver=9.2
pkgrel=1
pkgdesc="The Python <-> Objective-C Bridge with bindings for macOS frameworks"
url="https://github.com/ronaldoussoren/pyobjc"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer')
checkdepends=('python-pytest' 'python-pyobjc-framework-cocoa' 'python-pyobjc-framework-quartz')
source=("https://files.pythonhosted.org/packages/source/p/pyobjc-core/pyobjc-core-$pkgver.tar.gz"
        'fix-testcase-runner.patch'
        'fix-tests.patch')
sha256sums=('d734b9291fec91ff4e3ae38b9c6839debf02b79c07314476e87da8e90b2c68c3'
            '316078f5a3d9adcaffc69101a1f185132b5aae3aceb5769dff81446d09f97bad'
            '500794f3013624e25686a0c9eb0d96455eecad1f0c3ce95292b784364f7ebb8b')

export CFLAGS=${CFLAGS/-O2/-O3}
export CXXFLAGS=${CXXFLAGS/-O2/-O3}

prepare() {
  # this depends on some apple-libffi-specific nonsense function
  # see: https://github.com/macports/macports-ports/commit/217d8731dcc6247f67004de48d3f4760b07e33db

  # solution: force it to link explicitly against the apple one, not our one.
  cd pyobjc-core-${pkgver}
  sed -i.bak -e "s|-lffi|$(xcrun --show-sdk-path)/usr/lib/libffi.tbd|g" setup.py

  patch -Np1 -i ../fix-testcase-runner.patch

  # some other stuff is broken
  patch -Np1 -i ../fix-tests.patch

  # this test is broken
  rm -f Examples/NonFunctional/RemotePyInterpreter/test_client.py
}

build() {
  cd pyobjc-core-${pkgver}
  python -m build -wn
}

check() {
  cd pyobjc-core-${pkgver}

  # some tests fail but i can't be bothered to debug them right now
  PYTHONPATH="${PWD}/build/$(_GetPythonPlatformPath):${PYTHONPATH}" \
    python setup.py test || :
}

package() {
  cd pyobjc-core-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

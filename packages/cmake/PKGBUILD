# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=cmake
pkgver=3.28.1
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=('x86_64' 'arm64')
pkgrel+=".3"
url="https://www.cmake.org/"
license=('custom')
depends=('curl' 'libarchive' 'jsoncpp' 'libjsoncpp.dylib' 'libuv' 'rhash' 'cppdap' 'libiconv')
makedepends=('python-sphinx')
optdepends=('qt6-base: cmake-gui')
source=("https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz"
        "toolchain.patch"
        "default.cmake.in")
sha512sums=('62693c423dd46d252d046662ac34cf9c927b7ec23c213c6b5cd1683636b8bbb5326698af1e7f851f3c01fe3f14dd769b9a8bd405f8f9e73e6d8defae7517cbcb'
            'd34e4c271c7ce55dddb87cfc6ca918d2b970253ace26584b1942933d3adc0ccb4092792aca6c8d59b33bb9111df169715bb91fe3ed1b802b7589c9ce95a66bb5'
            '76d1fd0dbf7c680c5b571e40ecc8a0665fb7851b811fd6853e2176180aacb6baa91d98a12480e9a968dfa11fb6ad7e7f980302fe05d13c89eda8084837869994')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../toolchain.patch

  sed "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../default.cmake.in > default.cmake
}

build() {
  cd ${pkgname}-${pkgver}
  ./bootstrap --prefix=${pacman_root}/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --datadir=/share/cmake \
    --system-libs \
    --sphinx-man \
    --sphinx-html \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN) \
    -- \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DCMAKE_TOOLCHAIN_FILE="" \
    -DCMAKE_RANLIB="/usr/bin/ranlib" \
    -DCMAKE_AR="/usr/bin/ar"

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/${pacman_root}/usr/share/doc/cmake/html/_sources

  install -Dm644 Copyright.txt "${pkgdir}/${pacman_root}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 default.cmake "${pkgdir}/${pacman_root}/usr/share/cmake/"
}

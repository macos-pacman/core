# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=cmake
pkgver=3.28.3
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=('x86_64' 'arm64')
pkgrel+=".11"
url="https://www.cmake.org/"
license=('custom')
depends=('curl' 'libarchive' 'jsoncpp' 'libjsoncpp.dylib' 'libuv' 'rhash' 'cppdap' 'libiconv')
makedepends=('python-sphinx' 'nlohmann-json')
optdepends=('qt6-base: cmake-gui')
source=("https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz"
        "toolchain.patch"
        "default.cmake.in")
sha512sums=('66e923925b764e1fe3d150c69dab3e0abd9e0c90d8e30cab63c3a1f70c3e37df0a5e3ff12b378eeae3bdc6608495f41399e6f81602e26b513b19fa19ff6c48fc'
            'd34e4c271c7ce55dddb87cfc6ca918d2b970253ace26584b1942933d3adc0ccb4092792aca6c8d59b33bb9111df169715bb91fe3ed1b802b7589c9ce95a66bb5'
            'b991ae8d936291e203f83e01aab1bfe5ec7b51cf00c96c922caafa6d44d97900f56ef6d722af04e795dea7c75bbecd9c9e9dde7466b4b1574e85de088b61dbcb')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../toolchain.patch

  sed "s|%%PACMAN_ROOT%%|${pacman_root}|g" ../default.cmake.in > default.cmake
}

build() {
  cd ${pkgname}-${pkgver}
  ./bootstrap --prefix=${pacman_root}/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --datadir=/share/cmake \
    --system-libs \
    --sphinx-man \
    --sphinx-html \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN) \
    -- \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DCMAKE_TOOLCHAIN_FILE="" \
    -DCMAKE_RANLIB="/usr/bin/ranlib" \
    -DCMAKE_AR="/usr/bin/ar"

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/${pacman_root}/usr/share/doc/cmake/html/_sources

  install -Dm644 Copyright.txt "${pkgdir}/${pacman_root}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 default.cmake "${pkgdir}/${pacman_root}/usr/share/cmake/"
}

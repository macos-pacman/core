# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.28.3
pkgrel=1
pkgdesc="C++ bindings for ATK"
url="https://www.gtkmm.org/"
arch=('x86_64' 'arm64')
license=(LGPL)
depends=(atk glibmm)
makedepends=(git meson mm-common glibmm-docs)
options=(!emptydirs)
_commit=f925dec906324b724b9309726a2c40086ced430a  # tags/2.28.3^0
source=("git+https://gitlab.gnome.org/GNOME/atkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd atkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd atkmm
}

build() {
  arch-meson atkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_atkmm() {
  depends+=(libsigc-2.0.dylib libglibmm-2.4.dylib)
  provides=(libatkmm-1.6.dylib)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/${pacman_root}/usr/share
  mv "$pkgdir"/${pacman_root}/usr/share/{devhelp,doc} docs/${pacman_root}/usr/share/
}

package_atkmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir/"
}

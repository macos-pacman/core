# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=5.0.0
_commit=0d2b1e4585cddd22780d60422116ddbfa9de9b8c
pkgrel=3
pkgdesc="Generic Transparent Proxies"
arch=('x86_64' 'arm64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
makedepends=('git')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("git+https://github.com/zopefoundation/zope.proxy.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd zope.proxy
  python setup.py build
}

check() {
  cd zope.proxy
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.macosx-${MACOSX_DEPLOYMENT_TARGET}-$(uname -m)-cpython-${python_version}:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:

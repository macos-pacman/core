# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgbase=openblas
pkgname=(openblas openblas64)
_pkgname=OpenBLAS
pkgver=0.3.23
pkgrel=1
_blasver=3.9.0
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('x86_64' 'arm64')
url="https://www.openblas.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake' 'perl' 'gcc-fortran')
#checkdepends=('cblas')
source=(${_pkgname}-v${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha512sums=('ea64c693e57eb63cc2a88e4d6ab2d8cf9ab84ae6a15048fb12090a3570dd41053e62e99c1ff9d3e02dd67ca93233591ab41b8c017d06585d0a69222e1ad3023f')

build() {
  local _target_cpu
  if [ "${CARCH}" = "x86_64" ]; then
    _target_cpu="HASWELL"
  else
    # apple m1+
    _target_cpu="VORTEX"
  fi

  export CC=gcc
  export GXX=g++
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITHOUT_LAPACK=ON \
    -DBUILD_WITHOUT_CBLAS=ON \
    -DBUILD_TESTING=OFF \
    -DNO_AFFINITY=ON \
    -DUSE_OPENMP=1 \
    -DNO_WARMUP=1 \
    -DTARGET=${_target_cpu} \
    -DNUM_THREADS=64 \
    -DDYNAMIC_ARCH=OFF
  cmake --build build

  cmake -B build64 -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=${pacman_root}/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITHOUT_LAPACK=ON \
    -DBUILD_WITHOUT_CBLAS=ON \
    -DBUILD_TESTING=OFF \
    -DNO_AFFINITY=ON \
    -DUSE_OPENMP=1 \
    -DNO_WARMUP=1 \
    -DTARGET=${_target_cpu} \
    -DNUM_THREADS=64 \
    -DINTERFACE64=1 \
    -DDYNAMIC_ARCH=OFF

  cmake --build build64
}

check() {
  cd $_pkgname-$pkgver
  # TODO
  #make CFLAGS="$CPPFLAGS $CFLAGS -lcblas" TARGET=CORE2 tests
}

package_openblas() {
  provides=("blas=$_blasver")
  conflicts=('blas')

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname-$pkgver/LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/${pacman_root}//usr/lib/
  ln -s libopenblas.${pkgver%.*}.dylib libblas.dylib
  ln -s libopenblas.${pkgver%.*}.dylib libblas.3.dylib
  ln -s openblas.pc "$pkgdir"/${pacman_root}/usr/lib/pkgconfig/blas.pc
}

package_openblas64() {
  pkgdesc+=" (64-bit integers)"
  provides=("blas64=$_blasver")
  conflicts=('blas64')

  DESTDIR="$pkgdir" cmake --install build64
  install -Dm644 $_pkgname-$pkgver/LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/${pacman_root}/usr/lib/
  ln -s libopenblas_64.${pkgver%.*}.dylib libblas64.dylib
  ln -s libopenblas_64.${pkgver%.*}.dylib libblas64.3.dylib
  ln -s libopenblas_64.${pkgver%.*}.dylib libopenblas64_.dylib # Needed by julia
  ln -s openblas64.pc "$pkgdir"/${pacman_root}/usr/lib/pkgconfig/blas64.pc
}

# vim:set ts=2 sw=2 et:

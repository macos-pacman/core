# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=pacman
pkgver=7.0.0.r6.gc685ae6
pkgrel=2
pkgrel+=".7"
# use annotated tag and patch level commit from release branch (can be empty for no patches)
_git_tag=7.0.0
_git_patch_level_commit=c685ae6412af04cae1eaa5d6bda8c277c7ffb8c8
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64' 'arm64')
url="https://www.archlinux.org/pacman/"
license=('GPL-2.0-or-later')
depends=(
  bash
  coreutils
  curl
  gawk
  gettext
  glibc
  gnupg
  gpgme
  grep
  libarchive
)
makedepends=(
  asciidoc
  doxygen
  git
  meson
  pkgconf
)
checkdepends=(
  python
)
optdepends=(
  'perl-locale-gettext: translation support in makepkg-template'
)
options=('!strip')
provides=('libalpm.dylib')
backup=(${pacman_root#/}/etc/pacman.conf
        ${pacman_root#/}/etc/makepkg.conf
        ${pacman_root#/}/etc/makepkg.conf.d/rust.conf)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=("git+https://gitlab.archlinux.org/pacman/pacman.git#tag=v${_git_tag}?signed"
        revertme-makepkg-remove-libdepends-and-libprovides.patch::https://gitlab.archlinux.org/pacman/pacman/-/commit/354a300cd26bb1c7e6551473596be5ecced921de.patch
        pacman.conf
        makepkg.conf
        alpm.sysusers
        rust.conf
        'macos-bsd-touch-date.patch'
        'macos-no-statvfs.patch'
        'better-purge.patch'
        'writeable-pkg-dir.patch'
        'show-exec-error.patch'
        'no-fakeroot.patch'
        'macos-bsd-wc.patch'
        'macos-bsd-checksums.patch'
        'makepkg-rpath-fixer.patch'
        'makepkg-rpath-check.patch'
        'mac_checksums.sh.in'
        'check_rpath.sh.in'
        'fix_rpath.sh.in'
        'fix-bin-true.patch'
        'fix-test-fakeroot-path.patch'
        'macos-bsd-cp.patch'
        'macos-no-chroot.patch'
        'pacman-support-oci.patch'
        'pacman-support-oci-fix.patch'
        'makepkg-check-only-option.patch'
        'makepkg-msg3-etc.patch'
        'fix-makepkg-profile-path.patch'
        'pacman-support-oci-multipart.patch'
        'pacman-support-oci-multipart-p2.patch'
        'pacman-support-oci-multipart-p3.patch'
        'makepkg-dylib-provides.patch'
)
sha256sums=('SKIP'
            'b3bce9d662e189e8e49013b818f255d08494a57e13fc264625f852f087d3def2'
            'ecdefd7d41ee74c2602a9de8bb28d6377da916d14b4853dc303da0841237ce06'  # pacman.conf
            'cf34c33dc11169b8def0720836eb06acce902cebffddafe28522a9fe30d279c5'  # makepkg.conf
            'c8760d7ebb6c9817d508c691c67084be251cd9c8811ee1ccf92c1278bad74c1c'
            '84d101019d24e6280ede705b04456f088fb13b9fe6afc34de457e0af34ec09d3'  # rust.conf

            '5cd2645f982489e55c090716ec695ea0427bbf2bd200374508230cb5287903ae'  # macos-bsd-touch-date.patch
            'ff6c9766bf2e6391df3d581754b1299f3be7461c7e3cd66e10be6e1414ebf1cc'  # macos-no-statvfs.patch
            '22388ffa08d57934f64e93e3ba63675330ca6cc58c3af62e08f5a7a244b7a45e'  # better-purge.patch
            '2f13fe60de9368008e8bed6df0a238e5fff3c8ecb3dadf91e1b168b5ddc755fb'  # writeable-pkg-dir.patch
            '7478cb6c7b22bf9f3cb3224c1ff9571c96a956660409b5e3e96be36d6afff467'  # show-exec-error.patch
            '626376190b48667128cc667e78f20d713c5a708a7dcb711a3ed272ec89aabbd4'  # no-fakeroot.patch
            'cabf1f38abaed55d47f5967b64bf1c169cbfed02843c1f49439cc00932a743b7'  # macos-bsd-wc.patch
            '66ae375d6186a85e4feaf85b3c0a4a2fe37746c55ceb7134ca5f7a5e9e07d1ce'  # macos-bsd-checksums.patch
            '4f48a60e2035fcdc07032ef7fed1833cf58783bd013d0f8d9d9d7da0bcd82564'  # makepkg-rpath-fixer.patch
            '6b66c29a0a15689952e7ebb79df7b2a39123d9de6d51f150dc4cc48e739814c9'  # makepkg-rpath-check.patch
            'd9a18c1f9a9595b3ca2be6131eea8448420efaf34fc2e740e8d8ec00571a40e4'  # mac_checksums.sh.in
            '316e47603a6f8be41acd8d66a13fd682bc8ba84479107b3fc8d471eb1e2f1685'  # check_rpath.sh.in
            '486f0e618ffb44f1f3277aa18e6cf47f6bd285b97753a7e4f770928ebf4c4419'  # fix_rpath.sh.in
            '69b76b9a2eab66a3d9219d32f379cad426678de6c18485fab132348930f36ac9'  # fix-bin-true.patch
            'dc5e13d965f4c1e12630ba24601a223b908f12fe19d5e3287d7176369acb8fe4'  # fix-test-fakeroot-path.patch
            'fbd50755b9fd9ae11874fd83c94f5f922655e6256b527e4ba61be0e2c6157967'  # macos-bsd-cp.patch
            '5e84539f3c0690be6405e4170b35f1c3b3841fef11070b5002b6868218d5bd71'  # macos-no-chroot.patch
            '411f0ca9d469f2638143885e39bd29458161c9c000915cfb02e3146567b4c701'  # pacman-support-oci.patch
            '6382cc91cd21d78fe64497cadbfd446346726e35d1e09aaa88a5a75804b7bf4e'  # pacman-support-oci-fix.patch
            '223f4d6c5b8c2b99abaa8cf23a2f181e6ad2979e40fd21d85643153ff4d98c7f'  # makepkg-check-only-option.patch
            'ba2ff8714768ddd5870a4e8c16f647f490a2bb7db03e7ca41650b504ffadf3bb'  # makepkg-msg3-etc.patch
            '581fe381088a9a226638e8570700ff99809d1035a2a402af058beb0c9e40625c'  # fix-makepkg-profile-path.patch
            '3c66170a3974c6ac32a2f7a0624de31ef5cdabd89d414ee65a5b0ae9640e14e5'  # pacman-support-oci-multipart.patch
            '5001c17ba032b21c24f34c55e8b4f453385eff617ce8f9a83d157a9651f096d7'  # pacman-support-oci-multipart-p2.patch
            'e2a30b6b9a9780d63cda36b27e97271b593056257623c5dd3bf595e2cb0abeb0'  # pacman-support-oci-multipart-p3.patch
            '3d49f017f5ebe775d02e774e6472ca2301a01adfca63295d761c5e9da743f759'  # makepkg-dylib-provides.patch
)

pkgver() {
  cd "$pkgname"
  git describe --abbrev=7 --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"

  # apply patch level commits on top of annotated tag
  if [[ -n ${_git_patch_level_commit} ]]; then
    if [[ v${_git_tag} != $(git describe --tags --abbrev=0 "${_git_patch_level_commit}") ]] then
      error "patch level commit ${_git_patch_level_commit} is not a descendant of v${_git_tag}"
      exit 1
    fi
    git rebase "${_git_patch_level_commit}"
  fi

  # handle patches
  local -a patches
  patches=($(printf '%s\n' "${source[@]}" | grep '.patch'))
  patches=("${patches[@]%%::*}")
  patches=("${patches[@]##*/}")

  if (( ${#patches[@]} != 0 )); then
    for patch in "${patches[@]}"; do
      if [[ $patch =~ revertme-* ]]; then
        msg2 "Reverting patch $patch..."
        patch -RNp1 < "../$patch"
      else
        msg2 "Applying patch $patch..."
        patch -Np1 < "../$patch"
      fi
    done
  fi

  # patch is dumb
  cp ../fix_rpath.sh.in scripts/libmakepkg/util/
  cp ../mac_checksums.sh.in scripts/libmakepkg/util/
  cp ../check_rpath.sh.in scripts/libmakepkg/lint_package/
}

build() {
  cd "$pkgname"

  meson --prefix=${pacman_root}/usr \
        --localstatedir=${pacman_root}/var \
        --sysconfdir=${pacman_root}/etc \
        --default-library=static \
        --prefer-static \
        --buildtype=release \
        -Ddoc=disabled \
        -Ddoxygen=disabled \
        -Dscriptlet-shell=${pacman_root}/usr/bin/bash \
        build

  meson compile -C build
}

check() {
  cd "$pkgname"

  meson test -C build || :
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/${pacman_root}/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/${pacman_root}/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/${pacman_root}/etc"
  install -m644 "$srcdir/rust.conf" "$pkgdir/${pacman_root}/etc/makepkg.conf.d"
}

# vim: set ts=2 sw=2 et:

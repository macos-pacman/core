# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgbase=libxslt
pkgname=(
  libxslt
  libxslt-docs
)
pkgver=1.1.42
pkgrel=2
pkgrel+=".1"
pkgdesc="XML stylesheet transformation library"
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis/home"
arch=('x86_64' 'arm64')
license=(MIT)
depends=(
  bash
  glibc
  libgcrypt
  libxml2
  xz
)
makedepends=(
  git
  python
  pkgconf
  autoconf
  libtool
  automake
)
checkdepends=(
  docbook-xml
)
source=(
  "git+https://gitlab.gnome.org/GNOME/libxslt.git#tag=v$pkgver"
  0001-runtest-Print-output-causing-failure.patch
)
b2sums=('70549d4ad6784c41bc8f947db0f689399a0c6b4a1d91e1a8e67772f942192a391eebc58b64069500dd76b4c06bc4bd682b66945112298c7bc3f1af90e41ce804'
        'b63a1f2bf704d4561cc30734cee980534fb0586234b7b593d57d9c70fe59abd72610b6d825ab136d4d1dd7540bbfb6b8a6edc95618c15cc79e5a4bbd8f6341ee')

prepare() {
  cd libxslt

  # Make test failures easier to fix
  git apply -3 ../0001-runtest-Print-output-causing-failure.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=${pacman_root}/usr
    --sysconfdir=${pacman_root}/etc
    --localstatedir=${pacman_root}/var
    --disable-static
    --with-python=${pacman_root}/usr/bin/python
  )

  cd libxslt
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libxslt
  make check
}

package_libxslt() {
  optdepends=('python: Python bindings')
  provides=(lib{,e}xslt.dylib)

  cd libxslt
  make DESTDIR="$pkgdir" install

  mkdir -p ../doc/${pacman_root}/usr/share
  mv "$pkgdir"/${pacman_root}/usr/share/{doc,gtk-doc} ../doc/${pacman_root}/usr/share/

  install -Dm644 Copyright -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
}

package_libxslt-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"

  install -Dm644 libxslt/Copyright -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:

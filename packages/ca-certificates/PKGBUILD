pkgname=ca-certificates
pkgver=2023_12_12
pkgrel=6
pkgdesc="Common CA Certificates"
arch=(any)
license=(MIT)
url="https://curl.se/docs/caextract.html"
depends=(p11-kit)
source=("https://curl.se/ca/cacert-${pkgver//_/-}.pem"
        'update-ca-trust'
        '40-update-ca-trust.hook.in')
sha256sums=('ccbdfc2fe1a0d7bbbb9cc15710271acf1bb1afe4c8f1725fe95c4c7733fcbe5a'
            '69498ba260fefdda18e8e21d9f9c8219adf7aa02f5a11f96cb80102b11a4f942'
            '2156fccdadeebdb656b3a5f6133f88c4092ef347ab1ad79ec600c7e620681ba3')

macos_system_certs=(
	/Library/Keychains/System.keychain
	/System/Library/Keychains/SystemRootCertificates.keychain
)

extract_cert_file() {
	echo "extracting $1"

	/usr/bin/security find-certificate -a -p < $1 > all-$(basename $1).pem
	split -p "-----BEGIN CERTIFICATE-----" all-$(basename $1).pem cert-$(basename $1)-
}

build() {
	cd $srcdir

	for cert in "${macos_system_certs[@]}"; do
		extract_cert_file $cert
	done

	valid_certs=()
	for cert_file in $srcdir/cert-*; do
		# only include certs that haven't expired
		if openssl x509 -inform pem -checkend 0 -noout < $cert_file; then
			# only include certs that are meant as SSL roots
			# if openssl x509 -inform pem -purpose -noout < $cert_file | grep -qs "SSL server CA : Yes"; then
				valid_certs+=($cert_file)
			# fi
		fi
	done

	echo "found ${#valid_certs[@]} system certificates"

	# only add trusted certs
	# macos doesn't trust its own certs,
	# so maybe skip this.

#	trusted_certs=()
#	for cert_file in "${valid_certs[@]}"; do
#		if /usr/bin/security verify-cert -c $cert_file; then
#			echo $cert_file is trusted
#		else
#			echo $cert_file is not trusted
#		fi
#	done

	echo > "final-certs.pem"
	cat cacert-${pkgver//_/-}.pem > "final-certs.pem"
	for cert in "${valid_certs[@]}"; do
		cat $cert >> final-certs.pem
	done
}

package() {
	mkdir -p $pkgdir/${pacman_root}/etc/ssl/certs
	cp $srcdir/final-certs.pem $pkgdir/${pacman_root}/etc/ssl/cert.pem

	ln -sf ../cert.pem $pkgdir/${pacman_root}/etc/ssl/certs/cert.pem
	ln -sf ../cert.pem $pkgdir/${pacman_root}/etc/ssl/certs/ca-certificates.crt

  sed -e "s|%%PACMAN_ROOT_REL%%|${pacman_root#/}|g" \
      -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" 40-update-ca-trust.hook.in > 40-update-ca-trust.hook

  install -Dt "$pkgdir/${pacman_root}/usr/bin" -m755 update-ca-trust
  install -Dt "$pkgdir/${pacman_root}/usr/share/libalpm/hooks" -m644 *.hook
}

# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-dist-checkconflicts
pkgver=0.11
pkgrel=9
pkgdesc="Declare version conflicts for your dist"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-list-moreutils' 'perl-exporter')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-fatal' 'perl-module-runtime')
url="https://metacpan.org/release/Dist-CheckConflicts"
source=(https://search.cpan.org/CPAN/authors/id/D/DO/DOY/Dist-CheckConflicts-$pkgver.tar.gz)
sha256sums=('ea844b9686c94d666d9d444321d764490b2cde2f985c4165b4c2c77665caedc4')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps            \
      PERL_MM_OPT="INSTALLDIRS=vendor"       \
      PERL_MB_OPT="--installdirs vendor"     \
      MODULEBUILDRC=/dev/null
    cd Dist-CheckConflicts-$pkgver

    perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Dist-CheckConflicts-$pkgver

    make test
  )
}

package() {
  cd Dist-CheckConflicts-$pkgver

  make DESTDIR="$pkgdir"/ install
}

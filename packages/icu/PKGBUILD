# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>

pkgname=icu
pkgver=72.1
pkgrel=2
pkgdesc="International Components for Unicode library"
arch=('x86_64' 'arm64')
url="https://icu.unicode.org"
license=('custom:icu')
depends=('gcc-libs' 'sh')
makedepends=('python')
provides=(libicu{data,i18n,io,test,tu,uc}.dylib)
source=(https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/${pkgname}4c-${pkgver//./_}-src.tgz{,.asc})
# https://github.com/unicode-org/icu/releases/download/release-71-1/SHASUM512.txt
sha512sums=('848c341b37c0ff077e34a95d92c6200d5aaddd0ee5e06134101a74e04deb08256a5e817c8aefab020986abe810b7827dd7b2169a60dacd250c298870518dcae8'
            'SKIP')
#validpgpkeys=('BA90283A60D67BA0DD910A893932080F4FB419E3') #  "Steven R. Loomis (filfla-signing) <srloomis@us.ibm.com>"
#validpgpkeys+=('9731166CD8E23A83BEE7C6D3ACA5DBE1FD8FABF1') #  "Steven R. Loomis (ICU Project) <srl@icu-project.org>"
#validpgpkeys+=('FFA9129A180D765B7A5BEA1C9B432B27D1BA20D7') # "Fredrik Roubert <fredrik@roubert.name>"
#validpgpkeys+=('E4098B78AFC94394F3F49AA903996C7C83F12F11') # "keybase.io/srl295 <srl295@keybase.io>"
#validpgpkeys+=('4569BBC09DA846FC91CBD21CE1BBA44593CF2AE0') # "Steven R. Loomis (codesign-qormi) <srloomis@us.ibm.com>"
#validpgpkeys=('0E51E7F06EF719FBD072782A5F56E5AFA63CCD33') #"Craig Cornelius (For use with ICU releases) <ccornelius@google.com>"
validpgpkeys=('3DA35301A7C330257B8755754058F67406EAA6AB') # Craig Cornelius <ccornelius@google.com>


build() {
  cd icu/source
  ./configure --prefix=${pacman_root}/usr \
    --enable-icu-config \
    --enable-rpath \
	--sysconfdir=${pacman_root}/etc \
	--mandir=${pacman_root}/usr/share/man \
	--sbindir=${pacman_root}/usr/bin

  make

	# the makefile is broken
	chmod 755 config/icu-config
}

check() {
  cd icu/source

  make -k check
}

package() {
  cd icu/source
  make -j1 DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 "${srcdir}"/icu/LICENSE "${pkgdir}/${pacman_root}"/usr/share/licenses/icu/LICENSE
}

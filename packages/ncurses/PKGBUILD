# Maintainer:  David Runge <dvzrv@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=6.4_20230520
pkgrel=1
pkgdesc='System V Release 4.0 curses emulation library'
arch=(x86_64)
url='https://invisible-island.net/ncurses/ncurses.html'
license=(MIT)
depends=(glibc gcc-libs)
makedepends=(autoconf-archive)
optdepends=('bash: for ncursesw6-config')
provides=(libncurses++w.dylib libformw.dylib libmenuw.dylib libpanelw.dylib libncursesw.dylib)
source=(
  https://invisible-mirror.net/archives/$pkgname/current/$pkgname-${pkgver/_/-}.tgz{,.asc}
  $pkgname-6.3-libs.patch
  $pkgname-6.3-pkgconfig.patch
)
sha512sums=('bbb1dd33f4c701165b7612657a7577d3175b94c5815a89eaab3a8774510feb9322d284896dba8d3896f6dd89a8b98031829a76f7e3397aebb6cf2a71e989f93c'
            'SKIP'
            'adb02b838c40f1e58a1b31c26d5cd0f2a1c43f3b6d68e839981764c0f6c905a9eb51dd36ff018628fdeb20747cc7467727d57135408ab4848259384077a52b28'
            '2d2c0ec3c880e638ab4aa3dbff5e28e4cd233153e24816bd87e077f848aa3edd5114cd0f2a7f6e8869dd1861a2746e512886c18264ff1676927dcc320c5ef958')
b2sums=('d5e88198140ab6471a36bef08685e17ac2e43b4e66b53abaa961d5f8912eb3eae3784db194436c2b3e5f2deaeefaaed93e0550bcab0456f1486fc35d0cdac440'
        'SKIP'
        '31bb10e82dd018a75e57252052650d9f0f5eb5e7e887118c2ea40032b11f59ec6aa4d9bae804c615cbecdf3382f3434e0c9e9e8440fdefe66a507be020b8965c'
        'fb6cf606cf3db7f6b306272696a63bce83d52cfa91e850f9a7bdb9d3d8455a26943529a9cf79731dddc7f763c27211a9afab9c4c31dbb6d12fd720eb390eb0a3')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03')  # Thomas Dickey <dickey@invisible-island.net>

prepare() {
  # do not link against test libraries
  patch -Np1 -d $pkgname-${pkgver/_/-} -i ../$pkgname-6.3-libs.patch
  # do not leak build-time LDFLAGS into the pkgconfig files:
  # https://bugs.archlinux.org/task/68523
  patch -Np1 -d $pkgname-${pkgver/_/-} -i ../$pkgname-6.3-pkgconfig.patch
  # NOTE: can't run autoreconf because the autotools setup is custom and ancient
}

build() {
  local configure_options=(
    --prefix=${pacman_root}/usr
    --disable-root-access
    --disable-root-environ
    --disable-setuid-environ
    --enable-widec
    --enable-pc-files
    --enable-symlinks
    --enable-overwrite
    --enable-database
    --with-terminfo-dirs=${pacman_root}/usr/share/terminfo
    --mandir=${pacman_root}/usr/share/man
    --with-cxx-binding
    --with-cxx-shared
    --with-manpage-format=normal
    --with-pkg-config-libdir=${pacman_root}/usr/lib/pkgconfig
    --with-shared
    --without-ada
  )

  cd $pkgname-${pkgver/_/-}
  ./configure "${configure_options[@]}"
  make
}

package() {
  local _pkgver=${pkgver/_*/}

  make DESTDIR="$pkgdir" install -C $pkgname-${pkgver/_/-}
  install -vDm 644 $pkgname-${pkgver/_/-}/COPYING -t "$pkgdir/${pacman_root}/usr/share/licenses/$pkgname/"

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    ln -sv lib${lib}w.dylib "$pkgdir/${pacman_root}/usr/lib/lib${lib}.dylib"
    ln -sv ${lib}w.pc "$pkgdir/${pacman_root}/usr/lib/pkgconfig/${lib}.pc"
  done

  # some packages look for -lcurses during build
  ln -sv libncursesw.dylib "$pkgdir/${pacman_root}/usr/lib/libcursesw.dylib"
  ln -sv libncurses.dylib "$pkgdir/${pacman_root}/usr/lib/libcurses.dylib"

  # tic and ticinfo functionality is built in by default
  # make sure that anything linking against it links against libncursesw.dylib instead
  for lib in tic tinfo; do
    ln -sv libncursesw.${_pkgver:0:1}.dylib "$pkgdir/${pacman_root}/usr/lib/lib${lib}.dylib"
    ln -sv libncursesw.${_pkgver:0:1}.dylib "$pkgdir/${pacman_root}/usr/lib/lib${lib}.${_pkgver:0:1}.dylib"
    ln -sv ncursesw.pc "$pkgdir/${pacman_root}/usr/lib/pkgconfig/${lib}.pc"
  done
}

# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd <judd@archlinux.org>

pkgname=nano
pkgver=7.2
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64' 'arm64')
license=('GPL')
url="https://www.nano-editor.org"
depends=('ncurses' 'file' 'sh')
backup=('etc/nanorc')
source=(https://www.nano-editor.org/dist/v7/${pkgname}-${pkgver}.tar.xz{,.asc}
        "pkgbuild.nanorc")
sha256sums=('86f3442768bd2873cec693f83cdf80b4b444ad3cc14760b74361474fc87a4526'
            'SKIP'
            '302e04c65a9d81b298957cd735ae8575f60a28a8a7b153da54c50d1b8833a130')
#validpgpkeys=('8DA6FE7BFA7A418AB3CB2354BCB356DF91009FA7') # "Chris Allegretta <chrisa@asty.org>"
#validpgpkeys=('A7F6A64A67DA09EF92782DD79DF4862AF1175C5B') # "Benno Schulenberg <bensberg@justemail.net>"
#validpgpkeys=('BFD009061E535052AD0DF2150D28D4D2A0ACE884') # "Benno Schulenberg <bensberg@telfort.nl>"
validpgpkeys=('168E6F4297BFD7A79AFD4496514BBE2EB8E1961F') # "Benno Schulenberg <bensberg@telfort.nl>" 

pacman_root=$(pacman-root-dir)

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=${pacman_root}/usr \
    --sysconfdir=${pacman_root}/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -DTm644 ${srcdir}/pkgbuild.nanorc ${pkgdir}/${pacman_root}/usr/share/nano/pkgbuild.nanorc
  install -DTm644 "${srcdir}"/${pkgname}-${pkgver}/doc/sample.nanorc "${pkgdir}/${pacman_root}"/etc/nanorc
}

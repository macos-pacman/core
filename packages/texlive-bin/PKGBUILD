# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: francois <francois.archlinux.org>

pkgname=(texlive-bin libsynctex)
pkgver=2025.2
pkgrel=3
pkgrel+=".4"
license=(GPL-2.0-or-later)
arch=('x86_64' 'arm64')
makedepends=(bash
             cairo
             clisp
             ffcall
             fontconfig
             freetype2
             gcc-libs
             gd
             git
             glibc
             gmp
             graphite
             harfbuzz
             icu
             libjpeg-turbo
             libpaper
             libpng
             libsigsegv
             libunistring
             libx11
             libxaw
             libxcrypt
             libxmu
             libxpm
             libxt
             mpfr
             ncurses
             perl
             pixman
             potrace
             readline
             subversion
             zlib
             zziplib)
url='https://tug.org/texlive/'
_commit=8115c4d97aa0e68da1f957a56bca780449d1be59
source=(git+https://github.com/Tex-Live/texlive-source.git#commit=$_commit
        ptex-debug-print.patch
        lua-root.patch)
sha256sums=('842fc0a45cb4d363253eefcaae1f88be7b8446def80d78cce251dad32d3802ec'
            'aa838f09003c62c2efb5770a8de66f99b409df049fbd65098d80fd1957d06c50'
            'ea2f731b39f8945f13e9a5e796bbb41a7efe007473b8344d07fd63f2a277f2e2')

prepare() {
  local p1="s|%%PACMAN_ROOT%%|${pacman_root}|g"
  local p2="s|%%PACMAN_ROOT_REL%%|${pacman_root#/}|g"

  local _s; for _s in "${sources[@]}"; do
    [[ -f "${_s}" ]] && sed -i -e "$p1" -e "$p2" "${_s}"
  done

  cd texlive-source

# bibtex-x needs kpathsea flags
  sed -i '/AC_SEARCH_LIBS/a KPSE_KPATHSEA_FLAGS' texk/bibtex-x/configure.ac
  (cd texk/bibtex-x && autoreconf)
# t4ht expects to be un /usr/share/texmf/bin/t4ht (FS#27251)
  sed -i s/SELFAUTOPARENT/TEXMFROOT/ texk/tex4htk/t4ht.c
# remove spurious ptex "guessed encoding" print

  # the relver is the unix timestamp, eg. 1742119845
  # take digits 3-5, so '421'.
  local rv=$(cat libs/luajit/LuaJIT-src/.relver | awk '{print substr($0,3,3)}')
  echo "${rv}" > libs/luajit/LuaJIT-src/.relver

  # god fucking dammit
  local f; for f in libs/luajit/{version.ac,configure,native/configure}; do
    sed -i -e "s/\b81742\b/${rv}/g" "$f"
  done

  # yeet the .git folder to force luajit to use the relver file.
  rm -rf .git
  patch -p1 -i ../ptex-debug-print.patch
# Fix LUA_ROOT define
  patch -p1 -i ../lua-root.patch
}

build() {
  cd texlive-source

  mkdir -p builddir
  cd builddir
  CFLAGS=${CFLAGS/FORTIFY_SOURCE=3/FORTIFY_SOURCE=2} # https://gitlab.archlinux.org/archlinux/packaging/packages/texlive-bin/-/issues/3 
  CFLAGS+=" -Wno-incompatible-pointer-types -std=gnu17" \
  ax_cv_c_float_words_bigendian=no \
  ../configure --prefix=${pacman_root}/usr -C \
    --sysconfdir=${pacman_root}/etc \
    --datarootdir=${pacman_root}/usr/share \
    --datadir=${pacman_root}/usr/share \
    --mandir=${pacman_root}/usr/share/man \
    --disable-native-texlive-build \
    --disable-multiplatform \
    --disable-dialog \
    --disable-psutils \
    --disable-t1utils \
    --disable-dvisvgm \
    --disable-xz \
    --enable-shared \
    --disable-static \
    --with-system-zlib \
    --with-system-zziplib \
    --with-system-pnglib \
    --with-system-ncurses \
    --with-system-t1lib \
    --with-system-gd \
    --with-system-freetype2 \
    --with-system-pixman \
    --with-system-cairo \
    --with-system-harfbuzz \
    --with-system-graphite \
    --with-system-icu \
    --with-system-gmp \
    --with-system-mpfr \
    --with-system-potrace \
    --with-system-libpaper \
    --with-freetype2-libdir=${pacman_root}/usr/lib \
    --with-freetype2-include=${pacman_root}/usr/include/freetype2 \
    --with-xdvi-x-toolkit=xaw \
    --disable-dump-share \
    --with-clisp-runtime=default \
    --enable-xindy \
    --disable-xindy-rules \
    --disable-xindy-docs
  # prevent excessive overlinking due to libtool
  # sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_libsynctex() {
  pkgdesc='Library for synchronization between TeX files and resulting file'
  depends=(glibc zlib)
  provides=(libsynctex.so)

  cd texlive-source/builddir
  make -C texk/web2c DESTDIR="$pkgdir" \
    install-data-am install-libLTLIBRARIES

# remove docs included in texlive-doc
  rm -r "$pkgdir"/${pacman_root}/usr/share/man
}

package_texlive-bin() {
  pkgdesc='TeX Live binaries'
  depends=(bash
           cairo
           ffcall
           fontconfig
           freetype2
           gcc-libs
           gd
           glibc
           gmp
           graphite
           harfbuzz
           icu
           libpaper
           libpng
           libsigsegv
           libsynctex
           libunistring
           libx11
           libxaw
           libxcrypt
           libxmu
           libxpm
           libxt
           mpfr
           ncurses
           perl
           pixman
           potrace
           readline
           zlib
           zziplib)
  provides=(lcdf-typetools
            libptexenc.so
            libtexlua53.so
            libtexluajit.so
            kpathsea
            xindy)
  optdepends=('psutils: to manipulate the output of dvips')

  # kind of a hack
  ignore_broken_dylibs=(${pacman_root#/}/usr/bin/synctex)

  cd texlive-source
# fixes for xindy
  find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/${pacman_root}/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \;

  cd builddir
  make DESTDIR="$pkgdir" texmf="$pkgdir"/${pacman_root}/usr/share/texmf install
  DYLD_FALLBACK_LIBRARY_PATH="$pkgdir"/${pacman_root}/usr/lib/ \
  make DESTDIR="$pkgdir" texlinks

# remove stuff included in texlive-texmf
  rm -r "$pkgdir"/${pacman_root}/usr/share/texmf-dist

# remove docs included in texlive-doc
  rm -r "$pkgdir"/${pacman_root}/usr/share/{info,man}

# remove links to scripts
  for _link in $(ls "$pkgdir"/${pacman_root}/usr/bin); do
    [[ $(stat -f '%Y' "$pkgdir"/${pacman_root}/usr/bin/$_link) == */scripts/* ]] && _rmlinks+="$pkgdir/${pacman_root}/usr/bin/$_link "
  done
  rm $_rmlinks

# remove libsynctex
  rm "$pkgdir"/${pacman_root}/usr/include/synctex/*
  rm "$pkgdir"/${pacman_root}/usr/lib/libsynctex.*
  rm "$pkgdir"/${pacman_root}/usr/lib/pkgconfig/synctex.pc
}

# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

pkgname=jbigkit
pkgver=2.1
pkgrel=6
pkgdesc="Data compression library/utilities for bi-level high-resolution images"
arch=('x86_64' 'arm64')
url="https://www.cl.cam.ac.uk/~mgk25/jbigkit/"
license=('GPL')
depends=()
options=('staticlibs')
source=(https://www.cl.cam.ac.uk/~mgk25/download/jbigkit-$pkgver.tar.gz)
sha256sums=('de7106b6bfaf495d6865c7dd7ac6ca1381bd12e0d81405ea81e7f2167263d932')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  unset CFLAGS CPPFLAGS LDFLAGS
  [ "$CARCH" == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"
  make CFLAGS="$CFLAGS"
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -D -m644 libjbig/libjbig.a "$pkgdir"/${pacman_root}/usr/lib/libjbig.a
  install -D -m644 libjbig/libjbig85.a "$pkgdir"/${pacman_root}/usr/lib/libjbig85.a
  install -D -m644 libjbig/jbig.h "$pkgdir"/${pacman_root}/usr/include/jbig.h
  install -D -m644 libjbig/jbig_ar.h "$pkgdir"/${pacman_root}/usr/include/jbig_ar.h
  install -D -m644 libjbig/jbig85.h "$pkgdir"/${pacman_root}/usr/include/jbig85.h

  install -d -m755 "$pkgdir"/${pacman_root}/usr/share/man/man1
#  install -d -m755 "$pkgdir"/${pacman_root}/usr/share/man/man5
  install -m644 pbmtools/*.1* "$pkgdir"/${pacman_root}/usr/share/man/man1
#  install -m644 pbmtools/*.5* "$pkgdir"/${pacman_root}/usr/share/man/man5

  install -D -m755 pbmtools/jbgtopbm "$pkgdir"/${pacman_root}/usr/bin/jbgtopbm
  install -D -m755 pbmtools/pbmtojbg "$pkgdir"/${pacman_root}/usr/bin/pbmtojbg
  install -D -m755 pbmtools/jbgtopbm85 "$pkgdir"/${pacman_root}/usr/bin/jbgtopbm85
  install -D -m755 pbmtools/pbmtojbg85 "$pkgdir"/${pacman_root}/usr/bin/pbmtojbg85

  rm -f "$pkgdir"/${pacman_root}/usr/share/man/man5/p{b,g}m.5*
}

# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=wasi-libc
pkgver=0+410+574b88da
_commit=574b88da481569b65a237cb80daf9a2d5aeaf82d # tags/wasi-sdk-25
pkgrel=2
pkgrel+=".1"
pkgdesc='WASI libc implementation for WebAssembly'
url='https://github.com/WebAssembly/wasi-libc'
arch=('any')
license=('Apache-2.0 WITH LLVM-exception AND Apache-2.0 AND MIT')
makedepends=(
  'clang'
  'git'
  'llvm'
)
options=('staticlibs')
source=("git+https://github.com/WebAssembly/wasi-libc.git#commit=${_commit}")
b2sums=('SKIP')


pkgver() {
  cd ${pkgname}
  printf "0+%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  local target= make_options=(
    WASM_CC=${pacman_root}/usr/bin/clang
    WASM_AR=${pacman_root}/usr/bin/llvm-ar
    WASM_NM=${pacman_root}/usr/bin/llvm-nm
  )
  local -A targets=(
    wasm32-wasi           ''
    wasm32-wasip1         ''
    wasm32-wasip1-threads 'THREAD_MODEL=posix'
    wasm32-wasip2         'WASI_SNAPSHOT=p2'
  )

  cd ${pkgname}

  for target in "${!targets[@]}"; do
    make "${make_options[@]}" TARGET_TRIPLE="$target" ${targets[$target]}
  done
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}"/${pacman_root}/usr/share
  cp -a sysroot "${pkgdir}"/${pacman_root}/usr/share/wasi-sysroot
  install -Dm644 LICENSE* -t "${pkgdir}"/${pacman_root}/usr/share/licenses/${pkgname}

}

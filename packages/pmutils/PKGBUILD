# Maintainer: zhiayang <zhiayang@nowhere>

pkgname=pmutils
pkgver=0.5.1
pkgrel=3
pkgdesc="Repository management scripts for macos-pacman"
url="https://github.com/macos-pacman/pmutils"
license=('Apache')
arch=('any')
pkgrel+=".1"
depends=('sshpass'
         'pyalpm'
         'python'
         'python-requests'
         'python-aiohttp'
         'python-tqdm'
         'python-click'
         'python-zstandard'
         )
makedepends=('python-build' 'python-installer' 'python-setuptools')
_commit='c5383de9b42502098cc5218b766d0b2ed374be9a'
source=("git+https://github.com/macos-pacman/pmutils.git#commit=${_commit}")
sha512sums=('SKIP')

build() {
  cd pmutils
  find . -iname "*.whl" -print -delete
  python -m build --wheel --no-isolation
}

package() {
  cd pmutils
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  # generate completions
  local _sh; for _sh in "zsh" "bash" "fish"; do
    PYTHONPATH="$pkgdir/$site_packages:$PYTHONPATH" \
    _PM_COMPLETE="${_sh}_source" "$pkgdir/${pacman_root}/usr/bin/pm" > "./pm.$_sh"
  done

  install -Dm644 pm.bash "$pkgdir"/${pacman_root}/usr/share/bash-completion/completions/pm
  install -Dm644 pm.fish "$pkgdir"/${pacman_root}/usr/share/fish/vendor_completions.d/pm.fish
  install -Dm644 pm.zsh "$pkgdir"/${pacman_root}/usr/share/zsh/site-functions/_pm

  install -Dm644 LICENSE "$pkgdir"/${pacman_root}/usr/share/licenses/pmutils/
}

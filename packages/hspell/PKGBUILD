# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=hspell
pkgname=('hspell' 'hunspell-he')
pkgver=1.4
pkgrel=4
arch=('x86_64' 'arm64')
license=('GPL')
url="http://www.ivrix.org.il/projects/spell-checker/"
makedepends=('glibc' 'zlib' 'perl' 'hunspell' 'gawk' 'autoconf') # qt6-webengine
options=('!makeflags')
source=(http://hspell.ivrix.org.il/${pkgname}-${pkgver}.tar.gz{,.sig}
        'fix-macos-compile.patch::https://raw.githubusercontent.com/Homebrew/formula-patches/85fa66a9/hspell/1.3.patch')
sha256sums=('7310f5d58740d21d6d215c1179658602ef7da97a816bc1497c8764be97aabea3'
            'SKIP'
            '63cc1bc753b1062d1144dcdd959a0a8f712b8872dce89e54ddff2d24f2ca2065')
validpgpkeys=('996512CD85DC510BD45EAD46415E26D84D4E37DB')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i ../fix-macos-compile.patch
}

build() {
  cd ${pkgname}-${pkgver}
  export PERLLIB="$PWD"

  autoconf
  ./configure --prefix=${pacman_root}/usr --mandir=${pacman_root}/usr/share/man \
      --enable-linginfo --enable-fatverb --enable-shared

  make
  make hunspell
}

package_hspell() {

  pkgdesc="Hebrew spell-checker"
  depends=('glibc' 'zlib' 'perl')

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/${pacman_root}/usr/lib/libhspell.a

  local _f
  for _f in ${pkgdir}/${pacman_root}/usr/{bin/hspell,lib/libhspell.dylib}; do
    fix_rpath "${pacman_root}" "${pkgdir}" "${_f}"
  done
}

package_hunspell-he() {

  pkgdesc="Hebrew hunspell dictionary"

  cd ${pkgbase}-${pkgver}

  install -dm755 "$pkgdir"/${pacman_root}/usr/share/hunspell
  install -m644 he.dic "$pkgdir"/${pacman_root}/usr/share/hunspell/he_IL.dic
  install -m644 he.aff "$pkgdir"/${pacman_root}/usr/share/hunspell/he_IL.aff

  # the symlinks
  install -dm755 "$pkgdir"/${pacman_root}/usr/share/myspell/dicts
  pushd "$pkgdir"/${pacman_root}/usr/share/myspell/dicts
    for file in "$pkgdir"/${pacman_root}/usr/share/hunspell/*; do
      ln -sv ${pacman_root}/usr/share/hunspell/$(basename $file) .
    done
  popd

# don't.

# Install webengine dictionaries
#  install -d "$pkgdir"/${pacman_root}/usr/share/qt{,6}/qtwebengine_dictionaries/
#  for _file in "$pkgdir"/${pacman_root}/usr/share/hunspell/*.dic; do
#  _filename=$(basename $_file)
#    ${pacman_root}/usr/lib/qt6/qwebengine_convert_dict $_file "$pkgdir"/${pacman_root}/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
#  ln -rs "$pkgdir"/${pacman_root}/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic} "$pkgdir"/${pacman_root}/usr/share/qt/qtwebengine_dictionaries/
#  done
}

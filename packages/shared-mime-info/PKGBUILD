# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=shared-mime-info
_commit=a2ffb2810ebb387182612e3503dfe29455d4536d # = master 2022-12-15
pkgver=2.2+13+ga2ffb28
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64' 'arm64')
license=('GPL2')
depends=('libxml2' 'glib2')
makedepends=('gettext' 'xmlto' 'docbook-xsl' 'git' 'meson')
install=shared-mime-info.install
# https://gitlab.freedesktop.org/xdg/shared-mime-info
url="https://www.freedesktop.org/wiki/Specifications/shared-mime-info-spec/"
source=("git+https://gitlab.freedesktop.org/xdg/shared-mime-info.git#commit=$_commit"
        # to run the testsuite
        "git+https://gitlab.freedesktop.org/xdg/xdgmime.git#commit=92f6a09fda2b23c2ab95cede8eb0612ca96bd0f7" # master 2022-01-19
        update-mime-database.hook.in
        shared-mime-info.install.in)
sha256sums=('SKIP'
            'SKIP'
            '3079ffb5c5aadf433fe792d5e441fbf8c136728ae9281de9b6c9e6b0511abe34'
            '8da2f8483bc7846e3bd7c3e091a7317fdc5c67c11838be6f342131d9e272bd52')
options=(!makeflags)

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^Release.//;s/\([^-]*-g\)/\1/;s/-/+/g;'
}

prepare() {
  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" -e "s|%%PACMAN_ROOT_REL%%|${pacman_root#/}|g" \
    shared-mime-info.install.in > ../shared-mime-info.install

  # build xdgmime from source to run the testsuite - check HACKING.md
  CFLAGS= make -C xdgmime
}

build() {
  export GCP_PATH="/usr/bin/cp"
  export CFLAGS+=" -UHAVE_FDATASYNC "
  arch-meson ${pkgname} build \
    -D update-mimedb=false \
    -D xdgmime-path=${srcdir}/xdgmime

  # Print config
  meson configure build
  # compile it
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  # use a prefix to run it early enough - other hooks may depend on it - FS#72858

  sed -e "s|%%PACMAN_ROOT%%|${pacman_root}|g" -e "s|%%PACMAN_ROOT_REL%%|${pacman_root#/}|g" update-mime-database.hook.in \
    | install -Dm644 /dev/stdin "$pkgdir"/${pacman_root}/usr/share/libalpm/hooks/30-update-mime-database.hook
}




